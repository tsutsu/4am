<!doctype html>
<html><head>
<title>Microzine 2</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
</head><body>

<p>Previous: <a rel="previous" href="output/466-the-astronomy-disk.html">The Astronomy Disk</a></p>
<section>

    <p class="seen">Both sides are bootable to the main menu. I'll start with side A.</p>
</section>

<section>
  <h2>Chapter 0</h2>
  <h3>In Which Various Automated Tools Fail In Interesting Ways</h3>

    <p class="seen">What does the boot look and sound like?</p>
    <p class="seen">1. immediate blank screen</p>
    <p class="seen">2. several sequential track reads</p>
    <p class="seen">3. track seek (maybe to T11?)</p>
    <p>4. HGR-style screen clear</p>
    <p class="seen">5. more disk activity (back and forth like file access)</p>
    <p class="seen">6. title screen</p>
    <p class="seen">Does it access the disk after boot?</p>
    <p class="seen">Yes, repeatedly.</p>
    <p class="seen">Does it have an option to read, write, or format user-supplied data disks?</p>
    <p class="seen">Yes.</p>
    <p class="seen">COPYA</p>
    <p class="seen">immediate disk read error</p>
    <p class="seen">Locksmith Fast Disk Backup</p>
    <p>errors on tracks $00-$04, but $05-$22 copy OK (of course the copy does not boot)</p>
    <p class="seen">EDD 4 bit copy (no sync, no count)</p>
    <p class="seen">no read errors, but copy hangs after reading one track</p>
    <p class="seen">Copy ][+ nibble editor</p>
    <p class="seen">T00 -> standard prologues, modified epilogues (FF FF EB) T01 -> corrupted address fields that claim to be track $00 T02..T03 -> not full tracks? looks like they have some standard-ish sectors, but not 16 per track (also corrupted address fields) T04..T22 -> standard prologues, modified epilogues (FF FF EB), standard address fields</p>
    <div class="terminal_output">
      <code>   COPY ][ PLUS BIT COPY PROGRAM 8.4
(C) 1982-9 CENTRAL POINT SOFTWARE, INC.
---------------------------------------</code>
      <code>TRACK: 01  START: 2735  LENGTH: 185A
       ^^</code>
    </div>
    <div class="curses">
      <code>2710: FF FF FF FF FF FF FF FF   VIEW
2718: FF FF FF FF FF FF FF FF
2720: FF FF FF FF FF FF FF FF
2728: FF FF FF FF FF FF FF FF
2730: FF FF FF FF FF D5 AA 96  <-2735
                     ^^^^^^^^
                 address prologue</code>
      <code>2738: AA AA AA AA AA AA AA AA
      ^^^^^ ^^^^^ ^^^^^ ^^^^^
      V000   T00   S00  chksm</code>
      <code>2740: FF FF FF FF FC FF FF FF
      ^^^^^^^^
  address epilogue</code>
      <code>2748: FF D5 AA AD F2 FA D7 D7
         ^^^^^^^^
      data prologue</code>
    </div>
    <div class="terminal_output">
      <code>2750: A6 BE FE F7 FB EC 97 B9</code>
      <code>---------------------------------------</code>
      <code>  A  TO ANALYZE DATA  ESC TO QUIT</code>
      <code>  ?  FOR HELP SCREEN  /  CHANGE PARMS</code>
      <code>  Q  FOR NEXT TRACK   SPACE TO RE-READ</code>
    </div>
    <p class="seen">The disk is lying to me. The address field claims to be track $00, but it's really track $01. Bad disk! Stop lying!</p>
    <p class="seen">Disk Fixer</p>
    <p>["O" -> "Input/Output Control"] set Address Epilogue to "FF FF EB" set Data Epilogue to "FF FF EB" T00 readable T01..T04 unreadable (no option to ignore the corrupted address field) T05..T22 readable T11 looks like DOS 3.3 catalog</p>
    <p class="seen">Copy ][+ sector editor</p>
    <p>["P" -> "Sector Editor Patcher"] set type to "CUSTOM" set Address Epilogue to "FF FF" set Data Epilogue to "FF FF EB" T00 readable</p>
    <div class="curses_input">
      <code>  ["P" -> "Sector Editor Patcher"]
    set CHECK TRACK to "NO"
  T01 readable!
  only parts of T03 and T04 readable:
    T03: S03,04,05,06,07,0A,0B,0C,0D,0E
    T04: S01,02,04,08,09,0C,0F</code>
    </div>
    <p class="seen">Why didn't COPYA work?</p>
    <p>modified epilogue bytes on track $00</p>
    <p class="seen">Why didn't Locksmith FDB work?</p>
    <p class="seen">ditto</p>
    <p class="seen">Why didn't my EDD copy work?</p>
    <p>I don't know. Lots of weirdness going on in tracks $01-$04, though.</p>
    <p class="seen">Next steps:</p>
    <p class="seen">1. Trace the boot</p>
    <p class="seen">2. ???</p>
</section>

<section>
  <h2>Chapter 1</h2>
  <h3>Boot Trace and Chill</h3>

    <div class="curses_input">
      <code>[S6,D1=original disk]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
CAPTURING BOOT0
...reboots slot 6...
...reboots slot 5...
SAVING BOOT0</code>
      <code>]CALL -151</code>
      <code>*800<2800.28FFM</code>
      <code>*801L</code>
    </div>
    <div class="listing">
      <p class="comment">set reset vector</p>
      <code>0801-   8A          TXA
0802-   4A          LSR
0803-   4A          LSR
0804-   4A          LSR
0805-   4A          LSR
0806-   09 C0       ORA   #$C0
0808-   85 3F       STA   $3F
080A-   8D F3 03    STA   $03F3
080D-   49 A5       EOR   #$A5
080F-   8D F4 03    STA   $03F4
0812-   A9 00       LDA   #$00
0814-   8D F2 03    STA   $03F2</code>
      <p class="comment">hmm</p>
      <code>0817-   A9 04       LDA   #$04
0819-   48          PHA</code>
      <p class="comment">machine initialization (memory banks, TEXT, IN#0, PR#0, &c.)</p>
      <code>081A-   8D 81 C0    STA   $C081
081D-   20 2F FB    JSR   $FB2F
0820-   8D 52 C0    STA   $C052
0823-   20 89 FE    JSR   $FE89
0826-   20 93 FE    JSR   $FE93</code>
      <p class="comment">clear hi-res screen 1</p>
      <code>0829-   A2 20       LDX   #$20
082B-   A0 00       LDY   #$00
082D-   84 06       STY   $06
082F-   A9 20       LDA   #$20
0831-   85 07       STA   $07
0833-   98          TYA
0834-   91 06       STA   ($06),Y
0836-   C8          INY
0837-   D0 FB       BNE   $0834
0839-   E6 07       INC   $07
083B-   CA          DEX
083C-   D0 F6       BNE   $0834</code>
      <p class="comment">switch to hi-res screen 1 (blank)</p>
      <code>083E-   8D 57 C0    STA   $C057
0841-   8D 50 C0    STA   $C050
0844-   8D 54 C0    STA   $C054
0847-   8D 52 C0    STA   $C052</code>
      <p class="comment">set up ($3E) vector to point to the sector read routine in the disk controller ROM</p>
      <code>084A-   A9 5C       LDA   #$5C
084C-   85 3E       STA   $3E</code>
      <p class="comment">the disk controller ROM always exits via $0801, so set that to an RTS so we can JSR and not have to set up a loop</p>
      <code>084E-   A9 60       LDA   #$60
0850-   8D 01 08    STA   $0801</code>
      <p class="comment">hmm</p>
      <code>0853-   A9 72       LDA   #$72
0855-   48          PHA</code>
    </div>
    <p class="seen">OK, we've now pushed $04/$72 on the stack. That's probably important.</p>
    <div class="listing">
      <p class="comment">multi-sector read</p>
      <p class="comment">Y = start logical sector ($01)</p>
      <p class="comment">X = end logical sector ($03)</p>
      <p class="comment">A = start address high byte ($1D)</p>
      <code>0856-   A0 00       LDY   #$00
0858-   84 FC       STY   $FC
085A-   C8          INY
085B-   A9 9D       LDA   #$1D
085D-   A2 05       LDX   #$03</code>
      <p class="comment">multi-sector read routine</p>
      <code>085F-   20 6C 08    JSR   $086C</code>
      <p class="comment">another sector read, 3 more sectors ($04..$06) into $9D00..$9DFF</p>
      <code>0862-   A9 9D       LDA   #$9D
0864-   A2 06       LDX   #$06
0866-   20 6C 08    JSR   $086C</code>
      <p class="comment">another sector read, this time just one sector, into $0400 (X is already less than Y on entry, so loop will exit after one read)</p>
      <code>0869-   A9 04       LDA   #$04
086B-   AA          TAX</code>
      <p class="comment">falls through to multi-sector read entry point (was also called earlier from $085F and $0866)</p>
      <code>086C-   85 27       STA   $27
086E-   E8          INX
086F-   86 49       STX   $49
0871-   84 F9       STY   $F9</code>
      <p class="comment">map logical into physical sector and store it in zero page where the disk controller ROM will look for it</p>
      <code>0873-   B9 8A 08    LDA   $088A,Y
0876-   85 3D       STA   $3D</code>
      <p class="comment">read sector via disk controller ROM</p>
      <code>0878-   20 85 08    JSR   $0885</code>
      <p class="comment">loop until done</p>
      <code>087B-   A4 F9       LDY   $F9
087D-   C8          INY
087E-   C4 49       CPY   $49
0880-   90 EF       BCC   $0871
0882-   A5 27       LDA   $27
0884-   60          RTS</code>
      <code>0885-   A6 2B       LDX   $2B
0887-   6C 3E 00    JMP   ($003E)</code>
      <code>
088A-  [00 03 05 07 09 0B 0D 0F]
0892-  [02 04 06 08 0A 0C 0E 01]</code>
    </div>
    <p class="seen">That's it. Flexible but compact.</p>
    <p>It's a weird combination of reads, though. 3 pages at $1D00. 3 pages at $9D00. 1 page at $0400 (part of the text page, but it's hidden during boot because we cleared the entire hi-res graphics page and showed that instead).</p>
    <p class="seen">Of course, we manually pushed $04/$72 on the stack earlier, so once we fall through to the sector read routine and it hits the RTS at $088F, it will "return" to $0472 + 1 = $0473.</p>
    <p class="seen">Let's interrupt the boot before it gets there.</p>
</section>

<section>
  <h2>Chapter 2</h2>
  <h3>In Which Things Get Brilliantly Weird</h3>

    <div class="line_editing">
      <code>*9600<C600.C6FFM</code>
    </div>
    <div class="listing">
      <p class="comment">set up callback by changing the two bytes that are pushed to the stack</p>
      <code>96F8-   A9 97       LDA   #$97
96FA-   8D 18 08    STA   $0818
96FD-   A9 04       LDA   #$04
96FF-   8D 54 08    STA   $0854</code>
      <p class="comment">start the boot</p>
      <code>9702-   4C 01 08    JMP   $0801</code>
      <p class="comment">callback is here -- copy $0400 up to higher memory so it survives a reboot</p>
      <code>9705-   A0 00       LDY   #$00
9707-   B9 00 04    LDA   $0400,Y
970A-   99 00 24    STA   $2400,Y
970D-   C8          INY
970E-   D0 F7       BNE   $9707</code>
      <p class="comment">turn off slot 6 drive motor</p>
      <code>9710-   AD E8 C0    LDA   $C0E8</code>
      <p class="comment">reboot to my work disk</p>
      <code>9713-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE,A$9600,L$116</code>
      <code>*BRUN TRACE
...reboots slot 6...
...reboots slot 5...</code>
      <code>]BSAVE BOOT1 0400-04FF,A$2400,L$100
]CALL -151</code>
    </div>
    <p>The entry point was $0473, so let's start there. I'll have to leave the code at $2400. Relative branches will look correct, but absolute addresses in $04xx will be +$2000.</p>
    <div class="line_editing">
      <code>*2473L</code>
    </div>
    <div class="listing">
      <p class="comment">not sure what $4A is for yet</p>
      <code>2473-   46 4A       LSR   $4A</code>
      <code>2475-   20 C3 04    JSR   $04C3</code>
    </div>
    <div class="line_editing">
      <code>*24C3L</code>
    </div>
    <div class="listing">
      <code>24C3-   A9 A0       LDA   #$A0
24C5-   4C 0B 04    JMP   $040B</code>
    </div>
    <div class="line_editing">
      <code>*240BL</code>
    </div>
    <div class="listing">
      <code>240B-   20 0E 04    JSR   $040E</code>
    </div>
    <div class="line_editing">
      <code>*240EL</code>
    </div>
    <div class="listing">
      <code>240E-   20 33 04    JSR   $0433</code>
    </div>
    <div class="line_editing">
      <code>*2433L</code>
    </div>
    <div class="listing">
      <p class="comment">call the following line (then fall through and do it again)</p>
      <code>2433-   20 36 04    JSR   $0436</code>
      <p class="comment">save A and Y</p>
      <code>2436-   48          PHA
2437-   98          TYA
2438-   48          PHA</code>
      <p class="comment">low-level disk stuff (see below)</p>
      <code>2439-   A5 FC       LDA   $FC
243B-   85 FD       STA   $FD
243D-   E6 FC       INC   $FC
243F-   A5 FC       LDA   $FC
2441-   29 03       AND   #$03
2443-   0A          ASL
2444-   05 2B       ORA   $2B
2446-   A8          TAY
2447-   B9 81 C0    LDA   $C081,Y</code>
      <p class="comment">wait loop</p>
      <code>244A-   A9 30       LDA   #$30
244C-   20 A8 FC    JSR   $FCA8</code>
      <p class="comment">more low-level disk stuff</p>
      <code>244F-   A5 FD       LDA   $FD
2451-   29 03       AND   #$03
2453-   0A          ASL
2454-   05 2B       ORA   $2B
2456-   A8          TAY
2457-   B9 80 C0    LDA   $C080,Y</code>
      <p class="comment">more waiting</p>
      <code>245A-   A9 30       LDA   #$30
245C-   20 A8 FC    JSR   $FCA8</code>
      <p class="comment">restore A and Y on the way out</p>
      <code>245F-   68          PLA
2460-   A8          TAY
2461-   68          PLA
2462-   60          RTS</code>
    </div>
    <p class="seen">This is a very clever and compact way to advance the drive head to the next track. Normally DOS 3.3 keeps track of this and has a (much more complicated) routine to move the head back and forth as needed. But this loader only needs to move it forward, so the entire process collapses to this:</p>
    <p class="seen">1. Set up the Y register to be a slot</p>
    <p class="seen">number (x16) plus the appropriate phase (0-3, depending on which track the drive head is on)</p>
    <p class="seen">2. LDA $C081,Y to turn on the</p>
    <p class="seen">appropriate stepper motor</p>
    <p class="seen">3. Wait exactly the right amount of</p>
    <p class="seen">time (as measured in CPU cycles)</p>
    <p class="seen">4. LDA $C080,Y to turn off the</p>
    <p class="seen">appropriate stepper motor</p>
    <p class="seen">5. Wait the right amount of time again</p>
    <p class="seen">...which is exactly what this routine at $0436 is doing. But that only gets us halfway there -- literally, it only moves the drive head by half a track.</p>
    <p class="seen">But! Since $0433 "falls through" to $0436, it ends up doing this twice. Two half tracks equal one whole track, so calling the routine at $0433 will move the drive head to the next whole track.</p>
    <p class="seen">(By the way, this is why it initialized zero page $FC to $00 at $0858. That's the "current" track where the drive head is at boot; it gets updated when the drive head advances.)</p>
    <p class="seen">Everything I know about low-level disk stepping, I learned from this excellent</p>
    <p class="seen">Usenet post: macgui.com/usenet/?group=1&id=31160</p>
    <p class="seen">Continuing...</p>
    <div class="listing">
      <code>2411-   A2 0F       LDX   #$0F
2413-   A0 00       LDY   #$00</code>
      <p class="comment">store A in zero page $27, used by the disk controller ROM routine as the target page to store sectors read from disk</p>
      <code>2415-   85 27       STA   $27</code>
      <p class="comment">X is the final sector to read</p>
      <code>2417-   E8          INX
2418-   86 49       STX   $49</code>
      <p class="comment">Y is the current sector to read (starting with whatever was passed in and incrementing until it equals the value passed in the X register)</p>
      <code>241A-   84 F9       STY   $F9
241C-   98          TYA</code>
      <p class="comment">But wait, there's more! Based on the high bit of zero page $4A, Y is either a logical sector (the map of logical->physical sectors is at $0263) or a physical sector</p>
      <code>241D-   24 4A       BIT   $4A
241F-   30 03       BMI   $2424
2421-   B9 63 04    LDA   $0463,Y</code>
      <p class="comment">store physical sector in $3D (again, used by the disk controller ROM)</p>
      <code>2424-   85 3D       STA   $3D</code>
      <p class="comment">read sector by jumping to ($003E), which points to $Cx5C (e.g. $C65C if booting from slot 6) and exit via $0801, which is an RTS by now, so this just continues to the next line</p>
      <code>2426-   20 00 04    JSR   $0400</code>
      <p class="comment">increment sector index</p>
      <code>2429-   A4 F9       LDY   $F9
242B-   C8          INY</code>
      <p class="comment">are there more sectors to read?</p>
      <code>242C-   C4 49       CPY   $49</code>
      <p class="comment">yes, branch back and repeat</p>
      <code>242E-   90 EA       BCC   $241A</code>
      <p class="comment">no, exit with last page (+1) in A (disk controller ROM increments this after storing sector data, so on exit this will be the first page that was NOT filled with data in this loop)</p>
      <code>2430-   A5 27       LDA   $27
2432-   60          RTS</code>
    </div>
    <p class="seen">To sum up:</p>
    <p class="seen">These two lines of code...</p>
    <div class="terminal_output">
      <code>||  24C3-   A9 A0       LDA   #$A0   ||
||  24C5-   4C 0B 04    JMP   $040B  ||</code>
    </div>
    <p>advanced the drive head from track $00 to track $01, read the entire track into $A000..$AFFF (despite the fact that every sector's address field was corrupted and claimed to be track $00), then fall through from $040B to $040E and do it all over again, but moving to track $02 and reading into $B000..$BFFF instead.</p>
    <p class="seen">Beautiful.</p>
</section>

<section>
  <h2>Chapter 3</h2>
  <h3>Every Byte Is Sacred, Every Byte Is Great, If A Byte Gets Wasted, Woz Gets Quite Irate</h3>

    <p>Continuing from $0478...</p>
    <div class="listing">
      <code>2478-   20 00 BB    JSR   $BB00</code>
    </div>
    <div class="line_editing">
      <code>*9600<C600.C6FFM</code>
    </div>
    <div class="listing">
      <p class="comment">set up callback #1 by changing the bytes that are pushed to the stack</p>
      <code>96F8-   A9 97       LDA   #$97
96FA-   8D 13 08    STA   $0813
96FD-   A9 04       LDA   #$04
96FF-   8D 54 08    STA   $0854</code>
      <p class="comment">start the boot</p>
      <code>9702-   4C 01 08    JMP   $0801</code>
      <p class="comment">callback #1 is here -- set up callback #2 before calling mystery routine at $BB00</p>
      <code>9705-   A9 4C       LDA   #$4C
9707-   8D 78 04    STA   $0478
970A-   A9 17       LDA   #$17
970C-   8D 79 04    STA   $0479
970F-   A9 97       LDA   #$97
9711-   8D 7A 04    STA   $047A</code>
      <p class="comment">continue the boot</p>
      <code>9714-   4C 73 04    JMP   $0473</code>
      <p class="comment">callback #2 is here -- turn off drive motor and break</p>
      <code>9717-   AD E8 C0    LDA   $C0E8
971A-   4C 59 FF    JMP   $FF59</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE2,A$9600,L$11D
*9600G
...reboots slot 6...
<beep></code>
      <code>*BB00L</code>
    </div>
    <div class="listing">
      <p class="comment">this checks whether Applesoft is in ROM and displays an error message if it's not</p>
      <code>BB00-   AD 81 C0    LDA   $C081
BB03-   AD B3 FB    LDA   $FBB3
BB06-   C9 06       CMP   #$06
BB08-   D0 03       BNE   $BB0D
BB0A-   A9 03       LDA   #$03
BB0C-   60          RTS
BB0D-   AD 83 C0    LDA   $C083
BB10-   AD 83 C0    LDA   $C083
BB13-   8D 00 E0    STA   $E000
BB16-   CD 00 E0    CMP   $E000
BB19-   D0 19       BNE   $BB34
BB1B-   2A          ROL
BB1C-   8D 00 E0    STA   $E000
BB1F-   CD 00 E0    CMP   $E000
BB22-   D0 10       BNE   $BB34
.
. &c.
.</code>
    </div>
    <p>Unrelated to copy protection. But while</p>
    <p>I'm here, I should save these two tracks worth of data, because they appear to be most of a full copy of DOS</p>
    <p>3.3.</p>
    <div class="line_editing">
      <code>*2000<A000.BFFFM
*C500G
...
]BSAVE BOOT2 A000-BFFF,A$2000,L$2000</code>
    </div>
    <p>Now then. Continuing where we left off.</p>
    <div class="line_editing">
      <code>]BLOAD BOOT1 0400-04FF,A$24000
]CALL -151</code>
      <code>*247BL</code>
    </div>
    <div class="listing">
      <code>247B-   A5 3F       LDA   $3F
247D-   8D 82 BA    STA   $BA82
2480-   20 9D 04    JSR   $049D</code>
    </div>
    <div class="line_editing">
      <code>*249DL</code>
    </div>
    <div class="listing">
      <p class="comment">advance the drive head to track $03</p>
      <code>249D-   20 33 04    JSR   $0433</code>
      <p class="comment">zero page fiddling</p>
      <code>24A0-   A9 00       LDA   #$00
24A2-   85 41       STA   $41
24A4-   38          SEC
24A5-   66 4A       ROR   $4A</code>
      <p class="comment">call the multi-sector read routine again, but this time only read 5 sectors, into $0E00..$12FF</p>
      <code>24A7-   A9 0E       LDA   #$0E
24A9-   A0 01       LDY   #$01
24AB-   A2 05       LDX   #$05
24AD-   20 15 04    JSR   $0415</code>
      <p class="comment">move the drive head one phase only, to the next HALF track</p>
      <code>24B0-   20 36 04    JSR   $0436</code>
    </div>
    <div class="curses_input">
      <code>[now on track 3.5]</code>
    </div>
    <div class="listing">
      <p class="comment">read more sectors ($06..$0A) from track 3.5</p>
      <code>24B3-   A2 0A       LDX   #$0A
24B5-   20 15 04    JSR   $0415</code>
      <p class="comment">advance another half track</p>
      <code>24B8-   20 36 04    JSR   $0436</code>
    </div>
    <div class="curses_input">
      <code>[now on track 4]</code>
    </div>
    <div class="listing">
      <p class="comment">read more sectors ($0B..$0F) from track 4</p>
      <code>24BB-   A2 0F       LDX   #$0F
24BD-   20 15 04    JSR   $0415</code>
      <p class="comment">fiddle with $4A again</p>
      <code>24C0-   46 4A       LSR   $4A
24C2-   60          RTS</code>
    </div>
    <p class="seen">So here's the deal with $4A: we initialized it at $0473 by a blind LSR, which clears the high bit. This tells the multi-sector read routine at $0415 to use logical sectors. Then we set the high bit at $04A4 with SEC + ROR, indicating we want $0415 to read physical sectors. Then we read a few sectors from track 2, a few from track</p>
    <p class="seen">2.5, and a few from track 3. Then we reset $4A with another LSR, and we're back to using logical sectors.</p>
    <p>This explains why my EDD bit copy failed. This disk is storing data on half tracks. Worse, it's storing data on *adjacent* half tracks -- a few from track 3, a few from track 3.5, and a few from track 4. Due to limitations of the Disk II drive mechanism, that would be virtually impossible for a generic bit copier to reproduce on a blank floppy disk.</p>
    <p class="seen">Every part of this code is brilliant. AND it fits in a single sector in low memory. AND it's flexible enough to read from virtually uncopyable disks.</p>
    <p>Continuing from $0483...</p>
    <div class="listing">
      <p class="comment">now put slot number (x16) into... an RWTS parameter table?!?</p>
      <code>2483-   A6 2B       LDX   $2B
2485-   8E E9 B7    STX   $B7E9</code>
      <p class="comment">set up DOS globals (tracking where the drive head is)</p>
      <code>2488-   20 8E BE    JSR   $BE8E
248B-   A5 FC       LDA   $FC
248D-   99 78 04    STA   $0478,Y
2490-   4A          LSR
2491-   8D 78 04    STA   $0478</code>
      <p class="comment">push $B7/$3A on the stack</p>
      <code>2494-   A9 B7       LDA   #$B7
2496-   48          PHA
2497-   A9 3A       LDA   #$3A
2499-   48          PHA</code>
      <p class="comment">and exit through HOME (which will wipe this loader from memory)</p>
      <code>249A-   4C 58 FC    JMP   $FC58</code>
    </div>
    <p class="seen">Execution continues at $B73B (because we just pushed $B7/$3A on the stack).</p>
</section>

<section>
  <h2>Chapter 4</h2>
  <h3>In Which We See The Light At The End Of The Tunnel, And It's A DOS-shaped RWTS Which Is A Weird Thing To See In A Tunnel, Honestly</h3>

    <p>I can interrupt the boot at $0483 to capture whatever was read from tracks $03, $03.5, and $04.</p>
    <div class="line_editing">
      <code>*9600<C600.C6FFM</code>
    </div>
    <div class="listing">
      <p class="comment">set up callback #1 after boot0 loads boot1 into $0400</p>
      <code>96F8-   A9 97       LDA   #$97
96FA-   8D 18 08    STA   $0818
96FD-   A9 04       LDA   #$04
96FF-   8D 54 08    STA   $0854</code>
      <p class="comment">start the boot</p>
      <code>9702-   4C 01 08    JMP   $0801</code>
      <p class="comment">callback #1 is here -- set up callback #2 after calling the spiral track read routine</p>
      <code>9705-   A9 4C       LDA   #$4C
9707-   8D 83 04    STA   $0483
970A-   A9 17       LDA   #$17
970C-   8D 84 04    STA   $0484
970F-   A9 97       LDA   #$97
9711-   8D 85 04    STA   $0485</code>
      <p class="comment">continue the boot</p>
      <code>9714-   4C 73 04    JMP   $0473</code>
      <p class="comment">callback #2 is here -- turn off the drive motor and break</p>
      <code>9717-   AD E8 C0    LDA   $C0E8
971A-   4C 59 FF    JMP   $FF59</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE3,A$9600,L$11D
*9600G
...reboots slot 6...
<beep></code>
      <code>*2E00<E00.1CFFM
*C500G</code>
      <code>]BSAVE BOOT2 0E00-1CFF,A$2E00,L$F00</code>
    </div>
</section>

<section>
  <h2>Chapter 5</h2>
  <h3>In Which Simplicity Is In The Eye Of The Beholder</h3>

    <p>Using Super Demuffin (with epilogues "FF FF EB"), I copied track $00 onto a freshly formatted blank disk. Using</p>
    <p>Copy II Plus, I sector copied tracks $05-$22. Now let's write everything we captured from the corrupted tracks back to tracks $01-$03.</p>
    <p>Disk layout comparison:</p>
    <p>track | old | new ------+-------------+-------------</p>
    <p>$01 | $A000-$AFFF $02 | $B000-$BFFF $03 | $0E00-$12FF | $0E00-$1DFF</p>
    <p>$03.5 | $1300-$17FF | --</p>
    <p>$04 | $1800-$1CFF | --</p>
    <p>Of course, all the tracks will now have normal address fields (no more lying about the track numbers). Also no half tracks and no spirals. Just, you know, sectors on a disk.</p>
    <div class="curses_input">
      <code>[S6,D1=demuffin'd copy (T00,T05-T22)]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...
]CALL -151</code>
      <code>*300L</code>
    </div>
    <div class="listing">
      <p class="comment">page count (decremented)</p>
      <code>0300-   A9 30       LDA   #$30
0302-   85 FF       STA   $FF</code>
      <p class="comment">logical sector (incremented)</p>
      <code>0304-   A9 00       LDA   #$00
0306-   85 FE       STA   $FE</code>
      <p class="comment">call RWTS to write sector</p>
      <code>0308-   A9 03       LDA   #$03
030A-   A0 88       LDY   #$88
030C-   20 D9 03    JSR   $03D9</code>
      <p class="comment">increment logical sector, wrap around from $0F to $00 and increment track</p>
      <code>030F-   E6 FE       INC   $FE
0311-   A4 FE       LDY   $FE
0313-   C0 10       CPY   #$10
0315-   D0 07       BNE   $031E
0317-   A0 00       LDY   #$00
0319-   84 FE       STY   $FE
031B-   EE 8C 03    INC   $038C</code>
      <p class="comment">Convert to the interleave order that this disk expects</p>
      <code>031E-   B9 40 03    LDA   $0340,Y
0321-   8D 8D 03    STA   $038D</code>
      <p class="comment">increment page to write</p>
      <code>0324-   EE 91 03    INC   $0391
0327-   C6 FF       DEC   $FF</code>
      <p class="comment">loop until done with all pages</p>
      <code>0329-   D0 DD       BNE   $0308
032B-   60          RTS</code>
    </div>
    <div class="line_editing">
      <p class="comment">sector interleave table</p>
      <code>*340.34F</code>
    </div>
    <div class="listing">
      <code>0340- 00 06 05 04 03 02 01 0F
0348- 0E 0D 0C 0B 0A 09 08 07</code>
    </div>
    <div class="line_editing">
      <p class="comment">RWTS parameter table, pre-initialized with slot 6, drive 1, track $01, sector $00, address $2000, and RWTS write command ($02)</p>
      <code>*388.397</code>
    </div>
    <div class="listing">
      <code>0388- 01 60 01 00 01 00 FB F7
0390- 00 20 00 00 02 00 00 60</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE MAKE,A$300,L$98
*BLOAD BOOT2 A000-BFFF,A$2000
*BLOAD BOOT2 0E00-1CFF,A$4100
*300G</code>
    </div>
    <p>Now I need to modify the bootloader at $0473 slightly.</p>
    <p>1. Modify the routine at $049D to read all of track $03 instead of the crazy spiral stuff.</p>
    <p>2. Modify the routine at $0433 (that advances the drive head) so it updates zero page $41 with the current track.</p>
    <p>The sector read routine at $C65C compares the track listed in the address field to zero page $41 and loops forever until it matches. $C600 initializes $41 to 0, and the original disk never updates $41, but everything works because the address fields are corrupted and all claim to be track 0.</p>
    <p>So part of it will be simpler, because we'll no longer be spiraling between tracks. But part of it will actually be more complicated because the address fields are no longer corrupted and we need to track the track number.</p>
    <p>First, a patch to $049D to load a whole track into $0D00..$1CFF. (Page $0D is blank but ignored -- the original disk only loaded $E00..$1CFF.)</p>
    <p>T00,S0F,$9D change "20 33 04 A9 00"</p>
    <p>to "A9 0D 4C 0E 04"</p>
    <div class="terminal_output">
      <code>----------- DISASSEMBLY MODE ----------
009D:A9 0D          LDA   #$0D
009F:4C 0E 04       JMP   $040E</code>
    </div>
    <p>Second, a new routine at $04A2 (now unused space) to increment the track number in zero page.</p>
    <p>T00,S0F,$A2 change "85 41 38 66 4A"</p>
    <p class="seen">to "E6 41 4C 36 04"</p>
    <div class="terminal_output">
      <code>----------- DISASSEMBLY MODE ----------
00A2:E6 41          INC   $41
00A4:4C 36 04       JMP   $0436</code>
    </div>
    <p>Third, a patch to the routine at $0433 to call the patch at $04A2 to increment the track number in zero page before advancing the drive arm.</p>
    <p>T00,S0F,$34 change "36" to "A2"</p>
    <div class="terminal_output">
      <code>----------- DISASSEMBLY MODE ----------
0033:20 A2 04       JSR   $04A2</code>
    </div>
    <div class="line_editing">
      <code>]PR#6
...works...</code>
    </div>
    <p class="seen">Side B has identical protection.</p>
    <p class="seen">Quod erat liberandum.</p>
</section>

<p>Next: <a rel="next" href="output/468-basic-building-blocks.html">BASIC Building Blocks</a></p>
</body></html>
