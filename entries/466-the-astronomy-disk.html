<!doctype html>
<html><head>
<title>The Astronomy Disk</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
<meta name="viewport" content="width=420, initial-scale=1.0, user-scalable=no">
</head><body>

<p>Previous: <a rel="previous" href="./465-the-observatory-san-inc-crack.html">the observatory (san inc crack)</a></p>
<section>
  <h2>Chapter 0</h2>
  <h3>In Which Various Automated Tools Fail In Interesting Ways</h3>

    <p class="seen">COPYA</p>
    <p class="seen">immediate disk read error</p>
    <p class="seen">Locksmith Fast Disk Backup</p>
    <p class="seen">unable to read any track</p>
    <p class="seen">EDD 4 bit copy (no sync, no count)</p>
    <p>no read errors, but the copy just grinds and crashes</p>
    <p class="seen">Copy ][+ nibble editor</p>
    <p>the nibbles are pretty noisy (the sync bytes between sectors are not a pretty pattern of $FF), but the nibble editor is pretty sure this is a 16-sector disk with modified prologues and epilogues</p>
    <div class="terminal_output">
      <code>   COPY ][ PLUS BIT COPY PROGRAM 8.4
(C) 1982-9 CENTRAL POINT SOFTWARE, INC.
---------------------------------------</code>
      <code>TRACK: 03  START: 22FF  LENGTH: 1852</code>
    </div>
    <div class="curses">
      <code>2460: D3 B3 AF CE D3 D3 D3 D3   VIEW
2468: D3 D3 D3 D3 D3 F4 96 96
2470: 96 96 ED AA EB FF FF FF
            ^^^^^^^^
         data epilogue</code>
      <code>2478: 95 FF F4 EB AF ED FF DB
2480: B7 AD BF D7 AB AA D5 AB  <-2485
                     ^^^^^^^^
                 address prologue</code>
      <code>2488: FF FE AB AB AA AB FE FE
      ^^^^^ ^^^^^ ^^^^^ ^^^^^
       vol   trk   sec  chksm</code>
      <code>2490: DE AB EE EE EE B5 CF E5
      ^^^^^
 address epilogue</code>
      <code>2498: E9 D6 AF ED FF DB AA D5
                        ^^^^^
                    data prologue</code>
    </div>
    <div class="terminal_output">
      <code>24A0: EB D9 DF AD B7 B7 ED BF
      ^^</code>
      <code>---------------------------------------</code>
      <code>  A  TO ANALYZE DATA  ESC TO QUIT</code>
      <code>  ?  FOR HELP SCREEN  /  CHANGE PARMS</code>
      <code>  Q  FOR NEXT TRACK   SPACE TO RE-READ</code>
    </div>
    <p class="seen">Disk Fixer</p>
    <p>T00,S00 is readable, but nothing else on track $00 ["O" -> "Input/Output Control"] set Address Prologue to "AA D5 AB" set Address Epilogue to "DE AB EB" set Data Prologue to "AA D5 EB" set Data Epilogue to "ED AA EB" T01+ readable! T01-T02 -> full DOS 3.3 T11 -> DOS 3.3 catalog T01,S0D -> startup program is "HELLO"</p>
    <p class="seen">Why didn't COPYA work?</p>
    <p class="seen">modified prologues (every track)</p>
    <p class="seen">Why didn't Locksmith FDB work?</p>
    <p class="seen">modified prologues (every track)</p>
    <p class="seen">Next steps:</p>
    <p>1. Trace the boot to capture the RWTS</p>
    <p class="seen">2. Advanced Demuffin to convert the disk to a standard format</p>
    <p>3. Replace tracks $00-$02 with a standard copy of DOS 3.3</p>
    <p class="seen">4. Declare victory(*)</p>
    <p class="seen">(*) take a nap</p>
</section>

<section>
  <h2>Chapter 1</h2>
  <h3>In Which We Can Do This All Day</h3>

    <div class="curses_input">
      <code>[S6,D1=original disk]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...
]CALL -151</code>
      <code>*9600<C600.C6FFM</code>
    </div>
    <div class="listing">
      <p class="comment">copy boot0 to higher memory so it survives a reboot</p>
      <code>96F8-   A0 00       LDY   #$00
96FA-   B9 00 08    LDA   $0800,Y
96FD-   99 00 28    STA   $2800,Y
9700-   C8          INY
9701-   D0 F7       BNE   $96FA</code>
      <p class="comment">turn off slot 6 drive motor</p>
      <code>9703-   AD E8 C0    LDA   $C0E8</code>
      <p class="comment">immediately reboot to my work disk</p>
      <code>9706-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE,A$9600,L$109</code>
      <code>*9600G
...reboots slot 6...
...reboots slot 5...</code>
      <code>]BSAVE BOOT0,A$2800,L$100
]CALL -151</code>
      <code>*800<2800.28FFM</code>
      <code>*800</code>
    </div>
    <div class="listing">
      <code>0800- 01</code>
    </div>
    <div class="line_editing">
      <code>*801L</code>
    </div>
    <div class="listing">
      <code>0801-   A2 00       LDX   #$00
0803-   BD 00 08    LDA   $0800,X
0806-   9D 00 02    STA   $0200,X
0809-   E8          INX
080A-   D0 F7       BNE   $0803
080C-   4C 0F 02    JMP   $020F</code>
    </div>
    <p>Hmm. Moving boot0 to the input buffer.</p>
    <p>I've seen this technique before, but I have not seen this exact code. We'll need to trace it manually.</p>
    <div class="line_editing">
      <code>*200<800.8FFM
*20FL</code>
    </div>
    <div class="listing">
      <p class="comment">save text page</p>
      <code>020F-   BD 00 04    LDA   $0400,X
0212-   9D 00 20    STA   $2000,X
0215-   BD 00 05    LDA   $0500,X
0218-   9D 00 21    STA   $2100,X
021B-   BD 00 06    LDA   $0600,X
021E-   9D 00 22    STA   $2200,X
0221-   BD 00 07    LDA   $0700,X
0224-   9D 00 23    STA   $2300,X
0227-   E8          INX
0228-   D0 E5       BNE   $020F</code>
      <p class="comment">wipe text page 2</p>
      <code>022A-   A9 A0       LDA   #$A0
022C-   9D 00 08    STA   $0800,X
022F-   9D 00 09    STA   $0900,X
0232-   9D 00 0A    STA   $0A00,X
0235-   9D 00 0B    STA   $0B00,X
0238-   E8          INX
0239-   D0 F1       BNE   $022C</code>
      <p class="comment">show text page 2 (now blank)</p>
      <code>023B-   AD 55 C0    LDA   $C055</code>
      <p class="comment">set up zero page for a PROM read of another sector</p>
      <code>023E-   A9 00       LDA   #$00
0240-   85 26       STA   $26
0242-   85 41       STA   $41
0244-   A9 0D       LDA   #$0D
0246-   85 3D       STA   $3D
0248-   A9 04       LDA   #$04
024A-   85 27       STA   $27
024C-   A6 2B       LDX   $2B</code>
      <p class="comment">but we're not actually calling PROM, we're calling our own routine that reads a single sector with modified prologues</p>
      <code>024E-   20 54 02    JSR   $0254</code>
      <p class="comment">jump to the sector we just read</p>
      <code>0251-   4C 00 04    JMP   $0400</code>
    </div>
    <p>The custom read routine starts at $254:</p>
    <div class="listing">
      <p class="comment">find "AA D5 AB" address prologue</p>
      <code>0254-   BD 8C C0    LDA   $C08C,X
0257-   10 FB       BPL   $0254
0259-   49 AA       EOR   #$AA
025B-   D0 F7       BNE   $0254
025D-   BD 8C C0    LDA   $C08C,X
0260-   10 FB       BPL   $025D
0262-   C9 D5       CMP   #$D5
0264-   D0 F3       BNE   $0259
0266-   EA          NOP
0267-   BD 8C C0    LDA   $C08C,X
026A-   10 FB       BPL   $0267
026C-   C9 AB       CMP   #$AB
026E-   F0 02       BEQ   $0272
0270-   D0 E2       BNE   $0254</code>
      <p class="comment">parse address field</p>
      <code>0272-   A0 03       LDY   #$03
0274-   85 40       STA   $40
0276-   BD 8C C0    LDA   $C08C,X
0279-   10 FB       BPL   $0276
027B-   2A          ROL
027C-   85 3C       STA   $3C
027E-   BD 8C C0    LDA   $C08C,X
0281-   10 FB       BPL   $027E
0283-   25 3C       AND   $3C
0285-   88          DEY
0286-   D0 EC       BNE   $0274</code>
      <p class="comment">match sector</p>
      <code>0288-   C5 3D       CMP   $3D
028A-   D0 C8       BNE   $0254</code>
      <p class="comment">match track</p>
      <code>028C-   A5 40       LDA   $40
028E-   C5 41       CMP   $41
0290-   D0 C2       BNE   $0254</code>
      <p class="comment">find "AA D5 EB" data prologue</p>
      <code>0292-   BD 8C C0    LDA   $C08C,X
0295-   10 FB       BPL   $0292
0297-   49 AA       EOR   #$AA
0299-   D0 F7       BNE   $0292
029B-   BD 8C C0    LDA   $C08C,X
029E-   10 FB       BPL   $029B
02A0-   C9 D5       CMP   #$D5
02A2-   D0 F3       BNE   $0297
02A4-   EA          NOP
02A5-   BD 8C C0    LDA   $C08C,X
02A8-   10 FB       BPL   $02A5
02AA-   49 EB       EOR   #$EB
02AC-   F0 02       BEQ   $02B0
02AE-   D0 E2       BNE   $0292
.
. &c. (standard 6-and-2 decoding)
.</code>
    </div>
    <p>I can interrupt the boot at $0251 (instead of jumping to $0400).</p>
    <div class="line_editing">
      <code>*9600<C600.C6FFM</code>
    </div>
    <div class="listing">
      <p class="comment">reproduce initial copy of boot0 code</p>
      <code>96F8-   A2 00       LDX   #$00
96FA-   BD 00 08    LDA   $0800,X
96FD-   9D 00 02    STA   $0200,X
9700-   E8          INX
9701-   D0 F7       BNE   $96FA</code>
      <p class="comment">set up callback instead of jumping to $0400</p>
      <code>9703-   A9 10       LDA   #$10
9705-   8D 52 02    STA   $0252
9708-   A9 97       LDA   #$97
970A-   8D 53 02    STA   $0253</code>
      <p class="comment">start the boot</p>
      <code>970D-   4C 0F 02    JMP   $020F</code>
      <p class="comment">callback is here -- copy sector we read into $0400 to the graphics page so it survives a reboot</p>
      <code>9710-   A0 00       LDY   #$00
9712-   B9 00 04    LDA   $0400,Y
9715-   99 00 24    STA   $2400,Y
9718-   C8          INY
9719-   D0 F7       BNE   $9712</code>
      <p class="comment">turn off slot 6 drive motor</p>
      <code>971B-   AD E8 C0    LDA   $C0E8</code>
      <p class="comment">reboot to my work disk</p>
      <code>971E-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE2,A$9600,L$121
*9600G
...reboots slot 6...
...reboots slot 5...</code>
      <code>]BSAVE BOOT1 0400-04FF,A$2400,L$100
]CALL -151</code>
    </div>
    <p>I'm going to leave this code at $2400, since $0400 is part of the text page.</p>
    <p>Relative branches will look correct, but absolute addresses will be +$2000.</p>
    <div class="line_editing">
      <code>*2400L</code>
    </div>
    <div class="listing">
      <p class="comment">set up for another sector read</p>
      <code>2400-   A9 00       LDA   #$00
2402-   85 26       STA   $26
2404-   85 41       STA   $41
2406-   A9 0B       LDA   #$0B
2408-   85 3D       STA   $3D
240A-   A9 05       LDA   #$05
240C-   85 27       STA   $27
240E-   A6 2B       LDX   $2B</code>
      <p class="comment">use the same custom disk read routine as boot0 (still in low memory)</p>
      <code>2410-   20 54 02    JSR   $0254</code>
      <p class="comment">and jump there</p>
      <code>2413-   4C 00 05    JMP   $0500</code>
    </div>
    <p>Let's interrupt the boot (again) to see what's at $0500. I can do this all day.</p>
    <p>I am not that busy.</p>
    <div class="line_editing">
      <code>*9600<C600.C6FFM</code>
    </div>
    <div class="listing">
      <p class="comment">copy boot0 (save as previous trace)</p>
      <code>96F8-   A2 00       LDX   #$00
96FA-   BD 00 08    LDA   $0800,X
96FD-   9D 00 02    STA   $0200,X
9700-   E8          INX
9701-   D0 F7       BNE   $96FA</code>
      <p class="comment">set up callback #1 after loading $400</p>
      <code>9703-   A9 10       LDA   #$10
9705-   8D 52 02    STA   $0252
9708-   A9 97       LDA   #$97
970A-   8D 53 02    STA   $0253</code>
      <p class="comment">start the boot</p>
      <code>970D-   4C 0F 02    JMP   $020F</code>
      <p class="comment">callback #1 is here -- set up callback #2 after loading $500</p>
      <code>9710-   A9 1D       LDA   #$1D
9712-   8D 14 04    STA   $0414
9715-   A9 97       LDA   #$97
9717-   8D 15 04    STA   $0415</code>
      <p class="comment">continue the boot</p>
      <code>971A-   4C 00 04    JMP   $0400</code>
      <p class="comment">callback #2 is here -- copy the sector we read at $0500 to the graphics page so it survives a reboot</p>
      <code>971D-   A0 00       LDY   #$00
971F-   B9 00 05    LDA   $0500,Y
9722-   99 00 25    STA   $2500,Y
9725-   C8          INY
9726-   D0 F7       BNE   $971F</code>
      <p class="comment">turn off drive and reboot</p>
      <code>9728-   AD E8 C0    LDA   $C0E8
972B-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE3,A$9600,L$12E
*9600G
...reboots slot 6...
...reboots slot 5...</code>
      <code>]BSAVE BOOT1 0500-05FF,A$2500,L$100
]CALL -151</code>
      <code>*2500L</code>
    </div>
    <div class="listing">
      <code>2500-   A9 00       LDA   #$00
2502-   85 26       STA   $26
2504-   85 41       STA   $41
2506-   A9 09       LDA   #$09
2508-   85 3D       STA   $3D
250A-   A9 06       LDA   #$06
250C-   85 27       STA   $27
250E-   A6 2B       LDX   $2B
2510-   20 54 02    JSR   $0254
2513-   4C 00 06    JMP   $0600</code>
    </div>
    <div class="terminal_output">
      <code>COME AT ME, BRO!</code>
    </div>
    <div class="line_editing">
      <code>*9600<C600.C6FFM</code>
    </div>
    <p>. . [same as previous traces] . ; callback #2 is here -- ; set up callback #3 (!) 971D- A9 2A LDA #$2A 971F- 8D 14 05 STA $0514 9722- A9 97 LDA #$97 9724- 8D 15 05 STA $0515</p>
    <div class="listing">
      <p class="comment">continue the boot</p>
      <code>9727-   4C 00 05    JMP   $0500</code>
      <p class="comment">callback #3 is here -- save $0600 and reboot</p>
      <code>972A-   A0 00       LDY   #$00
972C-   B9 00 06    LDA   $0600,Y
972F-   99 00 26    STA   $2600,Y
9732-   C8          INY
9733-   D0 F7       BNE   $972C
9735-   AD E8 C0    LDA   $C0E8
9738-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE4,A$9600,L$13B
*9600G
...blah blah blah...</code>
      <code>]BSAVE BOOT1 0600-06FF,A$2600,L$100
]CALL -151</code>
      <code>*2600L</code>
    </div>
    <div class="listing">
      <p class="comment">set up a data table of some sort</p>
      <code>2600-   A9 B6       LDA   #$B6
2602-   A0 06       LDY   #$06
2604-   BE C0 05    LDX   $05C0,Y
2607-   9D E4 06    STA   $06E4,X
260A-   18          CLC
260B-   69 01       ADC   #$01
260D-   C8          INY
260E-   C0 10       CPY   #$10
2610-   D0 F2       BNE   $2604</code>
      <p class="comment">You get an RWTS, and YOU get an RWTS, and EVERYBODY GETS AN RWTS</p>
      <code>2612-   A6 2B       LDX   $2B
2614-   BD 8C C0    LDA   $C08C,X
2617-   10 FB       BPL   $2614</code>
      <p class="comment">still the same prologue ("AA D5 AB")</p>
      <code>2619-   49 AA       EOR   #$AA
261B-   D0 F7       BNE   $2614
261D-   BD 8C C0    LDA   $C08C,X
2620-   10 FB       BPL   $261D
2622-   C9 D5       CMP   #$D5
2624-   D0 F3       BNE   $2619
2626-   EA          NOP
2627-   BD 8C C0    LDA   $C08C,X
262A-   10 FB       BPL   $2627
262C-   C9 AB       CMP   #$AB
262E-   D0 E4       BNE   $2614</code>
      <p class="comment">parse address field</p>
      <code>2630-   A0 03       LDY   #$03
2632-   85 40       STA   $40
2634-   BD 8C C0    LDA   $C08C,X
2637-   10 FB       BPL   $2634
2639-   2A          ROL
263A-   85 3C       STA   $3C
263C-   BD 8C C0    LDA   $C08C,X
263F-   10 FB       BPL   $263C
2641-   25 3C       AND   $3C
2643-   88          DEY
2644-   D0 EC       BNE   $2632
2646-   85 3D       STA   $3D</code>
      <p class="comment">check the data table we created earlier to see if if we care about this sector</p>
      <code>2648-   A4 3D       LDY   $3D
264A-   B9 E4 06    LDA   $06E4,Y</code>
      <p class="comment">nope, branch back and keep looking</p>
      <code>264D-   F0 C3       BEQ   $2612</code>
      <p class="comment">yes, keep going (and use the value from the table as the address)</p>
      <code>264F-   85 27       STA   $27</code>
    </div>
    <p>The data table was filled starting with $B6 (A=$B6 at $0600) up to $BF (Y=$06 at $0602 and up to but not including $10 at $060E). So I'm guessing we're reading into $B600..$BFFF.</p>
    <div class="listing">
      <p class="comment">same data prologue, "AA D5 EB"</p>
      <code>2651-   BD 8C C0    LDA   $C08C,X
2654-   10 FB       BPL   $2651
2656-   49 AA       EOR   #$AA
2658-   D0 F7       BNE   $2651
265A-   BD 8C C0    LDA   $C08C,X
265D-   10 FB       BPL   $265A
265F-   C9 D5       CMP   #$D5
2661-   D0 F3       BNE   $2656
2663-   EA          NOP
2664-   BD 8C C0    LDA   $C08C,X
2667-   10 FB       BPL   $2664
2669-   49 EB       EOR   #$EB
266B-   D0 E4       BNE   $2651
.
. &c. (standard 6-and-2 decoding)
.
; mark that we read this sector
26B5-   A4 3D       LDY   $3D
26B7-   A9 00       LDA   #$00
26B9-   99 E4 06    STA   $06E4,Y</code>
      <p class="comment">check whether we've read every sector that we care about</p>
      <code>26BC-   A0 0F       LDY   #$0F
26BE-   B9 E4 06    LDA   $06E4,Y
26C1-   F0 03       BEQ   $26C6</code>
      <p class="comment">no, jump back to read more</p>
      <code>26C3-   4C 12 06    JMP   $0612
26C6-   88          DEY
26C7-   10 F5       BPL   $26BE</code>
      <p class="comment">If we fall through here, we've read all the sectors we care about. Now move some memory around</p>
      <code>26C9-   A2 00       LDX   #$00
26CB-   BD 00 05    LDA   $0500,X
26CE-   9D 00 03    STA   $0300,X
26D1-   E8          INX
26D2-   D0 F7       BNE   $26CB</code>
      <p class="comment">twice, in case it didn't take the first time (WTF)</p>
      <code>26D4-   A2 00       LDX   #$00
26D6-   BD 00 05    LDA   $0500,X
26D9-   9D 00 03    STA   $0300,X
26DC-   E8          INX
26DD-   D0 F7       BNE   $26D6
26DF-   A6 2B       LDX   $2B</code>
      <p class="comment">and continue elsewhere</p>
      <code>26E1-   4C 00 B7    JMP   $B700</code>
    </div>
    <p>And now this is starting to look like we're making progress towards some ordinary DOS 3.3 boot1 code. Maybe. No promises, though.</p>
</section>

<section>
  <h2>Chapter 2</h2>
  <h3>My God, It's Full Of Stars(*)</h3>

    <p>(*) Kevin insisted that I use this line</p>
    <p>at least once. Hi Kevin!</p>
    <div class="line_editing">
      <code>*9600<C600.C6FFM</code>
      <code>*96F8L</code>
    </div>
    <div class="listing">
      <p class="comment">reproduce initial copy of boot0 code</p>
      <code>96F8-   A2 00       LDX   #$00
96FA-   BD 00 08    LDA   $0800,X
96FD-   9D 00 02    STA   $0200,X
9700-   E8          INX
9701-   D0 F7       BNE   $96FA</code>
      <p class="comment">set up callback #1 instead of jumping to $0400</p>
      <code>9703-   A9 10       LDA   #$10
9705-   8D 52 02    STA   $0252
9708-   A9 97       LDA   #$97
970A-   8D 53 02    STA   $0253</code>
      <p class="comment">start the boot</p>
      <code>970D-   4C 0F 02    JMP   $020F</code>
      <p class="comment">callback #1 is here -- set up callback #2</p>
      <code>9710-   A9 1D       LDA   #$1D
9712-   8D 14 04    STA   $0414
9715-   A9 97       LDA   #$97
9717-   8D 15 04    STA   $0415</code>
      <p class="comment">continue the boot</p>
      <code>971A-   4C 00 04    JMP   $0400</code>
      <p class="comment">callback #2 is here -- set up callback #3</p>
      <code>971D-   A9 2A       LDA   #$2A
971F-   8D 14 05    STA   $0514
9722-   A9 97       LDA   #$97
9724-   8D 15 05    STA   $0515</code>
      <p class="comment">continue the boot</p>
      <code>9727-   4C 00 05    JMP   $0500</code>
      <p class="comment">callback #3 is here -- set up callback #4</p>
      <code>972A-   A9 37       LDA   #$37
972C-   8D E2 06    STA   $06E2
972F-   A9 97       LDA   #$97
9731-   8D E3 06    STA   $06E3</code>
      <p class="comment">continue the boot</p>
      <code>9734-   4C 00 06    JMP   $0600</code>
      <p class="comment">callback #4 is here -- copy next phase into graphics page so it survives a reboot</p>
      <code>9737-   A2 0A       LDX   #$0A
9739-   A0 00       LDY   #$00
973B-   B9 00 B6    LDA   $B600,Y
973E-   99 00 26    STA   $2600,Y
9741-   C8          INY
9742-   D0 F7       BNE   $973B
9744-   EE 3D 97    INC   $973D
9747-   EE 40 97    INC   $9740
974A-   CA          DEX
974B-   D0 EE       BNE   $973B</code>
      <p class="comment">turn off the slot 6 drive motor and reboot to my work disk</p>
      <code>974D-   AD E8 C0    LDA   $C0E8
9750-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE5,A$9600,L$153
*9600G
...reboots slot 6...
...reboots slot 5...</code>
      <code>]BSAVE BOOT1 B600-BFFF,A$2600,L$A00
]CALL -151</code>
      <code>*FE89G FE93G     ; disconnect DOS
*B600<2600.2FFFM ; move RWTS into place
*B700L</code>
    </div>
    <div class="listing">
      <code>B700-   4C EA B6    JMP   $B6EA</code>
    </div>
    <div class="line_editing">
      <code>*B6EAL</code>
    </div>
    <div class="listing">
      <code>B6EA-   A9 80       LDA   #$80
B6EC-   85 D8       STA   $D8</code>
      <p class="comment">put boot slot (x16) into... an RWTS parameter table?</p>
      <code>B6EE-   8E E9 B7    STX   $B7E9
B6F1-   8E F7 B7    STX   $B7F7
B6F4-   20 B3 B6    JSR   $B6B3</code>
    </div>
    <div class="line_editing">
      <code>*B6B3L</code>
    </div>
    <div class="listing">
      <p class="comment">overwrite previous boot code with the text page we saved way back at $020F</p>
      <code>B6B3-   A0 00       LDY   #$00
B6B5-   B9 00 20    LDA   $2000,Y
B6B8-   99 00 04    STA   $0400,Y
B6BB-   B9 00 21    LDA   $2100,Y
B6BE-   99 00 05    STA   $0500,Y
B6C1-   B9 00 22    LDA   $2200,Y
B6C4-   99 00 06    STA   $0600,Y
B6C7-   C8          INY
B6C8-   D0 EB       BNE   $B6B5</code>
      <p class="comment">and zap it all anyway by calling HOME (WTF)</p>
      <code>B6CA-   4C 58 FC    JMP   $FC58</code>
    </div>
    <p>Continuing from $B6F7...</p>
    <div class="listing">
      <p class="comment">back to text page 1 (now blank)</p>
      <code>B6F7-   AD 54 C0    LDA   $C054
B6FA-   AD 10 C0    LDA   $C010
B6FD-   4C 06 B7    JMP   $B706</code>
    </div>
    <div class="line_editing">
      <code>*B706L</code>
    </div>
    <div class="listing">
      <p class="comment">fairly standard setup of an RWTS parameter table</p>
      <code>B706-   A9 01       LDA   #$01
B708-   8D F8 B7    STA   $B7F8
B70B-   8D EA B7    STA   $B7EA
B70E-   AD E0 B7    LDA   $B7E0
B711-   8D E1 B7    STA   $B7E1</code>
      <p class="comment">start reading on T02,S08 (4 sectors offset from standard DOS 3.3, which means it won't need to read anything from track $00)</p>
      <code>B714-   A9 02       LDA   #$02
B716-   8D EC B7    STA   $B7EC
B719-   A9 08       LDA   #$08
B71B-   8D ED B7    STA   $B7ED
B71E-   AC E7 B7    LDY   $B7E7
B721-   88          DEY
B722-   8C F1 B7    STY   $B7F1
B725-   A9 01       LDA   #$01
B727-   8D F4 B7    STA   $B7F4
B72A-   8A          TXA
B72B-   4A          LSR
B72C-   4A          LSR
B72D-   4A          LSR</code>
      <p class="comment">oh what fresh hell is this</p>
      <code>B72F-   20 30 B6    JSR   $B630</code>
    </div>
    <div class="line_editing">
      <code>*B630L</code>
    </div>
    <div class="listing">
      <p class="comment">munging the boot slot into a reboot vector</p>
      <code>B630-   AA          TAX
B631-   09 C0       ORA   #$C0
B633-   8D 11 B6    STA   $B611
B636-   A9 00       LDA   #$00
B638-   60          RTS</code>
    </div>
    <p>Continuing from $B732...</p>
    <div class="listing">
      <code>B732-   9D F8 04    STA   $04F8,X
B735-   9D 78 04    STA   $0478,X</code>
      <p class="comment">load DOS</p>
      <code>B738-   20 93 B7    JSR   $B793
B73B-   A2 FF       LDX   #$FF
B73D-   9A          TXS
B73E-   8E EB B7    STX   $B7EB</code>
      <p class="comment">initialize ROM and stuff (normal)</p>
      <code>B741-   4C C8 BF    JMP   $BFC8
B744-   20 89 FE    JSR   $FE89</code>
      <p class="comment">jump to next phase of DOS to load the HELLO program</p>
      <code>B747-   4C 84 9D    JMP   $9D84</code>
    </div>
    <p>Poking around a bit further, the RWTS is not laid out in a normal fashion, but the entry point is still at $BD00. (There is also the standard higher level entry point at $B7B5, which is called by the even higher level entry point at $B793.)</p>
    <p>The RWTS is a weird hybrid that can read both the (protected) program disk and a standard data disk. This is not unusual in educational programs that let you load and save your work on your own disk.</p>
    <p>For example, here is the code to match the data field prologue:</p>
    <div class="line_editing">
      <code>*B9CDL</code>
    </div>
    <div class="listing">
      <code>B9CD-   BD 8C C0    LDA   $C08C,X
B9D0-   10 FB       BPL   $B9CD</code>
      <p class="comment">just an RTS (so, killing 12 cycles -- 6 to call it and 6 to return)</p>
      <code>B9D2-   20 59 BA    JSR   $BA59</code>
      <p class="comment">match $AA or $D5</p>
      <code>B9D5-   C9 AA       CMP   #$AA
B9D7-   F0 04       BEQ   $B9DD
B9D9-   C9 D5       CMP   #$D5
B9DB-   D0 EA       BNE   $B9C7
B9DD-   EA          NOP</code>
      <p class="comment">match $D5 or $AA</p>
      <code>B9DE-   BD 8C C0    LDA   $C08C,X
B9E1-   10 FB       BPL   $B9DE
B9E3-   C9 D5       CMP   #$D5
B9E5-   F0 04       BEQ   $B9EB
B9E7-   C9 AA       CMP   #$AA
B9E9-   D0 EA       BNE   $B9D5
B9EB-   A0 56       LDY   #$56</code>
      <p class="comment">match $EB or $AD</p>
      <code>B9ED-   BD 8C C0    LDA   $C08C,X
B9F0-   10 FB       BPL   $B9ED
B9F2-   C9 EB       CMP   #$EB
B9F4-   F0 04       BEQ   $B9FA
B9F6-   C9 AD       CMP   #$AD
B9F8-   D0 DB       BNE   $B9D5</code>
    </div>
    <p>So this one RWTS can read the program disk ("AA D5 EB" prologue) or a normal data disk ("D5 AA AD" prologue). Nice!</p>
    <p>And here's the kicker, a bit further down:</p>
    <div class="line_editing">
      <code>*BA2AL</code>
    </div>
    <div class="listing">
      <p class="comment">for the epilogue, match $DE...</p>
      <code>BA2A-   BD 8C C0    LDA   $C08C,X
BA2D-   10 FB       BPL   $BA2A
BA2F-   C9 DE       CMP   #$DE
BA31-   F0 1C       BEQ   $BA4F</code>
      <p class="comment">...or $ED, followed by a timing bit!</p>
      <code>BA33-   C9 ED       CMP   #$ED
BA35-   D0 21       BNE   $BA58
BA37-   20 59 BA    JSR   $BA59
BA3A-   EA          NOP
BA3B-   EA          NOP
BA3C-   EA          NOP
BA3D-   BD 8C C0    LDA   $C08C,X
BA40-   30 16       BMI   $BA58
BA42-   EA          NOP
BA43-   BD 8C C0    LDA   $C08C,X
BA46-   10 FB       BPL   $BA43
BA48-   C9 AA       CMP   #$AA
BA4A-   D0 0C       BNE   $BA58
BA4C-   4C 5A BA    JMP   $BA5A
BA4F-   BD 8C C0    LDA   $C08C,X
BA52-   10 FB       BPL   $BA4F
BA54-   C9 AA       CMP   #$AA
BA56-   F0 02       BEQ   $BA5A
BA58-   38          SEC
BA59-   60          RTS
BA5A-   18          CLC
BA5B-   60          RTS</code>
    </div>
    <p>This explains why my EDD bit copy would just grind. It wasn't preserving the extra timing bit after the first nibble of the data epilogue, so the RWTS could never find the epilogue. (Specifically, the RWTS intentionally wastes enough time between $BA37 and $BA3C that the $ED nibble on the copy has fallen out of the data latch, so the extra check at $BA3D fails and it jumps to $BA58 to set the carry, indicating a read error.</p>
    <p>On the original disk, the extra timing bit ensures that the data latch holds onto the $ED nibble for 4 extra cycles, so the branch at $BA40 is not taken and it falls through to check the second epilogue nibble at $BA43.)</p>
    <p>The good news: this RWTS is just barely normal enough to plug it into Advanced</p>
    <p>Demuffin and convert the disk sector by sector (starting from track $01).</p>
    <p>The bad news: there isn't any bad news.</p>
    <p>This was the wrong format to use to announce the good news.</p>
</section>

<section>
  <h2>Chapter 3</h2>
  <h3>In Which We Finally Use The Original Disk As A Weapon Against Itself And It Goes Splendidly</h3>

    <div class="line_editing">
      <code>*C500G       ; because we overwrote DOS
...
]BRUN ADVANCED DEMUFFIN 1.5</code>
    </div>
    <div class="curses_input">
      <code>["5" to switch to slot 5]</code>
      <code>["R" to load a new RWTS module]
  --> At $B6, load "BOOT1 B600-BFFF"
      from drive 1</code>
      <code>["6" to switch to slot 6]</code>
      <code>["C" to convert disk]</code>
    </div>
    <div class="terminal_output">
      <code>--> CHANGE DEFAULT VALUES? Y</code>
      <code>
ADVANCED DEMUFFIN 1.5    (C) 1983, 2014
ORIGINAL BY THE STACK    UPDATES BY 4AM
=======================================</code>
      <code>
INPUT ALL VALUES IN HEX</code>
      <code>
SECTORS PER TRACK? (13/16) 16</code>
    </div>
    <p>START TRACK: $01 <-- change this START SECTOR: $00</p>
    <div class="terminal_output">
      <code>END TRACK: $22
END SECTOR: $0F</code>
      <code>INCREMENT: 1</code>
      <code>MAX # OF RETRIES: 0</code>
      <code>COPY FROM DRIVE 1
TO DRIVE: 2
=======================================
16SC $01,$00-$22,$0F BY1.0 S6,D1->S6,D2</code>
    </div>
    <p class="seen"></p>
    <p class="seen">Now press RETURN to start the copy...</p>
    <div class="terminal_output">
      <code>ADVANCED DEMUFFIN 1.5    (C) 1983, 2014
ORIGINAL BY THE STACK    UPDATES BY 4AM
=======PRESS ANY KEY TO CONTINUE=======
TRK: ..................................
+.5:
    0123456789ABCDEF0123456789ABCDEF012
SC0: ..................................
SC1: ..................................
SC2: ..................................
SC3: ..................................
SC4: ..................................
SC5: ..................................
SC6: ..................................
SC7: ..................................
SC8: ..................................
SC9: ..................................
SCA: ..................................
SCB: ..................................
SCC: ..................................
SCD: ..................................
SCE: ..................................
SCF: ..................................
=======================================
16SC $01,$00-$22,$0F BY1.0 S6,D1->S6,D2</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
]CATALOG,S6,D2</code>
    </div>
    <div class="terminal_output">
      <code>C1983 DSR^C#254
093 FREE</code>
      <code> A 009 HELLO
 A 007 MENU
 A 013 EARTH SATELLITE
 A 007 STELLAR EVOLUTION
 A 019 MULTI-STAGE ROCKET
 A 011 TRANSFER ORBIT
 A 018 SPECTRAL TYPES
 A 021 STARSHIP
 A 014 BINARY STARS
 A 012 SOLAR SYSTEM
 B 034 PIC
 A 003 HREND
 A 010 SPIRAL GALAXY
 A 009 FREEZE-FRAME
 A 007 SINGLE EVOLUTION
 A 010 CLUSTER EVOLUTION
 T 013 EVOLDATA
 T 006 POLYDATA
 T 002 WAVELENGTHS
 T 005 LINESTRENGTHS
 A 010 JOVIAN MOONS
 A 017 SATELLITES
 A 010 ELLIPTICAL ORBIT
 A 031 BUILD-A-WORLD
 A 011 TEMPERATURE AND COLOR
 T 004 SSDATA
 A 012 INSIDE STARS
 A 016 COMETS
 T 004 SSLABELS
 A 010 INSIDESPT2
 T 002 SSTEMP
 A 012 SSPT2
 T 003 COMETDATA
 A 017 COMETSPT2
 T 002 COMETTEMP
 A 008 SINGLEPT2
 T 004 SINGLEDATA</code>
    </div>
    <div class="line_editing">
      <code>]RUN HELLO
...works...</code>
    </div>
    <p class="seen">Using Copy ][+, I can "copy DOS" from a freshly initialized DOS 3.3 disk onto the non-working copy. This function of</p>
    <p class="seen">Copy ][+ just sector-copies tracks 0-2 from one disk to another, but it's easier than setting that up manually in some other copy program.</p>
    <div class="curses_input">
      <code>[S6,D1=demuffin'd copy with T01+]
[S6,D2=newly formatted DOS 3.3 disk]</code>
    </div>
    <p class="seen">Copy ][+</p>
    <p class="seen">--> COPY --> DOS --> from slot 6, drive 2 --> to slot 6, drive 1</p>
    <p class="seen">...read read read... ...write write write...</p>
    <div class="line_editing">
      <code>]PR#6
...works...</code>
    </div>
    <p class="seen">Quod erat liberandum.</p>
</section>

<p>Next: <a rel="next" href="./467-microzine-2.html">Microzine 2</a></p>
</body></html>
