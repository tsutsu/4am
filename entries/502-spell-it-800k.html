<!doctype html>
<html><head>
<title>Spell It 800K</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
<meta name="viewport" content="width=420, initial-scale=1.0, user-scalable=no">
</head><body>

<p>Previous: <a rel="previous" href="./501-monkey-business.html">Monkey Business</a></p>
<section>
  <h2>Chapter 0</h2>
  <h3>In Which Various Automated Tools Fail In Interesting Ways</h3>

    <p class="seen">Copy ][+ 9.1 ("COPY" > "DISK")</p>
    <p>read error on block $0308; copy loads ProDOS, displays a prompt, prints "PLEASE PRESS SPACE BAR", and crashes</p>
    <p class="seen">CFFA 3000 import</p>
    <p class="seen">read error on block 776 (= $0308); booting the disk image in an emulator exhibits the same behavior as the backup I made with Copy ][+ disk copy</p>
    <p class="seen">The original disk boots without complaint, so either this bad block is part of the protection check, or it's unrelated and I got incredibly lucky.</p>
    <p class="seen">Next steps:</p>
    <p class="seen">1. Trace the startup program</p>
    <p class="seen">2. Find and disable the protection check</p>
    <p class="seen">3. Declare victory(*)</p>
    <p class="seen">(*) Take a nap</p>
</section>

<section>
  <h2>Chapter 1</h2>
  <h3>Success Is Failure, Failure Is Success, Black Is White, Night Is Day, Teaching Is Dead</h3>

    <div class="curses_input">
      <code>[S7,D1=my ProDOS hard drive]
[S5,D1=non-working copy]</code>
    </div>
    <div class="line_editing">
      <code>]PR#7
...
]CAT,S5,D1</code>
      <code>/SPELL</code>
    </div>
    <div class="terminal_output">
      <code> NAME           TYPE  BLOCKS  MODIFIED</code>
    </div>
    <div class="line_editing">
      <code>*DATA            DIR       4  25-OCT-87
*PICTURES        DIR       1  25-OCT-87
*PRODOS          SYS      32  17-APR-87
*BASIC.SYSTEM    SYS      21  15-NOV-83
*TONE.DATA       BIN       1  23-JUL-87
*STARTUP         BAS      39   3-OCT-87
 INIT            BAS       6  <NO DATE>
*EDITOR          BAS      20  17-NOV-87
*DEMO            BAS      29  25-OCT-87
 SPELL.C         BAS      31  <NO DATE>
 FINDER.ROOT     $C9       1  30-MAY-90
 FINDER.DATA     $C9       1  30-MAY-90</code>
    </div>
    <div class="terminal_output">
      <code>BLOCKS FREE: 1011     BLOCKS USED:  589</code>
    </div>
    <p>I saw a BASIC prompt during boot (even on my unsuccessful copy), so let's start with STARTUP.BAS.</p>
    <div class="line_editing">
      <code>]PREFIX /WORD
]LOAD STARTUP
]LIST</code>
    </div>
    <div class="terminal_output">
      <code> 10  CALL 9361</code>
    </div>
    <p>Ah! A fake BASIC program that really calls the embedded assembly language program within it.</p>
    <p>9361 = $2491. Let's see what's there.</p>
    <div class="line_editing">
      <code>]CALL -151</code>
      <code>*2491L</code>
    </div>
    <div class="listing">
      <code>2491-   4C 94 24    JMP   $2494</code>
    </div>
    <div class="line_editing">
      <code>*2494L</code>
    </div>
    <div class="listing">
      <code>2494-   20 00 24    JSR   $2400</code>
    </div>
    <div class="line_editing">
      <code>*2400L</code>
    </div>
    <div class="listing">
      <p class="comment">save registers, flags, and several zero page locations</p>
      <code>2400-   08          PHP
2401-   48          PHA
2402-   98          TYA
2403-   48          PHA
2404-   8A          TXA
2405-   48          PHA
2406-   A5 F8       LDA   $F8
2408-   48          PHA
2409-   A5 F9       LDA   $F9
240B-   48          PHA
240C-   A5 FA       LDA   $FA
240E-   48          PHA</code>
      <p class="comment">get unit number (slot+drive, but in a weird format that ProDOS likes)</p>
      <code>240F-   AD 30 BF    LDA   $BF30
2412-   4A          LSR
2413-   4A          LSR
2414-   4A          LSR
2415-   4A          LSR
2416-   29 07       AND   #$07
2418-   09 C0       ORA   #$C0</code>
      <p class="comment">so this is the boot slot, munged into $Cx form (so $C6 for slot 6)</p>
      <code>241A-   85 F9       STA   $F9
241C-   A9 00       LDA   #$00
241E-   85 F8       STA   $F8</code>
      <p class="comment">now look at some signature bytes in the peripheral ROM for the boot slot to determine what kind of floppy disk we booted from (5.25" or 3.5")</p>
      <code>2420-   A0 01       LDY   #$01
2422-   B1 F8       LDA   ($F8),Y
2424-   C9 20       CMP   #$20
2426-   D0 22       BNE   $244A
2428-   A0 03       LDY   #$03
242A-   B1 F8       LDA   ($F8),Y
242C-   D0 1C       BNE   $244A
242E-   A0 05       LDY   #$05
2430-   B1 F8       LDA   ($F8),Y
2432-   C9 03       CMP   #$03
2434-   D0 14       BNE   $244A
2436-   A0 FF       LDY   #$FF
2438-   B1 F8       LDA   ($F8),Y
243A-   D0 0E       BNE   $244A</code>
      <p class="comment">if 5.25-inch, use block $110 (which is somewhere on track $22)</p>
      <code>243C-   A9 10       LDA   #$10
243E-   8D 04 22    STA   $2204
2441-   A9 01       LDA   #$01
2443-   8D 05 22    STA   $2205
2446-   D0 0C       BNE   $2454
2448-   F0 0A       BEQ   $2454</code>
      <p class="comment">if 3.5-inch, use block $308 (this is the block I couldn't read when I tried to copy the disk)</p>
      <code>244A-   A9 08       LDA   #$08
244C-   8D 04 22    STA   $2204
244F-   A9 03       LDA   #$03
2451-   8D 05 22    STA   $2205</code>
      <p class="comment">fill out the rest of an MLI parameter table</p>
      <code>2454-   AD 30 BF    LDA   $BF30
2457-   8D 01 22    STA   $2201
245A-   A9 03       LDA   #$03
245C-   8D 00 22    STA   $2200
245F-   A9 00       LDA   #$00
2461-   8D 02 22    STA   $2202
2464-   A9 20       LDA   #$20
2466-   8D 03 22    STA   $2203</code>
      <p class="comment">now call the ProDOS MLI to perform a raw block read on the specified block</p>
      <code>2469-   20 00 BF    JSR   $BF00
246C-  [80 00 22]</code>
      <p class="comment">if there was NOT a disk read error, then branch</p>
      <code>246F-   90 0C       BCC   $247D
2471-   85 FA       STA   $FA</code>
      <p class="comment">if there was any error other than an</p>
      <p class="comment">I/O error ($27), then branch</p>
      <code>2473-   C9 27       CMP   #$27
2475-   D0 06       BNE   $247D</code>
      <p class="comment">success path falls through to here -- set a flag in zero page</p>
      <code>2477-   A9 FF       LDA   #$FF
2479-   85 FF       STA   $FF
247B-   D0 04       BNE   $2481</code>
      <p class="comment">failure path is here (via $246F or $2475) -- set the magic zero page flag to $00 instead of $FF</p>
      <code>247D-   A9 00       LDA   #$00
247F-   85 FF       STA   $FF</code>
      <p class="comment">restore everything on the way out (except the magic flag at zp$FF)</p>
      <code>2481-   68          PLA
2482-   85 FA       STA   $FA
2484-   68          PLA
2485-   85 F9       STA   $F9
2487-   68          PLA
2488-   85 F8       STA   $F8
248A-   68          PLA
248B-   AA          TAX
248C-   68          PLA
248D-   A8          TAY
248E-   68          PLA
248F-   28          PLP
2490-   60          RTS</code>
    </div>
    <p>Continuing from $2497...</p>
    <div class="listing">
      <p class="comment">munge reset vector</p>
      <code>2497-   A0 FF       LDY   #$FF
2499-   8C F4 03    STY   $03F4</code>
      <p class="comment">set RUN flag</p>
      <code>249C-   A0 80       LDY   #$80
249E-   8C D6 00    STY   $00D6</code>
      <p class="comment">check magic zero page flag that was set by the bad block check at $2400</p>
      <code>24A1-   A0 FF       LDY   #$FF
24A3-   C4 FF       CPY   $FF
24A5-   F0 03       BEQ   $24AA</code>
      <p class="comment">very bad</p>
      <code>24A7-   6C F2 03    JMP   ($03F2)</code>
      <p class="comment">very good</p>
      <code>24AA-   A0 40       LDY   #$40
24AC-   8C 68 00    STY   $0068
24AF-   4C 66 D5    JMP   $D566</code>
    </div>
    <p class="seen">This is definitely a form of copy protection. (Fun fact: it appears to be written so it can be re-used for 5.25- and 3.5-inch floppies.) It relies on one specific block or sector being unreadable, and fails if it's actually readable. Since my copy didn't reproduce the badness of block $308, the MLI read routine unexpectedly succeeds, and the program knows it's been copied.</p>
    <p>To bypass the protection, I can change the failure path at $247D so it sets the magic zero page flag to $FF instead of $00.</p>
    <p class="seen">Block Warden</p>
    <p>["C"hange device -> S5,D1] ["F"ollow file] ["STARTUP"]</p>
    <p>Block $01DD, offset $7D change 00 to FF</p>
    <div class="line_editing">
      <code>]PR#5
...works...</code>
    </div>
    <p class="seen">Quod erat liberandum.</p>
</section>

<p>Next: <a rel="next" href="./503-magic-spells-2-1.html">Magic Spells 2.1</a></p>
</body></html>
