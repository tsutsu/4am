<!doctype html>
<html><head>
<title>House-a-Fire</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
</head><body>

<p>Previous: <a rel="previous" href="output/139-microzine-13.html">Microzine 13</a></p>
<section>

    <p>"House-a-Fire!" is a 1988 educational program distributed by The Ellen Nelson</p>
    <p>Learning Library, a division of</p>
    <p>Decision Development Co.</p>
    <p class="seen">COPYA fails miserably and immediately.</p>
    <p>Locksmith Fast Disk Backup fares no better. EDD 4 bit copy gives no read errors, but the copy it creates just reboots endlessly.</p>
    <p>Turning to my trusty Disk Fixer sector editor, I go to "Input/Output Control" (press "O") and set CHECKSUM ENABLED= NO. (This ignores address and data epilogue bytes.) Now I can read every sector on every track.</p>
    <p>From a brief manual inspection, I'd say the RWTS on track 0 strongly resembles a DOS 3.3 RWTS. However, there is no VTOC (disk catalog) on track $11, and the original disk does not sound like it is loading a DOS from tracks 0-2 on boot.</p>
    <p class="seen">Given the (relatively) weak structural protection, I used to turn to the DOS</p>
    <p class="seen">3.3 master disk, patch the RWTS to ignore checksums and epilogue bytes (changing $B942 from "SEC" to "CLC"), and run COPYA. Then, one fine day, and completely by accident, I came across an original disk with a bad sector. I suppose this shouldn't surprise me.</p>
    <p class="seen">These floppies are decades old by now; it's amazing any of them work at all.</p>
    <p class="seen">The point is, I shouldn't be using tools that ignore potentially serious read errors. There are other tools, like Super Demuffin, that can convert a disk like this (with non-standard epilogue bytes) into a standard format.</p>
    <p class="seen">It requires figuring out what the actual epilogue bytes are, but it has the advantage of surfacing a read error if the original disk actually has a read error.</p>
    <p class="seen">So... no more COPYA+B942:18 patch. From now on, it's Super Demuffin or Advanced</p>
    <p>Demuffin to convert disks to a standard format. I should be able to use my AUTOTRACE program to extract the RWTS from the original disk, then load that into Advanced Demuffin to convert it to a standard format.</p>
    <p class="seen">Time for boot tracing with AUTOTRACE.</p>
    <div class="curses_input">
      <code>[S6,D1=original disk, side A]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...
CAPTURING BOOT0
...reboots slot 6...
...reboots slot 5...
SAVING BOOT0
CAPTURING BOOT1
...reboots slot 6...
...reboots slot 5...
SAVING BOOT1
SAVING RWTS
/!\ NIBBLE CHECK AT $BB00</code>
    </div>
    <p class="seen">Well, that worked flawlessly. My work disk first runs AUTOTRACE0, which sets up a minimal boot trace to capture the first sector of track 0 and saves it to the file "BOOT0" (on my work disk, not the original disk). If the first sector looks reasonably normal, it runs AUTOTRACE1, which sets up a more involved boot trace to capture the rest of track 0 and save it to the file "BOOT1". If *that* looks reasonably normal, it saves the RWTS to a file, unimaginatively named "RWTS", which can be loaded with Advanced Demuffin to copy each track of the original disk to a duplicate with standard address/data prologue/epilogue sequences.</p>
    <p>Furthermore, my AUTOTRACE program detected a possible nibble check at $BB00. This is a relatively new feature that I added quite recently. I'm glad to see it's already paying dividends. (It scans the $BB00 page for the three-byte sequence "BD E9 C0", which is the instruction "LDA $C0E9,X", which turns on the drive motor manually. Even though there are tons of other code variations that would accomplish the same thing, 99% of the nibble checks</p>
    <p>I've ever seen have started with that exact instruction.)</p>
    <p>I'll come back to the nibble check.</p>
    <p>First, I want to take the RWTS I extracted and load it into Advanced</p>
    <p class="seen">Demuffin to convert this disk to a standard format.</p>
    <div class="curses_input">
      <code>[S6,D1=original disk, side A]
[S6,D2=blank disk]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...</code>
      <code>]BRUN ADVANCED DEMUFFIN 1.5</code>
    </div>
    <div class="curses_input">
      <code>[press "5" to switch to slot 5]</code>
      <code>[press "R" to load a new RWTS module]
  --> At $B8, load "RWTS" from drive 1</code>
      <code>[press "6" to switch to slot 6]</code>
      <code>[press "C" to convert disk]</code>
    </div>
    <p class="seen">This disk is 16 sectors, and the default options (copy the entire disk, all tracks, all sectors) don't need to be changed unless something goes horribly wrong.</p>
    <div class="terminal_output">
      <code>ADVANCED DEMUFFIN 1.5    (C) 1983, 2014
ORIGINAL BY THE STACK    UPDATES BY 4AM
=======PRESS ANY KEY TO CONTINUE=======
TRK:...................................
+.5:
    0123456789ABCDEF0123456789ABCDEF012
SC0:...................................
SC1:...................................
SC2:...................................
SC3:...................................
SC4:...................................
SC5:...................................
SC6:...................................
SC7:...................................
SC8:...................................
SC9:...................................
SCA:...................................
SCB:...................................
SCC:...................................
SCD:...................................
SCE:...................................
SCF:...................................
=======================================
16SC $00,$00-$22,$0F BY1.0 S6,D1->S6,D2</code>
    </div>
    <p>The disk's own RWTS gave no read errors on any track. (I repeated the procedure for side B with the same RWTS; it also converted with no errors.) This is the power and the genius of Advanced</p>
    <p class="seen">Demuffin. Every disk must be able to read itself. So, let it read itself, then capture the data and write it out in a standard format.</p>
    <p>However, booting the newly demuffin'd copy shows the same behavior as my failed bit copy: an endless loop of rebooting very early in the boot process.</p>
    <p>Let's go find that nibble check.</p>
    <div class="line_editing">
      <code>]PR#5
...</code>
      <code>]BLOAD BOOT1,A$2600
]CALL -151</code>
      <code>*FE89G FE93G B600<2600.2FFFM</code>
      <code>*B700L</code>
    </div>
    <div class="listing">
      <code>B700-   20 1A BC    JSR   $BC1A   <-- !
B703-   8E F7 B7    STX   $B7F7
B706-   A9 01       LDA   #$01
B708-   8D F8 B7    STA   $B7F8
B70B-   8D EA B7    STA   $B7EA
B70E-   AD E0 B7    LDA   $B7E0
B711-   8D E1 B7    STA   $B7E1
B714-   A9 08       LDA   #$08
B716-   8D EC B7    STA   $B7EC
B719-   A9 0D       LDA   #$0D
B71B-   8D ED B7    STA   $B7ED</code>
    </div>
    <p>Most of that looks relatively normal.</p>
    <p>I mean, it's reading from track $08, sector $0D, which is unusual, but it appears to be setting up a standard RWTS parameter table to do so.</p>
    <p>That first line is definitely unusual, though. That jumped out to me right away.</p>
    <div class="line_editing">
      <code>*BC1AL</code>
    </div>
    <div class="listing">
      <p class="comment">this is the instruction I was expecting at $B700 (but it called this subroutine instead)</p>
      <code>BC1A-   8E E9 B7    STX   $B7E9</code>
      <p class="comment">OK</p>
      <code>BC1D-   A9 00       LDA   #$00
BC1F-   8D 78 04    STA   $0478
BC22-   48          PHA
BC23-   A6 2B       LDX   $2B</code>
      <p class="comment">seek to track $00 (we're already there, but whatever)</p>
      <code>BC25-   A9 00       LDA   #$00
BC27-   20 A0 B9    JSR   $B9A0</code>
      <p class="comment">hello</p>
      <code>BC2A-   20 03 BB    JSR   $BB03</code>
      <p class="comment">now clean up and restore everything</p>
      <code>BC2D-   68          PLA
BC2E-   08          PHP
BC2F-   A6 2B       LDX   $2B
BC31-   20 A0 B9    JSR   $B9A0
BC34-   28          PLP
BC35-   60          RTS</code>
    </div>
    <p>Anything in the $BB00 range is automatically suspicious, because in any relatively normal RWTS, that entire page of memory gets overwritten with every disk read during denibblization. (That is totally not a real word.)</p>
    <div class="line_editing">
      <code>*BB03L</code>
    </div>
    <div class="listing">
      <p class="comment">save some zero page state</p>
      <code>BB03-   A2 00       LDX   #$00
BB05-   B5 00       LDA   $00,X
BB07-   9D 00 50    STA   $5000,X
BB0A-   CA          DEX
BB0B-   10 F8       BPL   $BB05</code>
      <p class="comment">onward</p>
      <code>BB0D-   20 00 BC    JSR   $BC00</code>
    </div>
    <div class="line_editing">
      <code>*BC00L</code>
    </div>
    <div class="listing">
      <p class="comment">set reset vector to reboot</p>
      <code>BC00-   A5 2B       LDA   $2B
BC02-   4A          LSR
BC03-   4A          LSR
BC04-   4A          LSR
BC05-   4A          LSR
BC06-   09 C0       ORA   #$C0
BC08-   8D F3 03    STA   $03F3
BC0B-   49 A5       EOR   #$A5
BC0D-   8D F4 03    STA   $03F4
BC10-   A9 00       LDA   #$00
BC12-   8D F2 03    STA   $03F2</code>
      <p class="comment">and continue elsewhere</p>
      <code>BC15-   A6 2B       LDX   $2B
BC17-   4C 30 BB    JMP   $BB30</code>
    </div>
    <div class="line_editing">
      <code>*BB30L</code>
    </div>
    <div class="listing">
      <p class="comment">turn on the drive motor manually: always a sign of impending evil bits (see? it's always exactly "BD 89 C0")</p>
      <code>BB30-   A6 2B       LDX   $2B
BB32-   BD 89 C0    LDA   $C089,X
BB35-   BD 8E C0    LDA   $C08E,X</code>
      <p class="comment">set up some pointers and counters</p>
      <code>BB38-   A9 AE       LDA   #$AE
BB3A-   85 1E       STA   $1E
BB3C-   A9 BB       LDA   #$BB
BB3E-   85 1F       STA   $1F
BB40-   A9 0A       LDA   #$0A
BB42-   85 09       STA   $09
BB44-   A9 80       LDA   #$80
BB46-   85 08       STA   $08
BB48-   C6 08       DEC   $08</code>
      <p class="comment">if zero page $08 reaches 0, fail</p>
      <code>BB4A-   F0 5C       BEQ   $BBA8</code>
      <p class="comment">read next available address field</p>
      <code>BB4C-   20 44 B9    JSR   $B944</code>
      <p class="comment">if that failed, give up</p>
      <code>BB4F-   B0 57       BCS   $BBA8</code>
      <p class="comment">check which sector we got</p>
      <code>BB51-   A5 2D       LDA   $2D
BB53-   C9 0A       CMP   #$0A</code>
      <p class="comment">if we found the wrong sector, fail</p>
      <code>BB55-   D0 F1       BNE   $BB48
BB57-   A0 00       LDY   #$00
BB59-   BD 8C C0    LDA   $C08C,X
BB5C-   10 FB       BPL   $BB59
BB5E-   88          DEY</code>
      <p class="comment">fail</p>
      <code>BB5F-   F0 47       BEQ   $BBA8
BB61-   C9 D5       CMP   #$D5
BB63-   D0 F4       BNE   $BB59</code>
      <p class="comment">Search for a specific sequence of nibbles in the "dead zone" between the address field and data field.</p>
      <p class="comment">This area is normally not important, so COPYA didn't copy it precisely because normal disks don't care. (Actually, it's even more evil than that, because the original disk is written with timing bits in specific non-standard places between the nibbles in the dead zone. This code not only requires the right nibbles in the right order, it reads them just slightly slower than normal. So the timing bits need to be in the right places too, or else this code will read the wrong nibble values while it's out of sync. This will trip up even the best bit copiers.</p>
      <p class="comment">And you can forget about making a disk image for emulators -- those don't store timing bits at all.)</p>
      <code>BB65-   A0 00       LDY   #$00
BB67-   BD 8C C0    LDA   $C08C,X
BB6A-   10 FB       BPL   $BB67
BB6C-   88          DEY</code>
      <p class="comment">fail</p>
      <code>BB6D-   F0 39       BEQ   $BBA8
BB6F-   C9 E7       CMP   #$E7
BB71-   D0 F4       BNE   $BB67
BB73-   BD 8C C0    LDA   $C08C,X
BB76-   10 FB       BPL   $BB73
BB78-   C9 E7       CMP   #$E7</code>
      <p class="comment">fail</p>
      <code>BB7A-   D0 2C       BNE   $BBA8
BB7C-   BD 8C C0    LDA   $C08C,X
BB7F-   10 FB       BPL   $BB7C
BB81-   C9 E7       CMP   #$E7</code>
      <p class="comment">fail</p>
      <code>BB83-   D0 23       BNE   $BBA8</code>
      <p class="comment">kill some time to get out of sync with the "proper" start of nibbles)</p>
      <code>BB85-   BD 8D C0    LDA   $C08D,X
BB88-   A0 10       LDY   #$10
BB8A-   24 FF       BIT   $FF</code>
      <p class="comment">now start looking for nibbles that don't really exist (except they do, because we're out of sync and reading timing bits as data)</p>
      <code>BB8C-   BD 8C C0    LDA   $C08C,X
BB8F-   10 FB       BPL   $BB8C
BB91-   88          DEY</code>
      <p class="comment">fail</p>
      <code>BB92-   F0 14       BEQ   $BBA8
BB94-   C9 EE       CMP   #$EE
BB96-   D0 F4       BNE   $BB8C</code>
      <p class="comment">check for nibble sequence stored in reverse order at $BBAE</p>
      <code>BB98-   A0 07       LDY   #$07
BB9A-   BD 8C C0    LDA   $C08C,X
BB9D-   10 FB       BPL   $BB9A
BB9F-   D1 1E       CMP   ($1E),Y</code>
      <p class="comment">fail</p>
      <code>BBA1-   D0 05       BNE   $BBA8
BBA3-   88          DEY
BBA4-   10 F4       BPL   $BB9A
BBA6-   18          CLC
BBA7-   60          RTS</code>
      <p class="comment">all failures end up here -- try a few more times then give up completely</p>
      <code>BBA8-   C6 09       DEC   $09
BBAA-   D0 98       BNE   $BB44</code>
      <p class="comment">out of chances, set carry and exit</p>
      <code>BBAC-   38          SEC
BBAD-   60          RTS</code>
    </div>
    <p class="seen">OK, so it's a nibble check that clears the carry if it works and sets the carry if it fails. Unwinding the calling stack, we left off here:</p>
    <div class="listing">
      <code>BB0D-   20 00 BC    JSR   $BC00</code>
      <p class="comment">save flags</p>
      <code>BB10-   08          PHP</code>
      <p class="comment">restore zero page state</p>
      <code>BB11-   A2 00       LDX   #$00
BB13-   B9 00 50    LDA   $5000,Y
BB16-   95 00       STA   $00,X
BB18-   CA          DEX
BB19-   10 F8       BPL   $BB13</code>
      <p class="comment">restore flags</p>
      <code>BB1B-   28          PLP</code>
      <p class="comment">if nibble check passed, continue</p>
      <code>BB1C-   90 0F       BCC   $BB2D</code>
      <p class="comment">if nibble check failed, push the $Cx00 address onto the stack and "return" to it (i.e. reboot)</p>
      <code>BB1E-   A5 2B       LDA   $2B
BB20-   4A          LSR
BB21-   4A          LSR
BB22-   4A          LSR
BB23-   4A          LSR
BB24-   09 C0       ORA   #$C0
BB26-   A8          TAY
BB27-   88          DEY
BB28-   98          TYA
BB29-   48          PHA
BB2A-   A9 FF       LDA   #$FF
BB2C-   48          PHA</code>
      <p class="comment">success path branches to here</p>
      <code>BB2D-   A6 2B       LDX   $2B
BB2F-   60          RTS</code>
    </div>
    <p>Which is exactly the behavior I saw: endless rebooting.</p>
    <p>I can simply bypass this entire routine without adverse consequences. Unwinding the stack even further, this all started at $B700, which called $BC1A, which stored the X register in $B7E9 (useful) then did a nibble check (not useful). If I restore that original instruction at $B700, it should bypass all the protection-related code and continue with the normal boot.</p>
    <p class="seen">T00,S01,$00 change "20 1A BC"</p>
    <p class="seen">to "8E E9 B7"</p>
    <p>Success! The disk boots and loads its title screens and continues to the main menu. All options appear to work. The original disk's RWTS is liberal enough that it doesn't require any changes to read the unprotected copy.</p>
    <p class="seen">Quod erat liberandum.</p>
</section>

<p>Next: <a rel="next" href="output/141-crumb-eater.html">Crumb Eater</a></p>
</body></html>
