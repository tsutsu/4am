<!doctype html>
<html><head>
<title>Tangrams Puzzler</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
</head><body>

<p>Previous: <a rel="previous" href="./134-spell-it.html">Spell It</a></p>
<section>

    <p>"Tangrams Puzzler" is a 1984 educational game developed by Ken</p>
    <p>Coates and distributed by Milliken</p>
    <p>Publishing Company.</p>
    <p>COPYA fails immediately and spectacularly(*).</p>
    <p>(*)not actually spectacular</p>
    <p>EDD 4 bit copy gives no read errors, but the copy does not work. It sounds like a standard DOS 3.3 boot, but then it puts a "." in the top-left corner of the screen and reboots.</p>
    <p>Disk Fixer is a standalone sector editor I just discovered. I've taken to using it for most of the things I used to do in my trusty Copy ][+ sector editor. It's small and fast, and I can launch it from ProDOS with the help of DOS 3.3 Launcher.</p>
    <p>Anyway, in Disk Fixer, in the Input/</p>
    <p>Output Control settings (press "O"), I turn off checksums. This ignores the address and data epilogue bytes, and also the checksum byte after the data field. And lo and behold, I can read every sector on every track! It does indeed appear to be a modified version of DOS 3.3. T11,S0F looks like a standard VTOC directory sector with filenames and pointers to track/sector lists and whatnot.</p>
    <p class="seen">Based on my limited experience cracking other disks, I would guess that this disk has</p>
    <p class="seen">- Standard prologue bytes before the</p>
    <p class="seen">address and data fields [otherwise</p>
    <p>Disk Fixer would give read errors, even after ignoring checksums]</p>
    <p class="seen">- Non-standard epilogue bytes after the</p>
    <p class="seen">address and data fields [otherwise COPYA would work]</p>
    <p class="seen">- Some secondary protection [otherwise</p>
    <p class="seen">the bit copy created with EDD 4 would work]</p>
    <p class="seen">Given the (relatively) weak structural protection, I used to turn to the DOS</p>
    <p class="seen">3.3 master disk, patch the RWTS to ignore checksums and epilogue bytes (changing $B942 from "SEC" to "CLC"), and run COPYA. Then, one fine day, and completely by accident, I came across an original disk with a bad sector. I suppose this shouldn't surprise me.</p>
    <p class="seen">These floppies are decades old by now; it's amazing any of them work at all.</p>
    <p class="seen">The point is, I shouldn't be using tools that ignore potentially serious read errors. So, no more COPYA+B942:18 patch. From now on, it's Super Demuffin or Advanced Demuffin to convert disks to a standard format.</p>
    <p>Since this disk looks like DOS 3.3 and sounds like DOS 3.3, I'm going to see what my AUTOTRACE program can do with it.</p>
    <div class="curses_input">
      <code>[S6,D1=original disk]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...
CAPTURING BOOT0
...reboots slot 6...
...reboots slot 5...
SAVING BOOT0
CAPTURING BOOT1
...reboots slot 6...
...reboots slot 5...
SAVING BOOT1
SAVING RWTS</code>
    </div>
    <p class="seen">For those of you just tuning in, my work disk uses a custom program that I affectionately call "AUTOTRACE" to automate the process of boot tracing as far as possible. For some disks, this just captures track 0, sector 0 (saved in a file called "BOOT0") and stops.</p>
    <p class="seen">For other disks that load in the same way that an unprotected DOS 3.3 disk loads, it captures the next stage of the boot process as well (in a file called "BOOT1"). BOOT1 contains sectors 0-9 on track 0, which are loaded into memory at $B600..$BFFF. This generally contains the RWTS routines which the program uses to read the rest of the disk.</p>
    <p class="seen">If the RWTS is fairly normal as well (and my AUTOTRACE program just spot- checks a few memory locations to guess at its "normalcy"), there's a good chance I'll be able to use a tool called Advanced Demuffin (written in 1983 by The Stack) to convert the disk from whatever weird format it uses to store its sector data into a standard disk readable by unprotected DOS 3.3 disks or any other third-party tools.</p>
    <p class="seen">In this case, AUTOTRACE extracts the RWTS routines (generally loaded from track 0, sectors 2-9 into $B800..$BFFF) and saves *that* into a third file called "RWTS".</p>
    <p class="seen">If anything looks fishy or non- standard, AUTOTRACE just stops, and I have to check the files it saved so far to determine why. But in this case, it ran all the way through, automatically capturing BOOT0, BOOT1, and RWTS files.</p>
    <p class="seen">Now I can use Advanced Demuffin to convert the disk to a standard format. (It uses the disk's own RWTS to read the original, then a standard DOS 3.3- compatible RWTS to write out the data, sector by sector.)</p>
    <div class="curses_input">
      <code>[S6,D1=original disk]
[S6,D2=blank disk]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...</code>
      <code>]BRUN ADVANCED DEMUFFIN 1.5</code>
    </div>
    <div class="curses_input">
      <code>[press "5" to switch to slot 5]</code>
      <code>[press "R" to load a new RWTS module]
  --> At $B8, load "RWTS" from drive 1</code>
      <code>[press "6" to switch to slot 6]</code>
      <code>[press "C" to convert disk]</code>
    </div>
    <p class="seen">This disk is 16 sectors, and the default options (copy the entire disk, all tracks, all sectors) don't need to be changed unless something goes horribly wrong.</p>
    <div class="terminal_output">
      <code>ADVANCED DEMUFFIN 1.5    (C) 1983, 2014
ORIGINAL BY THE STACK    UPDATES BY 4AM
=======PRESS ANY KEY TO CONTINUE=======
TRK:..R................................
+.5:
    0123456789ABCDEF0123456789ABCDEF012
SC0:...................................
SC1:...................................
SC2:...................................
SC3:...................................
SC4:...................................
SC5:..R................................
SC6:...................................
SC7:...................................
SC8:...................................
SC9:...................................
SCA:...................................
SCB:...................................
SCC:...................................
SCD:...................................
SCE:...................................
SCF:...................................
=======================================
16SC $00,$00-$22,$0F BY1.0 S6,D1->S6,D2</code>
    </div>
    <p>The disk's own RWTS gave one read error on track $02, but the rest of the disk copied without a hitch. The original disk gives no indication of a bad sector, so either this sector is unused (and I got lucky) or this is part of the copy protection.</p>
    <p>Let's see if I can launch the program from my work disk.</p>
    <div class="line_editing">
      <code>]PR#5
...</code>
      <code>]CATALOG,S6,D2</code>
    </div>
    <div class="terminal_output">
      <code>C1983 DSR^C#254
149 FREE</code>
      <code>ERROR #8 I/O ERR</code>
    </div>
    <p class="seen"></p>
    <p class="seen">Wait, what?</p>
    <p>I'm pretty sure I saw a disk catalog on track $11. So where is it?</p>
    <p>Turning to my trusty Disk Fixer sector editor, I see the problem. Track $11 does have a catalog, but the pointer to the first directory sector has been intentionally corrupted.</p>
    <div class="curses">
      <code>-------------- DISK EDIT --------------
TRACK $11/SECTOR $00/VOLUME $FE/BYTE$00
---------------------------------------
$00: 00 F1 0F 03 00 00 FE 00   @qOC@@~@
        ^^ ^^
     track,sector
   (should be 11,0F)</code>
    </div>
    <div class="terminal_output">
      <code>$08: 00 00 00 00 00 00 00 00   @@@@@@@@
$10: 00 00 00 00 00 00 00 00   @@@@@@@@
$18: 00 00 00 00 00 00 00 00   @@@@@@@@
$20: 00 00 00 00 00 00 00 7A   @@@@@@@:
$28: 00 00 00 00 00 00 00 00   @@@@@@@@
$30: 06 FF 00 00 23 10 00 01   F.@@#P@A
$38: 00 00 00 00 00 00 00 00   @@@@@@@@
$40: 00 00 00 00 FF FF 00 00   @@@@..@@
$48: FF FF 00 00 FF FF 00 00   ..@@..@@
$50: 7F FF 00 00 00 00 00 00   ?.@@@@@@
$58: 00 00 00 00 00 00 00 00   @@@@@@@@
$60: 00 00 00 00 3F FF 00 00   @@@@?.@@
$68: 00 00 00 00 00 00 00 00   @@@@@@@@
$70: 3F FF 00 00 00 00 00 00   ?.@@@@@@
$78: 00 00 00 00 00 00 00 00   @@@@@@@@
---------------------------------------
BUFFER 0/SLOT 6/DRIVE 1/MASK OFF/NORMAL</code>
      <code>---------------------------------------
COMMAND :</code>
    </div>
    <p>Third-party utilities can not catalog this disk or work with any of its files (every DOS file function routes through this pointer), but the original disk obviously has no problem. But how does the original disk know? I scoured "Beneath Apple DOS" until I found the answer on page 8-28:</p>
    <div class="curses">
      <code>B011-B036 Read a directory sector
; (If CARRY flag is zero on entry, read
  first directory sector. If CARRY is
  one, read next)
; Memorize entry code.
; Set buffer pointers (B045).
; First or next?
; If first, get track/sector of
  directory sector from VTOC at offset
  +1,+2.
; Otherwise, get track/sector from
  directory sector at offset +1,+2. If
  track is zero, exit with error code
  (end of directory).
; Call RWTS to read sector.
; Exit with normal return code.</code>
    </div>
    <p>So, to read the first sector of file names and other metadata, this routine is supposed to look at the VTOC sector buffer (read from T11,S00 and stored at $B3BB..$B4BA). The VTOC says "hey, the first sector of files and stuff is in TF1,S0F" so DOS goes and tries to read track $F1, sector $0F, which obviously won't work.</p>
    <p>But the DOS on this disk made one small modification to that routine. (This is stored on T01,S0F.)</p>
    <div class="listing">
      <code>B011-   08          PHP
B012-   20 45 B0    JSR   $B045
B015-   28          PLP
B016-   B0 08       BCS   $B020
B018-   AC BD B3    LDY   $B3BD
                                ------
B01B-   A2 11       LDX   #$11  << hey
B01D-   EA          NOP         << now
                                ------
B01E-   D0 0A       BNE   $B02A
B020-   AE BC B4    LDX   $B4BC
B023-   D0 02       BNE   $B027
B025-   38          SEC
B026-   60          RTS
B027-   AC BD B4    LDY   $B4BD
B02A-   8E 97 B3    STX   $B397
B02D-   8C 98 B3    STY   $B398
B030-   A9 01       LDA   #$01
B032-   20 52 B0    JSR   $B052
B035-   18          CLC
B036-   60          RTS</code>
    </div>
    <p>Instead of getting the track number from the VTOC, it hard-codes track $11.</p>
    <p>Now that I've identified the problem, the fix is straightforward. If I change the VTOC header (T11,S00) to point to the actual first directory sector (T11,S0F), DOS 3.3 or any other copy utility should be able to read the entire catalog. This disk doesn't care either way, since it never looks at it.</p>
    <p>T11,S00,$01 change "F1" to "11"</p>
    <p>Now I should be able to catalog it from my work disk.</p>
    <div class="line_editing">
      <code>]PR#5
...</code>
      <code>]CATALOG,S6,D2</code>
    </div>
    <div class="terminal_output">
      <code>C1983 DSR^C#254
149 FREE</code>
    </div>
    <div class="line_editing">
      <code>*A 011 BOOT
*B 010 JCODE.2048
*B 005 CS4
*A 019 MANAGER.BAS
*B 012 INS1
*B 009 INS2
*B 015 T.TPGS
*B 017 PLAY.DATA
*B 017 DATA.EDIT
*T 002 INTERFACE
*B 058 OBJ.PLAY
*B 057 OBJ.EDIT
 B 007 EDIT.DATA
 T 002 STDTAN
 T 065 STDTA
 B 034 PIC.LOGO
 B 007 A.LOGO</code>
      <code>]RUN BOOT</code>
    </div>
    <p>The game loads without complaint. (It even runs from drive 2!) There doesn't appear to be any further protection beyond the custom RWTS and DOS.</p>
    <p class="seen">There are two problems with this copy:</p>
    <p>1. Depending on how the original RWTS</p>
    <p>was written, a demuffin'd disk may not be able to read itself. Some developers just patch the RWTS to ignore epilogue bytes, while others patch the RWTS to look for specific non-standard epilogue bytes.</p>
    <p>Demuffin'd disks in the latter category will grind immediately on boot, since as soon as the RWTS is loaded, all further disk reads will look for the original (non-standard) epilogue bytes and not find them.</p>
    <p class="seen">2. Even if it can read itself, it won't</p>
    <p class="seen">run. The copies I tried to make -- even the bit copies -- just rebooted endlessly, which means there is some code being executed during boot to check if the disk is original. (Hint: it's not.)</p>
    <p>This disk appears to be in the "strict RWTS" category. My demuffin'd copy just grinds on boot, which tells me that the RWTS is looking for specific (non- standard) epilogue bytes and failing to find them. So I need to patch the RWTS to undo the modifications and look for the standard epilogue bytes instead.</p>
    <p class="seen">For future reference (mostly mine), here's a nice chart of the memory locations for all the prologues and epilogues in a DOS 3.3-shaped RWTS. If the RWTS stores $B700 in T00,S01 (most do), then $B8xx will be in T00,S02; $B9xx in T00,S03; and so on.</p>
    <p class="seen"></p>
    <p class="seen">0x | read | write</p>
    <p class="seen">---------------+-------+------- D5 | $B955 | $BC7A prologue AA | $B95F | $BC7F / 96 | $B96A | $BC84 ADDRESS -------+-------+------- \ DE | $B991 | $BCAE epilogue AA | $B99B | $BCB3 EB | | $BCB8 ---------------+-------+------- D5 | $B8E7 | $B853 prologue AA | $B8F1 | $B858 / AD | $B8FC | $B85D DATA ----------+-------+------- \ DE | $B935 | $B89E epilogue AA | $B93F | $B8A3 EB | | $B8A8 ---------------+-------+-------</p>
    <p class="seen"></p>
    <p class="seen">I spent way too much time making that.</p>
    <p>Here are the patches to normalize the RWTS. (Note to self: make an RWTS comparison tool.)</p>
    <p>T00,S02,$9E change "DF" to "DE" T00,S03,$35 change "DF" to "DE" T00,S03,$91 change "DF" to "DE" T00,S06,$AE change "DF" to "DE"</p>
    <p>Now my demuffin'd copy boots, loads DOS, puts a "." in the top-left of the screen, then -- and I am not making this up -- displays the following message and hangs:</p>
    <div class="terminal_output">
      <code>      ATTENTION!</code>
      <code>THE DISK DRIVE YOU ARE USING IS
OPERATING AT AN UNSAFE SPEED.</code>
      <code>OPERATION OF THIS PROGRAM ON THIS DRIVE
MAY DESTROY THE PROGRAM.</code>
      <code>PLEASE HAVE YOUR DRIVE SPEED CHECKED,
OR USE ANOTHER DISK DRIVE.</code>
    </div>
    <p>I should note at this point that my "demuffin'd copy" is actually a disk image being managed by a CFFA 3000 card. I copied the disk image back to a physical floppy and booted it, and it displayed the behavior I was expecting (load DOS, print ".", then reboot -- just like my failed bit copy). Then I copied the disk image to a modern PC and ran it in two different emulators; each time, it gave me the message about the drive operating at an unsafe speed.</p>
    <p>I'm about to give up on this DOS and just blow it away and replace it with a fresh copy of DOS 3.3. I've already confirmed that the program itself loads when I boot from my work disk. But now</p>
    <p>I want to know whether this DOS is really checking the drive speed, or if that's just bullshit and misdirection.</p>
    <p>So here we go.</p>
    <div class="line_editing">
      <code>]PR#5
...</code>
      <code>]BLOAD BOOT0
]CALL -151</code>
      <code>*801L
.
. nothing unusual
.</code>
      <code>*BLOAD BOOT1</code>
      <code>*FE89G FE93G     ; disconnect DOS</code>
      <code>*B600<2600.2FFFM ; move RWTS into place</code>
      <code>*B700L
.
. nothing unusual, until
.
B747-   4C 03 9B    JMP   $9B03</code>
    </div>
    <p>This usually jumps to $9D84, the DOS cold start vector. I don't know what's at $9B03 yet, but I can find out.</p>
    <div class="line_editing">
      <code>*C500G    ; because I overwrote DOS
...</code>
      <code>]CALL -151</code>
      <code>*9600<C600.C6FFM</code>
    </div>
    <div class="listing">
      <p class="comment">set up first callback</p>
      <code>96F8-   A9 4C       LDA   #$4C
96FA-   8D 4A 08    STA   $084A
96FD-   A9 0A       LDA   #$0A
96FF-   8D 4B 08    STA   $084B
9702-   A9 97       LDA   #$97
9704-   8D 4C 08    STA   $084C</code>
      <p class="comment">start the boot</p>
      <code>9707-   4C 01 08    JMP   $0801</code>
      <p class="comment">first callback is here -- set up second callback</p>
      <code>970A-   A9 17       LDA   #$17
970C-   8D 48 B7    STA   $B748
970F-   A9 97       LDA   #$97
9711-   8D 49 B7    STA   $B749</code>
      <p class="comment">continue the boot</p>
      <code>9714-   4C 00 B7    JMP   $B700</code>
      <p class="comment">second callback is here -- copy all of DOS to lower memory so it will survive a reboot</p>
      <code>9717-   A2 25       LDX   #$25
9719-   A0 00       LDY   #$00
971B-   B9 00 9B    LDA   $9B00,Y
971E-   99 00 2B    STA   $2B00,Y
9721-   C8          INY
9722-   D0 F7       BNE   $971B
9724-   EE 1D 97    INC   $971D
9727-   EE 20 97    INC   $9720
972A-   CA          DEX
972B-   D0 EE       BNE   $971B</code>
      <p class="comment">reboot to my work disk</p>
      <code>972D-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE2,A$9600,L$130</code>
      <code>*9600G
...reboots slot 6...
...reboots slot 5...</code>
      <code>]BSAVE BOOT2,A$2B00,L$2500
]CALL -151</code>
      <code>*FE89G FE93G</code>
      <code>*9B00<2B00.4FFFM</code>
      <code>*9B03L</code>
    </div>
    <div class="listing">
      <p class="comment">well that explains the "." in the top-left corner of the screen</p>
      <code>9B03-   A9 AE       LDA   #$AE
9B05-   8D 00 04    STA   $0400
9B08-   4C 99 AD    JMP   $AD99</code>
    </div>
    <div class="line_editing">
      <code>*AD99L</code>
    </div>
    <div class="listing">
      <p class="comment">read T11,S00...</p>
      <code>AD99-   A9 11       LDA   #$11
AD9B-   8D EC B7    STA   $B7EC
AD9E-   A0 00       LDY   #$00
ADA0-   8C ED B7    STY   $B7ED
ADA3-   8C F0 B7    STY   $B7F0
ADA6-   8C EB B7    STY   $B7EB
ADA9-   C8          INY
ADAA-   8C F4 B7    STY   $B7F4
ADAD-   C8          INY</code>
      <p class="comment">...into $0200..$02FF</p>
      <code>ADAE-   8C F1 B7    STY   $B7F1
ADB1-   A9 B7       LDA   #$B7
ADB3-   A0 E8       LDY   #$E8
ADB5-   20 00 BD    JSR   $BD00</code>
      <p class="comment">not sure why we're checking the last byte of the sector we just read, but it appears to always be $00, so this branch will not be taken</p>
      <code>ADB8-   AD FF 02    LDA   $02FF
ADBB-   30 03       BMI   $ADC0
ADBD-   4C 0B 9B    JMP   $9B0B</code>
    </div>
    <div class="line_editing">
      <code>*9B0BL</code>
    </div>
    <div class="listing">
      <p class="comment">get slot number (x16)</p>
      <code>9B0B-   AE F8 05    LDX   $05F8</code>
      <p class="comment">turn on drive motor (suspicious)</p>
      <code>9B0E-   BD 89 C0    LDA   $C089,X</code>
      <p class="comment">this subroutine is a wait loop</p>
      <code>9B11-   A9 0A       LDA   #$0A
9B13-   20 3A 9B    JSR   $9B3A</code>
      <p class="comment">this subroutine looks for the next available address field and does some suspicious bit math</p>
      <code>9B16-   A9 05       LDA   #$05
9B18-   85 03       STA   $03
9B1A-   20 4E 9B    JSR   $9B4E</code>
    </div>
    <div class="line_editing">
      <code>*9B4EL</code>
    </div>
    <div class="listing">
      <p class="comment">look for address prologue</p>
      <code>9B4E-   AE F8 05    LDX   $05F8
9B51-   BD 8C C0    LDA   $C08C,X
9B54-   10 FB       BPL   $9B51
9B56-   C9 D5       CMP   #$D5
9B58-   D0 F7       BNE   $9B51
9B5A-   BD 8C C0    LDA   $C08C,X
9B5D-   10 FB       BPL   $9B5A
9B5F-   C9 AA       CMP   #$AA
9B61-   D0 F3       BNE   $9B56
9B63-   BD 8C C0    LDA   $C08C,X
9B66-   10 FB       BPL   $9B63
9B68-   C9 96       CMP   #$96
9B6A-   D0 EA       BNE   $9B56</code>
      <p class="comment">capture address field in $0200..$0205</p>
      <code>9B6C-   A0 00       LDY   #$00
9B6E-   BD 8C C0    LDA   $C08C,X
9B71-   10 FB       BPL   $9B6E
9B73-   99 00 02    STA   $0200,Y
9B76-   C8          INY
9B77-   C0 06       CPY   #$06
9B79-   90 F3       BCC   $9B6E
9B7B-   AD 02 02    LDA   $0202
9B7E-   38          SEC
9B7F-   2A          ROL
9B80-   2D 03 02    AND   $0203
9B83-   85 06       STA   $06</code>
      <p class="comment">do some bit math on the sector number (result is in accumulator on exit)</p>
      <code>9B85-   AD 04 02    LDA   $0204
9B88-   2A          ROL
9B89-   2D 05 02    AND   $0205
9B8C-   60          RTS</code>
    </div>
    <p>Backtracking to $9B1D...</p>
    <div class="line_editing">
      <code>*9B1DL</code>
    </div>
    <div class="listing">
      <p class="comment">store the result of the suspicious bit math</p>
      <code>9B1D-   85 01       STA   $01</code>
      <p class="comment">another wait loop</p>
      <code>9B1F-   A2 A8       LDX   #$A8
9B21-   A9 07       LDA   #$07
9B23-   20 3A 9B    JSR   $9B3A</code>
      <p class="comment">another call to the subroutine that looks for an address field</p>
      <code>9B26-   20 4E 9B    JSR   $9B4E</code>
      <p class="comment">compare the two results and branch to $9B37 if they're different</p>
      <code>9B29-   85 02       STA   $02
9B2B-   C5 01       CMP   $01
9B2D-   D0 08       BNE   $9B37</code>
      <p class="comment">do this whole thing several times</p>
      <code>9B2F-   C6 03       DEC   $03
9B31-   D0 E7       BNE   $9B1A
9B33-   EA          NOP</code>
      <p class="comment">success path is here</p>
      <code>9B34-   4C AC 9B    JMP   $9BAC</code>
      <p class="comment">failure path is here</p>
      <code>9B37-   4C 10 9C    JMP   $9C10</code>
    </div>
    <div class="line_editing">
      <code>*9C10L</code>
    </div>
    <div class="listing">
      <p class="comment">turn off drive motor</p>
      <code>9C10-   AE F8 05    LDX   $05F8
9C13-   9D 88 C0    STA   $C088,X</code>
      <p class="comment">clear screen</p>
      <code>9C16-   20 58 FC    JSR   $FC58</code>
      <p class="comment">this subroutine prints a zero- terminated string that follows, then resets the program counter to continue execution after the string</p>
      <code>9C19- 20 8D 9B      JSR   $9B8D</code>
    </div>
    <div class="line_editing">
      <code>*9B8DL</code>
    </div>
    <div class="listing">
      <code>9B8D-   68          PLA
9B8E-   85 04       STA   $04
9B90-   68          PLA
9B91-   85 05       STA   $05
9B93-   E6 04       INC   $04
9B95-   D0 02       BNE   $9B99
9B97-   E6 05       INC   $05
9B99-   A0 00       LDY   #$00
9B9B-   B1 04       LDA   ($04),Y
9B9D-   F0 06       BEQ   $9BA5
9B9F-   20 F0 FD    JSR   $FDF0
9BA2-   4C 93 9B    JMP   $9B93
9BA5-   A5 05       LDA   $05
9BA7-   48          PHA
9BA8-   A5 04       LDA   $04
9BAA-   48          PHA
9BAB-   60          RTS</code>
    </div>
    <p>9C1C..9CEF is the ATTENTION message</p>
    <div class="listing">
      <code>9CF0-   A9 00       LDA   #$00
9CF2-   8D F4 03    STA   $03F4
9CF5-   20 C8 AD    JSR   $ADC8
9CF8-   10 03       BPL   $9CFD
9CFA-   4C 03 9C    JMP   $9C03
9CFD-   4C AC 9B    JMP   $9BAC</code>
    </div>
    <p>That seems to be the path that my disk image takes on both the CFFA 3000 card and under emulation. It really is using a timing loop and repeated sector reads to check the drive speed. The fact that it fails under emulation is a result of imperfect emulation, not any malice on the part of the author. He honestly cares about my drive speed. He never even gets as far as checking if my disk is original.</p>
    <p>If the drive speed check passes, execution continues at $9BAC. This is the actual copy protection.</p>
    <div class="listing">
      <p class="comment">position drive on track $02</p>
      <code>9BAC-   A9 02       LDA   #$02
9BAE-   8D EC B7    STA   $B7EC</code>
      <p class="comment">don't actually read, just seek</p>
      <code>9BB1-   A9 00       LDA   #$00
9BB3-   8D F4 B7    STA   $B7F4
9BB6-   A9 B7       LDA   #$B7
9BB8-   A0 E8       LDY   #$E8
9BBA-   20 00 BD    JSR   $BD00
9BBD-   B0 44       BCS   $9C03</code>
      <p class="comment">look for address prologue</p>
      <code>9BBF-   20 4E 9B    JSR   $9B4E</code>
      <p class="comment">if not T02,S05, try again</p>
      <code>9BC2-   C9 05       CMP   #$05
9BC4-   D0 F9       BNE   $9BBF</code>
    </div>
    <p>Aha! Remember that one unreadable sector on track $02? The one that even the disk's own RWTS couldn't read? It turns out that sector is part of the copy protection.</p>
    <div class="listing">
      <p class="comment">look for a prologue</p>
      <code>9BC6-   A0 63       LDY   #$63
9BC8-   AE F8 05    LDX   $05F8
9BCB-   BD 8C C0    LDA   $C08C,X
9BCE-   10 FB       BPL   $9BCB
9BD0-   C9 D5       CMP   #$D5
9BD2-   D0 F7       BNE   $9BCB
9BD4-   BD 8C C0    LDA   $C08C,X
9BD7-   10 FB       BPL   $9BD4
9BD9-   C9 AA       CMP   #$AA
9BDB-   D0 F3       BNE   $9BD0
9BDD-   BD 8C C0    LDA   $C08C,X
9BE0-   10 FB       BPL   $9BDD
9BE2-   C9 AD       CMP   #$AD
9BE4-   D0 EA       BNE   $9BD0
9BE6-   BD 8C C0    LDA   $C08C,X
9BE9-   10 FB       BPL   $9BE6
9BEB-   C9 AD       CMP   #$AD
9BED-   F0 F7       BEQ   $9BE6</code>
      <p class="comment">look for an unreasonable number of sync bytes (64) in a row</p>
      <code>9BEF-   C9 FF       CMP   #$FF</code>
      <p class="comment">anything else just fails immediately</p>
      <code>9BF1-   D0 10       BNE   $9C03
9BF3-   BD 8C C0    LDA   $C08C,X
9BF6-   10 FB       BPL   $9BF3
9BF8-   C9 FF       CMP   #$FF</code>
      <p class="comment">fail immediately</p>
      <code>9BFA-   D0 07       BNE   $9C03
9BFC-   88          DEY
9BFD-   D0 F4       BNE   $9BF3</code>
      <p class="comment">ultimate success path is here -- jump to the DOS cold start vector and continue the boot as normal</p>
      <code>9BFF-   EA          NOP
9C00-   4C 84 9D    JMP   $9D84</code>
      <p class="comment">failure path is here -- calculate the boot slot and reboot</p>
      <code>9C03-   8A          TXA
9C04-   4A          LSR
9C05-   4A          LSR
9C06-   4A          LSR
9C07-   4A          LSR
9C08-   09 C0       ORA   #$C0
9C0A-   8D 0F 9C    STA   $9C0F
9C0D-   4C 00 C6    JMP   $C600</code>
    </div>
    <p>There are no side effects to this copy protection. If it succeeds, it jumps to $9D84 as usual. I can safely bypass the entire thing by changing the JMP that started it all, at $B747.</p>
    <p>T00,S01,$48 change "03 9B" to "84 9D"</p>
    <p class="seen">Quod erat liberandum.</p>
</section>

<p>Next: <a rel="next" href="./136-survival-math.html">Survival Math</a></p>
</body></html>
