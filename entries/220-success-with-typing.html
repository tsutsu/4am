<!doctype html>
<html><head>
<title>Success with Typing</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
</head><body>

<p>Previous: <a rel="previous" href="./219-championship-wrestling.html">Championship Wrestling</a></p>
<section>

    <p class="seen">Only side A is bootable, so I'll start there.</p>
</section>

<section>
  <h2>Chapter 0</h2>
  <h3>In Which Various Automated Tools Fail In Interesting Ways</h3>

    <p class="seen">COPYA</p>
    <p class="seen">immediate disk read error</p>
    <p class="seen">Locksmith Fast Disk Backup</p>
    <p class="seen">unable to read any track</p>
    <p class="seen">EDD 4 bit copy (no sync, no count)</p>
    <p>no errors, but copy reboots endlessly</p>
    <p class="seen">Copy ][+ nibble editor</p>
    <p class="seen">all tracks use standard prologues (address: D5 AA 96, data: D5 AA AD) but modified epilogues (address: FF FF FF, data: FF FF FF)</p>
    <p class="seen">Disk Fixer</p>
    <p>["O" -> "Input/Output Control"] set Address Epilogue to "FF FF FF" set Data Epilogue to "FF FF FF" all tracks readable T00 -> looks like a DOS 3.3 RWTS no other signs of DOS though no disk catalog on any track</p>
    <p class="seen">Why didn't COPYA work?</p>
    <p class="seen">modified epilogue bytes (every track)</p>
    <p class="seen">Why didn't Locksmith FDB work?</p>
    <p class="seen">modified epilogue bytes (every track)</p>
    <p class="seen">Why didn't my EDD copy work?</p>
    <p>probably a nibble check during boot (disks do not reboot unless someone tells them to)</p>
    <p class="seen">Next steps:</p>
    <p class="seen">1. capture RWTS with AUTOTRACE</p>
    <p class="seen">2. convert disk to standard format with Advanced Demuffin</p>
    <p class="seen">3. find nibble check and bypass it</p>
</section>

<section>
  <h2>Chapter 1</h2>
  <h3>In Which We Attempt To Use The Original Disk As A Weapon Against Itself</h3>

    <div class="curses_input">
      <code>[S6,D1=original disk, side A]
[S6,D2=blank disk]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...
CAPTURING BOOT0
...reboots slot 6...
...reboots slot 5...
SAVING BOOT0
CAPTURING BOOT1
...reboots slot 6...
...reboots slot 5...
SAVING BOOT1
SAVING RWTS
/!\ NIBBLE CHECK AT $BB00</code>
      <code>]BRUN ADVANCED DEMUFFIN 1.5</code>
    </div>
    <div class="curses_input">
      <code>["5" to switch to slot 5]</code>
      <code>["R" to load a new RWTS module]
  --> At $B8, load "RWTS" from drive 1</code>
      <code>["6" to switch to slot 6]</code>
      <code>["C" to convert disk]</code>
    </div>
    <div class="terminal_output">
      <code>ADVANCED DEMUFFIN 1.5    (C) 1983, 2014
ORIGINAL BY THE STACK    UPDATES BY 4AM
=======PRESS ANY KEY TO CONTINUE=======
TRK:...................................
+.5:
    0123456789ABCDEF0123456789ABCDEF012
SC0:...................................
SC1:...................................
SC2:...................................
SC3:...................................
SC4:...................................
SC5:...................................
SC6:...................................
SC7:...................................
SC8:...................................
SC9:...................................
SCA:...................................
SCB:...................................
SCC:...................................
SCD:...................................
SCE:...................................
SCF:...................................
=======================================
16SC $00,$00-$22,$0F BY1.0 S6,D1->S6,D2</code>
    </div>
    <div class="curses_input">
      <code>[S6,D1=demuffin'd copy]</code>
    </div>
    <div class="line_editing">
      <code>]PR#6
...reboots endlessly...</code>
    </div>
    <p class="seen">Let's go find that nibble check.</p>
</section>

<section>
  <h2>Chapter 2</h2>
  <h3>In Which We Go Code Spelunking</h3>

    <div class="line_editing">
      <code>]PR#5
]BLOAD BOOT1,A$2600
]CALL -151
*FE89G FE93G     ; disconnect DOS
*B600<2600.2FFFM ; move RWTS into place
*B700L</code>
    </div>
    <div class="listing">
      <code>B700-   20 1A BC    JSR   $BC1A
B703-   8E F7 B7    STX   $B7F7
B706-   A9 01       LDA   #$01
B708-   8D F8 B7    STA   $B7F8
B70B-   8D EA B7    STA   $B7EA
B70E-   AD E0 B7    LDA   $B7E0
B711-   8D E1 B7    STA   $B7E1
B714-   A9 0A       LDA   #$0A
B716-   8D EC B7    STA   $B7EC
B719-   A9 09       LDA   #$09
B71B-   8D ED B7    STA   $B7ED
B71E-   AC E7 B7    LDY   $B7E7
B721-   88          DEY
B722-   8C F1 B7    STY   $B7F1
B725-   A9 01       LDA   #$01
B727-   8D F4 B7    STA   $B7F4
B72A-   8A          TXA
B72B-   4A          LSR
B72C-   4A          LSR
B72D-   4A          LSR</code>
    </div>
    <p class="seen">Most of that looks relatively normal.</p>
    <p>I mean, it's reading from track $0A, sector $09, which is unusual, but it appears to be setting up a standard RWTS parameter table to do so.</p>
    <p class="seen">That first line is definitely unusual, though. That jumped out to me right away.</p>
    <div class="line_editing">
      <code>*BC1AL</code>
    </div>
    <div class="listing">
      <p class="comment">this is the instruction I was expecting at $B700 (but it called this subroutine instead)</p>
      <code>BC1A-   8E E9 B7    STX   $B7E9</code>
      <p class="comment">initialize current track for RWTS</p>
      <code>BC1D-   A9 00       LDA   #$00
BC1F-   8D 78 04    STA   $0478
BC22-   48          PHA
BC23-   A6 2B       LDX   $2B</code>
      <p class="comment">seek to track $20</p>
      <code>BC25-   A9 40       LDA   #$40
BC27-   20 A0 B9    JSR   $B9A0</code>
      <p class="comment">hello</p>
      <code>BC2A-   20 03 BB    JSR   $BB03</code>
      <p class="comment">now clean up and restore everything</p>
      <code>BC2D-   68          PLA
BC2E-   08          PHP
BC2F-   A6 2B       LDX   $2B</code>
      <p class="comment">seek back to trace 0</p>
      <code>BC31-   20 A0 B9    JSR   $B9A0
BC34-   28          PLP
BC35-   60          RTS</code>
    </div>
    <div class="line_editing">
      <code>*BB03L</code>
    </div>
    <div class="listing">
      <p class="comment">save some zero page state</p>
      <code>BB03-   A2 00       LDX   #$00
BB05-   B5 00       LDA   $00,X
BB07-   9D 00 50    STA   $5000,X
BB0A-   CA          DEX
BB0B-   10 F8       BPL   $BB05</code>
      <p class="comment">onward</p>
      <code>BB0D-   20 00 BC    JSR   $BC00</code>
    </div>
    <div class="line_editing">
      <code>*BC00L</code>
    </div>
    <div class="listing">
      <p class="comment">set reset vector to reboot</p>
      <code>BC00-   A5 2B       LDA   $2B
BC02-   4A          LSR
BC03-   4A          LSR
BC04-   4A          LSR
BC05-   4A          LSR
BC06-   09 C0       ORA   #$C0
BC08-   8D F3 03    STA   $03F3
BC0B-   49 A5       EOR   #$A5
BC0D-   8D F4 03    STA   $03F4
BC10-   A9 00       LDA   #$00
BC12-   8D F2 03    STA   $03F2</code>
      <p class="comment">and continue elsewhere</p>
      <code>BC15-   A6 2B       LDX   $2B
BC17-   4C 30 BB    JMP   $BB30</code>
    </div>
    <div class="line_editing">
      <code>*BB30L</code>
    </div>
    <div class="listing">
      <p class="comment">turn on the drive motor manually (always suspicious)</p>
      <code>BB30-   A6 2B       LDX   $2B
BB32-   BD 89 C0    LDA   $C089,X
BB35-   BD 8E C0    LDA   $C08E,X</code>
      <p class="comment">probably an address</p>
      <code>BB38-   A9 AE       LDA   #$AE
BB3A-   85 1E       STA   $1E
BB3C-   A9 BB       LDA   #$BB
BB3E-   85 1F       STA   $1F</code>
      <p class="comment">probably a death counter</p>
      <code>BB40-   A9 0A       LDA   #$0A
BB42-   85 09       STA   $09
BB44-   A9 80       LDA   #$80
BB46-   85 08       STA   $08
BB48-   C6 08       DEC   $08</code>
      <p class="comment">if zero page $08 reaches 0, fail</p>
      <code>BB4A-   F0 5C       BEQ   $BBA8</code>
      <p class="comment">read next available address field</p>
      <code>BB4C-   20 44 B9    JSR   $B944</code>
      <p class="comment">if that failed, give up</p>
      <code>BB4F-   B0 57       BCS   $BBA8</code>
      <p class="comment">look for sector $0A</p>
      <code>BB51-   A5 2D       LDA   $2D
BB53-   C9 0A       CMP   #$0A</code>
      <p class="comment">loop until we find it</p>
      <code>BB55-   D0 F1       BNE   $BB48</code>
      <p class="comment">look for $D5 nibble</p>
      <code>BB57-   A0 00       LDY   #$00
BB59-   BD 8C C0    LDA   $C08C,X
BB5C-   10 FB       BPL   $BB59
BB5E-   88          DEY
BB5F-   F0 47       BEQ   $BBA8  ; fail
BB61-   C9 D5       CMP   #$D5
BB63-   D0 F4       BNE   $BB59</code>
      <p class="comment">Search for a specific sequence of nibbles in the "dead zone" between the address field and data field.</p>
      <p class="comment">This area is normally not important, so COPYA didn't copy it precisely because normal disks don't care. (Actually, it's even more evil than that, because the original disk is written with timing bits in specific non-standard places between the nibbles in the dead zone. This code not only requires the right nibbles in the right order, it reads them just slightly slower than normal. So the timing bits need to be in the right places too, or else this code will read the wrong nibble values while it's out of sync. This will trip up even the best bit copiers.</p>
      <p class="comment">And you can forget about making a disk image for emulators -- those don't store timing bits at all.)</p>
      <code>BB65-   A0 00       LDY   #$00
BB67-   BD 8C C0    LDA   $C08C,X
BB6A-   10 FB       BPL   $BB67
BB6C-   88          DEY
BB6D-   F0 39       BEQ   $BBA8  ; fail
BB6F-   C9 E7       CMP   #$E7
BB71-   D0 F4       BNE   $BB67
BB73-   BD 8C C0    LDA   $C08C,X
BB76-   10 FB       BPL   $BB73
BB78-   C9 E7       CMP   #$E7
BB7A-   D0 2C       BNE   $BBA8  ; fail
BB7C-   BD 8C C0    LDA   $C08C,X
BB7F-   10 FB       BPL   $BB7C
BB81-   C9 E7       CMP   #$E7
BB83-   D0 23       BNE   $BBA8  ; fail</code>
      <p class="comment">kill some time to get out of sync with the "proper" start of nibbles)</p>
      <code>BB85-   BD 8D C0    LDA   $C08D,X
BB88-   A0 10       LDY   #$10
BB8A-   24 FF       BIT   $FF</code>
      <p class="comment">now start looking for nibbles that don't really exist (except they do, because we're out of sync and reading timing bits as data)</p>
      <code>BB8C-   BD 8C C0    LDA   $C08C,X
BB8F-   10 FB       BPL   $BB8C
BB91-   88          DEY
BB92-   F0 14       BEQ   $BBA8  ; fail
BB94-   C9 EE       CMP   #$EE
BB96-   D0 F4       BNE   $BB8C</code>
      <p class="comment">check for nibble sequence stored in reverse order at $BBAE</p>
      <code>BB98-   A0 07       LDY   #$07
BB9A-   BD 8C C0    LDA   $C08C,X
BB9D-   10 FB       BPL   $BB9A
BB9F-   D1 1E       CMP   ($1E),Y
BBA1-   D0 05       BNE   $BBA8  ; fail
BBA3-   88          DEY
BBA4-   10 F4       BPL   $BB9A
BBA6-   18          CLC
BBA7-   60          RTS</code>
      <p class="comment">all failures end up here -- try a few more times then give up completely</p>
      <code>BBA8-   C6 09       DEC   $09
BBAA-   D0 98       BNE   $BB44</code>
      <p class="comment">out of chances, set carry and exit</p>
      <code>BBAC-   38          SEC
BBAD-   60          RTS</code>
    </div>
    <p class="seen">OK, so it's a nibble check that clears the carry if it works and sets the carry if it fails. Unwinding the calling stack, we left off here:</p>
    <div class="listing">
      <code>BB0D-   20 00 BC    JSR   $BC00</code>
      <p class="comment">save flags</p>
      <code>BB10-   08          PHP</code>
      <p class="comment">restore zero page state</p>
      <code>BB11-   A2 00       LDX   #$00
BB13-   B9 00 50    LDA   $5000,Y
BB16-   95 00       STA   $00,X
BB18-   CA          DEX
BB19-   10 F8       BPL   $BB13</code>
      <p class="comment">restore flags</p>
      <code>BB1B-   28          PLP</code>
      <p class="comment">if nibble check passed, continue</p>
      <code>BB1C-   90 0F       BCC   $BB2D</code>
      <p class="comment">if nibble check failed, push the $Cx00 address onto the stack and "return" to it (i.e. reboot)</p>
      <code>BB1E-   A5 2B       LDA   $2B
BB20-   4A          LSR
BB21-   4A          LSR
BB22-   4A          LSR
BB23-   4A          LSR
BB24-   09 C0       ORA   #$C0
BB26-   A8          TAY
BB27-   88          DEY
BB28-   98          TYA
BB29-   48          PHA
BB2A-   A9 FF       LDA   #$FF
BB2C-   48          PHA</code>
      <p class="comment">success path branches to here</p>
      <code>BB2D-   A6 2B       LDX   $2B
BB2F-   60          RTS</code>
    </div>
    <p class="seen">Which is exactly the behavior I saw: endless rebooting.</p>
    <p class="seen">I can simply bypass this entire routine without adverse consequences. Unwinding the stack even further, this all started at $B700, which called $BC1A, which stored the X register in $B7E9 (useful) then did a nibble check (not useful). If I restore that original instruction at $B700, it should bypass all the protection-related code and continue with the normal boot.</p>
</section>

<section>
  <h2>Chapter 3</h2>
  <h3>In Which We Remove All Traces Of Copy Protection Using An Automated Tool That I Wrote For Just Such An Occasion</h3>

    <div class="curses_input">
      <code>[S6,D1=demuffin'd copy]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
]BRUN PDP</code>
    </div>
    <p class="seen">T00,S01,$00 change 201ABC to 8EE9B7</p>
    <p>Advanced Demuffin converted side B with the same RWTS I captured from side A.</p>
    <p>The RWTS is permissive enough that it doesn't require any changes to read the unprotected copy.</p>
    <p class="seen">Quod erat liberandum.</p>
</section>

<p>Next: <a rel="next" href="./221-spy-hunter.html">Spy Hunter</a></p>
</body></html>
