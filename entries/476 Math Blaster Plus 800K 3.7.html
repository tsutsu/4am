<!doctype html>
<html><head>
<title>Math Blaster Plus 800K 3.7</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
</head><body>

<section>
  <h2>Chapter 0</h2>
  <h3>In Which Various Automated Tools Fail In Interesting Ways</h3>

    <p class="seen">Copy ][+ 9.1 ("COPY" > "DISK")</p>
    <p class="seen">read error on block $0308; copy loads ProDOS then quits to program selector</p>
    <p class="seen">CFFA 3000 import</p>
    <p class="seen">read error on block 776 (= $0308); booting the disk image in an emulator exhibits the same behavior as the backup I made with Copy ][+ disk copy (boots ProDOS then quits via a "Quit" MLI call)</p>
    <p class="seen">It's a very ProDOS-y way of saying "f--- you."</p>
    <p class="seen">The original disk boots without complaint, so either this bad block is part of the protection check, or it's unrelated and I got incredibly lucky.</p>
    <p class="seen">Next steps:</p>
    <p class="seen">1. Trace the first .SYSTEM file</p>
    <p class="seen">2. Find and disable the protection check</p>
    <p class="seen">3. Declare victory(*)</p>
    <p class="seen">(*) Take a nap</p>
</section>

<section>
  <h2>Chapter 1</h2>
  <h3>In Which We Learn Many Things, But Not The Right Things</h3>

    <div class="curses_input">
      <code>[S7,D1=my ProDOS hard drive]
[S5,D1=original disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#7
...
]CAT,S5,D1</code>
      <code>/MATH.0</code>
    </div>
    <div class="terminal_output">
      <code> NAME           TYPE  BLOCKS  MODIFIED</code>
      <code> PICTURE0        TXT       9  <NO DATE>
 MATH            TXT      31   9-MAY-88
 GAME            TXT      30  26-APR-88
 MBPTEXT         TXT       5  15-OCT-87
 GOODIES         TXT       9  <NO DATE>
 TK.DATA         TXT      14  11-NOV-87
 SPRITES1        TXT       4  <NO DATE>
 SPRITES2        TXT      14  <NO DATE>
 MATH.SYSTEM     SYS       3   6-OCT-87
 QUIT            BIN       1   6-OCT-87
 VIEW            TXT      10  12-NOV-87
 CUST            TXT       7   5-JAN-88
 PACK            TXT       3  11-NOV-87
 NAME            TXT       4  12-NOV-87
 SPRITES1.1      TXT       4  <NO DATE>
 PRODOS          SYS      32  <NO DATE>
 TK.ABS          BIN      36  <NO DATE>
 TEST.FONT       BIN       7  <NO DATE>
 GETPIC          TXT       3  11-NOV-87
 EMENU           TXT       5  <NO DATE>
 TK.DATA1        TXT       1  <NO DATE>
 EDTEXT          TXT       3  19-OCT-87
 ENTRY.PIC       TXT       7  <NO DATE>
 MBP.CERT1       TXT       9  <NO DATE>
 MATH.BLASTER    BIN      85  12-NOV-87
 CERT            TXT      10  11-NOV-87
 PRINTER.DRIVERS TXT      12  <NO DATE>
 PRINTER.DATA    TXT       1   1-JAN-87
 INTER.DRIVERS   TXT       5  <NO DATE>
 EDIT            TXT      31   9-MAY-88
 DUMP.HIRES.R    BIN       5  <NO DATE>
 CTEXT           TXT       1   2-OCT-87
 ENTR            TXT       3  <NO DATE>
 DATA            DIR       3   7-MAR-62
 FINDER.DATA     $C9       3   7-MAR-62</code>
      <code>BLOCKS FREE: 1075     BLOCKS USED:  525</code>
    </div>
    <div class="line_editing">
      <code>]PREFIX /MATH.0
]-MATH.SYSTEM
...works...</code>
    </div>
    <p class="seen">OK, I can boot from my hard drive, then run the program successfully from the original disk. Whatever copy protection there is, it's not dependent on the PRODOS file.</p>
    <div class="curses_input">
      <code>[S5,D1=non-working copy]</code>
    </div>
    <div class="line_editing">
      <code>]PR#7
]PREFIX /MATH.0
]-MATH.SYSTEM
...quits via ProDOS quit handler...</code>
    </div>
    <p class="seen">Time to start tracing MATH.SYSTEM.</p>
    <div class="line_editing">
      <code>]PR#7
]PREFIX /MATH.0
]BLOAD MATH.SYSTEM,A$2000,TSYS
]CALL -151
*2000L</code>
    </div>
    <div class="listing">
      <code>2000-   A2 00       LDX   #$00
2002-   BD 1A 20    LDA   $201A,X
2005-   9D 01 08    STA   $0801,X
2008-   BD 1A 21    LDA   $211A,X
200B-   9D 01 09    STA   $0901,X
200E-   BD 1A 22    LDA   $221A,X
2011-   9D 01 0A    STA   $0A01,X
2014-   E8          INX
2015-   D0 EB       BNE   $2002
2017-   4C 01 08    JMP   $0801</code>
    </div>
    <div class="line_editing">
      <code>*2017:60
*2000G
*801L</code>
    </div>
    <div class="listing">
      <code>0801-   4C 52 08    JMP   $0852</code>
    </div>
    <div class="line_editing">
      <code>*852L
.
. ProDOS-y initialization
.
; subroutine gets file info via ProDOS
; MLI (command $C4) -- filename is at
; the address pointed to by (X/A)
087D-   A9 5B       LDA   #$5B
087F-   A2 0A       LDX   #$0A
0881-   20 C4 09    JSR   $09C4</code>
    </div>
    <p class="seen">The string at $0A5B is "MATH.BLASTER".</p>
    <div class="listing">
      <p class="comment">open the "MATH.BLASTER" file (MLI command $C8), but at $4060 instead of its default starting address ($0860, according to an extended catalog listing)</p>
      <code>0884-   A9 60       LDA   #$60
0886-   8D 8F 0A    STA   $0A8F
0889-   A9 40       LDA   #$40
088B-   8D 90 0A    STA   $0A90
088E-   20 D9 09    JSR   $09D9</code>
      <p class="comment">get file EOF (MLI $D1)</p>
      <code>0891-   20 01 0A    JSR   $0A01</code>
      <p class="comment">read part of the file (MLI $CA)</p>
      <code>0894-   A9 A0       LDA   #$A0
0896-   85 64       STA   $64
0898-   A9 17       LDA   #$17
089A-   85 65       STA   $65
089C-   AD 8C 0A    LDA   $0A8C
089F-   8D 29 09    STA   $0929
08A2-   AD 8D 0A    LDA   $0A8D
08A5-   8D 2A 09    STA   $092A
08A8-   20 1F 0A    JSR   $0A1F</code>
      <code>08AB-   20 4E 09    JSR   $094E</code>
    </div>
    <div class="line_editing">
      <code>*94EL</code>
    </div>
    <div class="listing">
      <p class="comment">copy to aux memory</p>
      <code>094E-   A9 60       LDA   #$60
0950-   85 3C       STA   $3C
0952-   85 42       STA   $42
0954-   A9 40       LDA   #$40
0956-   85 3D       STA   $3D
0958-   A9 08       LDA   #$08
095A-   85 43       STA   $43
095C-   A9 00       LDA   #$00
095E-   85 3E       STA   $3E
0960-   A9 58       LDA   #$58
0962-   85 3F       STA   $3F
0964-   38          SEC
0965-   4C 11 C3    JMP   $C311</code>
    </div>
    <p class="seen">According to "Inside the Apple //e" (pp. 296-8), $C311 copies data from main memory to aux memory and back. (Aux memory is what you get by having an 80-column card, 128K instead of 64.)</p>
    <p class="seen">The routine itself takes 4 parameters:</p>
    <div class="line_editing">
      <code>  ($3C/$3D) starting address
  ($3E/$3F) ending address
  ($42/$43) destination address in the
            other memory bank
  carry bit set for main->aux copy, or
            clear for aux->main copy</code>
    </div>
    <p class="seen">So this is copying $4060..$5800 in main memory (from the "MATH.BLASTER" file we just read) to $0860 in aux memory.</p>
    <div class="line_editing">
      <code>*8AEL</code>
    </div>
    <div class="listing">
      <p class="comment">set the mark within the open "MATH.BLASTER" file (MLI $CE)</p>
      <code>08AE-   20 2B 09    JSR   $092B</code>
      <p class="comment">read more of the file (MLI $CA)</p>
      <code>08B1-   A9 00       LDA   #$00
08B3-   85 60       STA   $60
08B5-   85 64       STA   $64
08B7-   A9 40       LDA   #$40
08B9-   85 61       STA   $61
08BB-   A9 50       LDA   #$50
08BD-   85 65       STA   $65
08BF-   20 1F 0A    JSR   $0A1F</code>
      <p class="comment">and copy that part to aux memory</p>
      <code>08C2-   A9 00       LDA   #$00
08C4-   85 3C       STA   $3C
08C6-   85 42       STA   $42
08C8-   85 3E       STA   $3E
08CA-   A9 40       LDA   #$40
08CC-   85 3D       STA   $3D
08CE-   85 43       STA   $43
08D0-   A9 90       LDA   #$90
08D2-   85 3F       STA   $3F
08D4-   38          SEC
08D5-   20 11 C3    JSR   $C311</code>
      <p class="comment">check if we're at the end of the file yet (I assume because this is a generic routine) -- on this disk it always returns #$FF</p>
      <code>08D8-   20 13 09    JSR   $0913</code>
      <p class="comment">will not branch</p>
      <code>08DB-   F0 2D       BEQ   $090A</code>
      <p class="comment">read more from the file</p>
      <code>08DD-   A9 20       LDA   #$20
08DF-   85 65       STA   $65
08E1-   A9 00       LDA   #$00
08E3-   85 60       STA   $60
08E5-   A9 FF       LDA   #$FF
08E7-   85 64       STA   $64
08E9-   A9 40       LDA   #$40
08EB-   85 61       STA   $61
08ED-   20 1F 0A    JSR   $0A1F</code>
      <p class="comment">and copy it to aux memory</p>
      <code>08F0-   A9 00       LDA   #$00
08F2-   85 3C       STA   $3C
08F4-   85 42       STA   $42
08F6-   A9 FF       LDA   #$FF
08F8-   85 3E       STA   $3E
08FA-   A9 40       LDA   #$40
08FC-   85 3D       STA   $3D
08FE-   A9 90       LDA   #$90
0900-   85 43       STA   $43
0902-   A9 60       LDA   #$60
0904-   85 3F       STA   $3F
0906-   38          SEC
0907-   20 11 C3    JSR   $C311</code>
      <p class="comment">close the file (MLI $CC)</p>
      <code>090A-   20 47 0A    JSR   $0A47</code>
      <p class="comment">copy more bits to aux memory</p>
      <code>090D-   20 68 09    JSR   $0968</code>
      <code>0910-   4C 80 09    JMP   $0980</code>
    </div>
    <div class="line_editing">
      <code>*980L</code>
    </div>
    <div class="listing">
      <p class="comment">copy even more bits to aux memory</p>
      <code>0980-   A9 00       LDA   #$00
0982-   85 3C       STA   $3C
0984-   85 42       STA   $42
0986-   A9 08       LDA   #$08
0988-   85 3D       STA   $3D
098A-   85 43       STA   $43
098C-   A9 5F       LDA   #$5F
098E-   85 3E       STA   $3E
0990-   A9 08       LDA   #$08
0992-   85 3F       STA   $3F
0994-   38          SEC
0995-   20 11 C3    JSR   $C311</code>
      <p class="comment">subroutine to read an entire file -- filename is at the address pointed to by (X/A), $0A68, which is "TK.ABS"</p>
      <code>0998-   A9 68       LDA   #$68
099A-   A2 0A       LDX   #$0A
099C-   20 B5 09    JSR   $09B5</code>
      <p class="comment">read file "TEST.FONT"</p>
      <code>099F-   A9 6F       LDA   #$6F
09A1-   A2 0A       LDX   #$0A
09A3-   20 B5 09    JSR   $09B5</code>
      <p class="comment">set up a jump to AUX memory (see "Inside the Apple //e", p. 300) jump address ($0860) is stored in $03ED/$03EE</p>
      <code>09A6-   A9 60       LDA   #$60
09A8-   8D ED 03    STA   $03ED
09AB-   A9 08       LDA   #$08
09AD-   8D EE 03    STA   $03EE</code>
      <p class="comment">use main memory's stack and zero page</p>
      <code>09B0-   B8          CLV</code>
      <p class="comment">transfer control from main to aux mem</p>
      <code>09B1-   38          SEC</code>
      <p class="comment">call XFER routine</p>
      <code>09B2-   4C 14 C3    JMP   $C314</code>
    </div>
    <div class="line_editing">
      <code>*9B2:4C 59 FF
*801G
<beep></code>
    </div>
    <p class="seen">Execution reaches here, even on my non- working copy. I haven't found the copy protection routine yet.</p>
</section>

<section>
  <h2>Chapter 2</h2>
  <h3>FORTH? In *My* Apple II? It's More Likely Than You Think</h3>

    <p class="seen">Execution continues in auxiliary memory at $0860 (set up at $09A6). This code was loaded from the "MATH.BLASTER" file at $4060 then moved to aux memory.</p>
    <p class="seen">Patching it may be tricky (if it comes to that), but I can load it up in main memory and take a look.</p>
    <div class="line_editing">
      <code>]BLOAD MATH.BLASTER,A$860</code>
    </div>
    <div class="terminal_output">
      <code>NO BUFFERS AVAILABLE</code>
    </div>
    <p class="seen">Ah, because the file itself is too big to be loaded all at once. (It's over $C000 bytes long.) OK, let's reuse the partial loader in MATH.SYSTEM.</p>
    <div class="line_editing">
      <code>*8AB:60    ; put RTS after first loader
*87DG      ; load part of file at $4060</code>
      <code>*860<4060.57FFM  ; move code into place</code>
      <code>*860L</code>
    </div>
    <div class="listing">
      <code>0860-   4C BE 18    JMP   $18BE</code>
    </div>
    <div class="line_editing">
      <code>*18BEL</code>
    </div>
    <div class="listing">
      <p class="comment">zero page initialization</p>
      <code>18BE-   A9 19       LDA   #$19
18C0-   85 19       STA   $19
18C2-   A9 68       LDA   #$68
18C4-   85 18       STA   $18</code>
      <p class="comment">check for 80-column card using bit 1 of the machine ID (set by ProDOS)</p>
      <code>18C6-   A9 02       LDA   #$02
18C8-   2C 98 BF    BIT   $BF98
18CB-   F0 05       BEQ   $18D2</code>
      <p class="comment">switch to 80-column mode</p>
      <code>18CD-   A9 00       LDA   #$00
18CF-   20 00 C3    JSR   $C300</code>
      <p class="comment">is DELETE key held down on boot?</p>
      <code>18D2-   AD 00 C0    LDA   $C000
18D5-   AC 10 C0    LDY   $C010
18D8-   C9 FF       CMP   #$FF</code>
      <p class="comment">if yes, branch over a bunch of stuff (I tried this and the program crashed -- maybe it hooks into some external routines that the developers had installed?)</p>
      <code>18DA-   F0 6E       BEQ   $194A</code>
      <p class="comment">other initialization</p>
      <code>18DC-   A0 FF       LDY   #$FF
18DE-   84 32       STY   $32
18E0-   AD DF 0E    LDA   $0EDF
18E3-   8D EA 0E    STA   $0EEA
18E6-   AD E0 0E    LDA   $0EE0
18E9-   8D EB 0E    STA   $0EEB
18EC-   A9 00       LDA   #$00
18EE-   A2 11       LDX   #$11
18F0-   9D A3 08    STA   $08A3,X
18F3-   CA          DEX
18F4-   10 FA       BPL   $18F0</code>
      <p class="comment">$0885 is a wrapper around the ProDOS MLI, and this command ($CC) closes all open files and flushes everything</p>
      <code>18F6-   A9 CC       LDA   #$CC
18F8-   8D 8A 08    STA   $088A
18FB-   E8          INX
18FC-   8E 92 08    STX   $0892
18FF-   E8          INX
1900-   8E 91 08    STX   $0891
1903-   20 85 08    JSR   $0885</code>
      <p class="comment">set the Ctrl-Y vector</p>
      <code>1906-   A9 4C       LDA   #$4C
1908-   8D F8 03    STA   $03F8
190B-   A9 4A       LDA   #$4A
190D-   8D F9 03    STA   $03F9
1910-   A9 19       LDA   #$19
1912-   8D FA 03    STA   $03FA</code>
      <p class="comment">set reset vector</p>
      <code>1915-   A9 BE       LDA   #$BE
1917-   8D F2 03    STA   $03F2
191A-   A9 18       LDA   #$18
191C-   8D F3 03    STA   $03F3
191F-   20 6F FB    JSR   $FB6F</code>
      <p class="comment">clear system bitmap (tracks which memory pages are in use)</p>
      <code>1922-   A9 00       LDA   #$00
1924-   A2 17       LDX   #$17
1926-   9D 58 BF    STA   $BF58,X
1929-   CA          DEX
192A-   10 FA       BPL   $1926
192C-   AD 84 08    LDA   $0884
192F-   8D FD BF    STA   $BFFD
1932-   AD 6C 08    LDA   $086C
1935-   8D 38 18    STA   $1838
1938-   AD 6D 08    LDA   $086D
193B-   8D 39 18    STA   $1839
193E-   A0 17       LDY   #$17
1940-   D0 0A       BNE   $194C</code>
      <p class="comment">[always skipped] 1942- A9 18 LDA #$18 1944- 85 19 STA $19 1946- A9 9B LDA #$9B 1948- 85 18 STA $18 194A- A0 0F LDY #$0F</p>
      <p class="comment">memory move</p>
      <code>194C-   AD 70 08    LDA   $0870
194F-   85 86       STA   $86
1951-   AD 71 08    LDA   $0871
1954-   85 87       STA   $87
1956-   B9 6C 08    LDA   $086C,Y
1959-   91 86       STA   ($86),Y
195B-   88          DEY
195C-   10 F8       BPL   $1956
195E-   D8          CLD
195F-   A9 6C       LDA   #$6C
1961-   85 1A       STA   $1A
1963-   4C 0D 0C    JMP   $0C0D</code>
    </div>
    <div class="line_editing">
      <code>*C0DL</code>
    </div>
    <div class="listing">
      <p class="comment">stack fiddling</p>
      <code>0C0D-   86 88       STX   $88
0C0F-   A0 08       LDY   #$08
0C11-   B1 86       LDA   ($86),Y
0C13-   AA          TAX
0C14-   9A          TXS
0C15-   A6 88       LDX   $88
0C17-   4C D5 08    JMP   $08D5</code>
    </div>
    <div class="line_editing">
      <code>*8D5L</code>
    </div>
    <div class="listing">
      <p class="comment">ah, ($18) is an address, initialized as $1968 (at $18BE)</p>
      <code>08D5-   A0 01       LDY   #$01</code>
      <p class="comment">get the word at ($18) and put it in zero page $1B and $1C</p>
      <code>08D7-   B1 18       LDA   ($18),Y
08D9-   85 1C       STA   $1C
08DB-   88          DEY
08DC-   B1 18       LDA   ($18),Y
08DE-   85 1B       STA   $1B</code>
      <p class="comment">increment ($18) by 2</p>
      <code>08E0-   18          CLC
08E1-   A5 18       LDA   $18
08E3-   69 02       ADC   #$02
08E5-   85 18       STA   $18
08E7-   90 02       BCC   $08EB
08E9-   E6 19       INC   $19</code>
      <p class="comment">zero page $1A is $6C (set at $195F), so this is all an indirect jump to the address listed at $1968</p>
      <code>08EB-   4C 1A 00    JMP   $001A</code>
    </div>
    <div class="line_editing">
      <code>*1968.1969
1968- 1D 1B</code>
      <code>*1B1D.1B1E
1B1D- F2 0D</code>
    </div>
    <p class="seen">Execution continues at $0DF2.</p>
    <div class="line_editing">
      <code>*DF2L</code>
    </div>
    <div class="listing">
      <p class="comment">fiddling with the addresses again ($1B) and ($18)</p>
      <code>0DF2-   A5 19       LDA   $19
0DF4-   48          PHA
0DF5-   A5 18       LDA   $18
0DF7-   48          PHA
0DF8-   18          CLC
0DF9-   A5 1B       LDA   $1B
0DFB-   69 02       ADC   #$02
0DFD-   85 18       STA   $18
0DFF-   98          TYA
0E00-   65 1C       ADC   $1C
0E02-   85 19       STA   $19</code>
      <p class="comment">and jump back to...$08D5</p>
      <code>0E04-   4C D5 08    JMP   $08D5</code>
    </div>
    <p class="seen">Oh no. I just figured this out. This is not the code I'm looking for. This is the code that interprets the code I'm looking for. This is an interpreter.</p>
    <p class="seen">Now what?</p>
    <p class="seen">I've had some limited success with reverse engineering Pascal p-code, but that was</p>
    <p class="seen">1. painful,</p>
    <p class="seen">2. only possible because p-code is well known and meticulously documented and I already knew</p>
    <p class="seen">Pascal, and</p>
    <p class="seen">3. still painful</p>
    <p class="seen">The about box says it was written in</p>
    <p class="seen">Forth. I don't know any Forth. I've never used Forth on any computer, 8-bit or otherwise. There were apparently several different versions of Forth on the Apple II, but I don't know which version this disk uses.</p>
    <p class="seen">Let's regroup.</p>
</section>

<section>
  <h2>Chapter 3</h2>
  <h3>In Which We Regroup, Take Stock, And Continue From First Principles</h3>

    <p class="seen">Here's what I know:</p>
    <p class="seen">* The program runs (from the original disk) even if I first boot ProDOS from my hard drive. That's not even the same version of ProDOS as the original disk.</p>
    <p class="seen">* Other disks by this publisher (e.g. #478 Word Attack Plus! Spanish) have similar disk characteristics (single unreadable block) and behavior (copies quit via ProDOS program selector).</p>
    <p class="seen">* Under ProDOS, "quitting via the program selector" and "reading a raw sector" are accomplished by sending commands to the ProDOS MLI.</p>
    <p class="seen">* All calls to the ProDOS MLI route through the same entry point ($BF00).</p>
    <p class="seen">This gives me an idea. Since I can run this program even after booting ProDOS from my hard drive, maybe I can also install a little logging program that lets me see ProDOS MLI calls as they whiz by.</p>
    <div class="line_editing">
      <code>]PR#7
]CALL -151</code>
      <code>*BF00L</code>
    </div>
    <div class="listing">
      <code>BF00-   4C 4B BF    JMP   $BF4B</code>
    </div>
    <p class="seen">OK, at the end of my routine, I'll need to jump to $BF4B to get to the real MLI entry point.</p>
    <div class="listing">
      <p class="comment">get, print, and store the top address on the stack</p>
      <code>0100-   BA          TSX</code>
      <p class="comment">high byte</p>
      <code>0101-   BD 02 01    LDA   $0102,X
0104-   8D 22 01    STA   $0122
0107-   20 DA FD    JSR   $FDDA</code>
      <p class="comment">low byte</p>
      <code>010A-   BD 01 01    LDA   $0101,X
010D-   8D 21 01    STA   $0121
0110-   20 DA FD    JSR   $FDDA</code>
      <p class="comment">print a space</p>
      <code>0113-   A9 A0       LDA   #$A0
0115-   20 F0 FD    JSR   $FDF0</code>
      <p class="comment">increment the address we got earlier (and put in $0121/$0122)</p>
      <code>0118-   EE 21 01    INC   $0121
011B-   D0 03       BNE   $0120
011D-   EE 22 01    INC   $0122</code>
      <p class="comment">this line of code will now load the MLI command that was passed to the ProDOS MLI (it's the byte immediately after the JSR to the MLI entry point)</p>
      <code>0120-   AD FF FF    LDA   $FFFF</code>
      <p class="comment">print that too</p>
      <code>0123-   20 DA FD    JSR   $FDDA</code>
      <p class="comment">print a carriage return</p>
      <code>0126-   A9 8D       LDA   #$8D
0128-   20 F0 FD    JSR   $FDF0</code>
      <p class="comment">wait for a key</p>
      <code>012B-   2C 10 C0    BIT   $C010
012E-   AD 00 C0    LDA   $C000
0131-   10 FB       BPL   $012E</code>
      <p class="comment">jump to real MLI</p>
      <code>0133-   4C 4B BF    JMP   $BF4B</code>
    </div>
    <div class="line_editing">
      <code>*PREFIX /MATH.0
*BLOAD MATH.SYSTEM,A$2000,TSYS</code>
      <p class="comment">route all MLI calls through my logger</p>
      <code>*BF00:4C 00 01</code>
      <p class="comment">run the program</p>
      <code>*2000G</code>
    </div>
    <p class="seen">I've annotated the following output with the name of each MLI command. The logger only outputs the two columns of hex (stack address + command ID).</p>
    <div class="terminal_output">
      <code>09D1 C4          ; GET_FILE_INFO
09F4 C8          ; OPEN
0A0D D1          ; GET_EOF
0A3F CA          ; READ
0946 CE          ; SET_MARK
0A3F CA          ; READ
0A3F CA          ; READ
0A53 CC          ; CLOSE
09D1 C4          ; GET_FILE_INFO
09F4 C8          ; OPEN
0A0D D1          ; GET_EOF
0A3F CA          ; READ
0A53 CC          ; CLOSE
09D1 C4          ; GET_FILE_INFO
09F4 C8          ; OPEN
0A0D D1          ; GET_EOF
0A3F CA          ; READ
0A53 CC          ; CLOSE</code>
    </div>
    <div class="curses_input">
      <code>[...clears screen in 80-column mode and
continues...]</code>
    </div>
    <div class="terminal_output">
      <code>0889 CC          ; CLOSE
0889 80          ; READ_BLOCK     <-- !
0889 C8          ; OPEN
0889 D1          ; GET_EOF
0889 CA          ; READ
0889 CD          ; FLUSH
4053 65          ; QUIT</code>
    </div>
    <p class="seen">Several interesting things here. I've stepped through some of the initial commands already, so they come as no surprise. I know that it loads three files, "MATH.BLASTER", "TK.ABS", and "TEST.FONT". I even know where it switches to 80-column mode ($18CF).</p>
    <p class="seen">But that's where it gets interesting.</p>
    <p class="seen">After it switches to 80-column mode, every stack address is the same ($0889) except the final QUIT command ($4059).</p>
    <p class="seen">So all of those MLI calls are coming from inside the interpreter, presumably some MLI wrapper function.</p>
    <p class="seen">Also, that READ_BLOCK (command $80) is very suspicious. That's a raw sector read (really two sectors, since ProDOS does everything by blocks). So, it</p>
    <p class="seen">1. issues a READ_BLOCK MLI call, then</p>
    <p class="seen">2. opens/reads/closes a file, then</p>
    <p class="seen">3. quits at $4053</p>
    <p class="seen">Also, there is a file named "QUIT" on this disk which loads at $4000. If that isn't all connected, it would be a coincidence of magnificent proportions.</p>
</section>

<section>
  <h2>Chapter 4</h2>
  <h3>Success Is Failure, Failure Is Success, Black Is White, Night Is Day, Teaching Is Dead</h3>

    <p class="seen">I have another crazy idea: upgrade my logger to modify the MLI command on the fly. That is, if the command on entry is $80 (READ_BLOCK), change it to something else, like $CC (CLOSE). Why CLOSE? Because it will always fail. No, really. READ_BLOCK takes 3 parameters; CLOSE takes 1. The first thing the MLI does is check whether the parameter count is correct for the given command; if not, it exits immediately with return code $02.</p>
    <p class="seen">(And remember, I want the READ_BLOCK command to fail. On the original disk, it fails because of the intentionally bad sector on track $22. Failure is success.)</p>
    <div class="line_editing">
      <code>]PR#7
]CALL -151</code>
    </div>
    <div class="listing">
      <p class="comment">get the top stack address and put it in $0116/$0117</p>
      <code>0100-   BA          TSX
0101-   BD 02 01    LDA   $0102,X
0104-   8D 17 01    STA   $0117
0107-   BD 01 01    LDA   $0101,X
010A-   8D 16 01    STA   $0116</code>
      <p class="comment">increment the address we got from the stack (does not touch the stack itself, just our local copy)</p>
      <code>010D-   EE 16 01    INC   $0116
0110-   D0 03       BNE   $0115
0112-   EE 17 01    INC   $0117</code>
      <p class="comment">now this instruction will load the MLI command</p>
      <code>0115-   AD FF FF    LDA   $FFFF</code>
      <p class="comment">is it READ_BLOCK?</p>
      <code>0118-   C9 80       CMP   #$80</code>
      <p class="comment">no, branch without changing anything</p>
      <code>011A-   D0 11       BNE   $012D</code>
      <p class="comment">yes, change the MLI command to CLOSE by modifying the STA instruction at $012A</p>
      <code>011C-   AD 16 01    LDA   $0116
011F-   8D 2B 01    STA   $012B
0122-   AD 17 01    LDA   $0117
0125-   8D 2C 01    STA   $012C
0128-   A9 CC       LDA   #$CC
012A-   8D FF FF    STA   $FFFF</code>
      <p class="comment">call the real MLI</p>
      <code>012D-   4C 4B BF    JMP   $BF4B</code>
    </div>
    <div class="line_editing">
      <code>*PREFIX /MATH.0
*BLOAD MATH.SYSTEM,A$2000,TSYS</code>
      <p class="comment">set the trap</p>
      <code>*BF00:4C 00 01</code>
      <p class="comment">execute the program</p>
      <code>*2000G
...works, and it is glorious...</code>
    </div>
    <p class="seen">Now I have a way to get a copy to load.</p>
    <p class="seen">But how should I make this change permanent? I could install this MLI trap on boot and literally route every MLI command through it, changing any READ_BLOCK commands on the fly. But I'm not 100% sure that there aren't other legitimate uses of READ_BLOCK buried deep within the program. I'm also not 100% sure that the trap won't get overwritten if the stack gets too big, and I'm not sure where else I could put it in memory that wouldn't ever be used during the entire lifetime of the program. It all just feels inelegant.</p>
    <p class="seen">It's not a solution; it's a prototype.</p>
    <p class="seen">A real solution would be to find the interpreted code that calls that MLI function wrapper (near $889) with the READ_BLOCK command and checks for its expected failure. Then I can either change the if-then logic, or NOP it out somehow, or possibly just change that one READ_BLOCK command to CLOSE. Like my prototype, but without affecting every single MLI call and without taking up any extra memory.</p>
    <p class="seen">OK, that's what I want to do. Find the interpreted code that ends up issuing the READ_BLOCK MLI call, and change the MLI command from $80 to $CC. A one byte patch. Let's go.</p>
</section>

<section>
  <h2>Chapter 5</h2>
  <h3>One Byte To Rule Them All, And In The Darkness Patch Them</h3>

    <div class="line_editing">
      <code>]PR#7
]CALL -151</code>
    </div>
    <div class="listing">
      <p class="comment">get the top stack address and put it in $0122/$0123</p>
      <code>0100-   BA          TSX
0101-   BD 02 01    LDA   $0102,X
0104-   8D 23 01    STA   $0123
0107-   BD 01 01    LDA   $0101,X
010A-   8D 22 01    STA   $0122</code>
      <p class="comment">increment the address I just copied from the stack</p>
      <code>010D-   EE 22 01    INC   $0122
0110-   D0 03       BNE   $0115
0112-   EE 23 01    INC   $0123</code>
      <p class="comment">print the address of the current opcode, which is stored in ($18)</p>
      <code>0115-   A5 19       LDA   $19
0117-   20 DA FD    JSR   $FDDA
011A-   A5 18       LDA   $18
011C-   20 DA FD    JSR   $FDDA
011F-   EA          NOP
0120-   EA          NOP</code>
      <p class="comment">check the MLI command</p>
      <code>0121-   AD FF FF    LDA   $FFFF</code>
      <p class="comment">is it READ_BLOCK?</p>
      <code>0124-   C9 80       CMP   #$80</code>
      <p class="comment">no, branch</p>
      <code>0126-   D0 03       BNE   $012B</code>
      <p class="comment">yes, break</p>
      <code>0128-   4C 59 FF    JMP   $FF59</code>
      <p class="comment">continue to real MLI (everything other than READ_BLOCK command)</p>
      <code>012B-   4C 4B BF    JMP   $BF4B</code>
    </div>
    <div class="line_editing">
      <code>*PREFIX /MATH.0
*BLOAD MATH.SYSTEM,A$2000,TSYS</code>
      <p class="comment">set the trap</p>
      <code>*BF00:4C 00 01</code>
      <p class="comment">and go</p>
      <code>*2000G</code>
    </div>
    <div class="curses_input">
      <code>[output before 80-column mode omitted]
1968
73B6
<beep></code>
    </div>
    <p class="seen">The interpreter is at $73B6 when it issues the raw block read command. That may not be the actual MLI command ID.</p>
    <p class="seen">In fact, it's almost certainly not, since you need to pass a bunch of other parameters with a READ_COMMAND MLI call (unit number, data address, and block number). But it's probably nearby.</p>
    <div class="line_editing">
      <code>*73A0.73CF</code>
    </div>
    <div class="listing">
      <code>73A0- 81 0E 05 41 D1 0D 97 0E
73A8- 09 1D D1 0D BB 08 80 00
73B0- FD 1C D1 0D EC 1C 1F 0C
73B8- 88 47 45 54 5F D5 88 73
73C0- F2 0D BB 08 30 BF AA 0D
73C8- 1F 0C 8A 44 45 56 5F CF</code>
    </div>
    <p class="seen">Bingo. I see an $80 byte at $73AE. I bet that's the MLI command in the interpreted code.</p>
    <p class="seen">Turning to my trusty Block Warden block editor, I look for the surrounding opcodes.</p>
    <p class="seen">Block Warden</p>
    <p class="seen">["C"hange device -> S5,D1] ["E"dit mode] [<Ctrl-S>earch] ["$08 80 00 FD 1C D1 0D EC"]</p>
    <p class="seen">It finds two matches.</p>
    <p class="seen">Block $0136,offset $14E change 80 to CC</p>
    <p class="seen">Block $0196,offset $027 change 80 to CC</p>
    <div class="line_editing">
      <code>]PR#5
...works, and it is glorious...</code>
    </div>
    <p class="seen">Quod erat liberandum.</p>
</section>

</body></html>
