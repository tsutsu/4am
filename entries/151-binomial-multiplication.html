<!doctype html>
<html><head>
<title>Binomial Multiplication</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
</head><body>

<p>Previous: <a rel="previous" href="./150-safari-search.html">Safari Search</a></p>
<section>

    <p>"Binomial Multiplication" is a 1984 educational program programmed by Mark</p>
    <p>Berman and Kevin Vessio, designed by</p>
    <p>Don Ross, and distributed by Mindscape.</p>
    <p>COPYA reads the entire disk without complaint, but the copy does not boot.</p>
    <p>It sounds like it's loading DOS from tracks 0-2, then it swings out to a high track and stays there forever.</p>
    <p class="seen">Time for boot tracing with AUTOTRACE.</p>
    <div class="curses_input">
      <code>[S6,D1=original disk]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...
CAPTURING BOOT0
...reboots slot 6...
...reboots slot 5...
SAVING BOOT0
CAPTURING BOOT1
...reboots slot 6...
...reboots slot 5...
SAVING BOOT1
SAVING RWTS</code>
    </div>
    <p class="seen">For those of you just tuning in, my work disk uses a custom program that I affectionately call "AUTOTRACE" to automate the process of boot tracing as far as possible. For some disks, this just captures track 0, sector 0 (saved in a file called "BOOT0") and stops.</p>
    <p class="seen">For other disks that load in the same way that an unprotected DOS 3.3 disk loads, it captures the next stage of the boot process as well (in a file called "BOOT1"). BOOT1 contains sectors 0-9 on track 0, which are loaded into memory at $B600..$BFFF. This generally contains the RWTS routines which the program uses to read the rest of the disk.</p>
    <p>Since this disk is readable by COPYA and appears to load DOS 3.3 or some variant, I wasn't expecting any major surprises. But you never know.</p>
    <p>I already know that boot0 is entirely normal, otherwise my AUTOTRACE program would have warned me. So let's start with boot1.</p>
    <div class="line_editing">
      <code>]BLOAD BOOT1,A$2600
]CALL -151</code>
      <code>*FE89G FE93G     ; disconnect DOS</code>
      <code>*B600<2600.2FFFM ; move RWTS into place</code>
      <code>*B700L
.
. all normal, until
.
B747-   4C 00 B4    JMP   $B400</code>
    </div>
    <p>That usually jumps to $9D84 to cold- start DOS and execute the HELLO program or whatever. $B400 isn't supposed to contain any executable code. ("Beneath</p>
    <p>Apple DOS" says it's somewhere in the middle of the VTOC sector buffer, used for reading the disk catalog.)</p>
    <p>What danger lurks at $B400?</p>
    <div class="line_editing">
      <code>*C500G        ; because I overwrote DOS
...</code>
      <code>*9600<C600.C6FFM</code>
    </div>
    <div class="listing">
      <code>96F8-   A9 4C       LDA   #$4C
96FA-   8D 4A 08    STA   $084A
96FD-   A9 0A       LDA   #$0A
96FF-   8D 4B 08    STA   $084B
9702-   A9 97       LDA   #$97
9704-   8D 4C 08    STA   $084C
9707-   4C 01 08    JMP   $0801
970A-   A9 4C       LDA   #$4C
970C-   8D 47 B7    STA   $B747
970F-   A9 1C       LDA   #$1C
9711-   8D 48 B7    STA   $B748
9714-   A9 97       LDA   #$97
9716-   8D 49 B7    STA   $B749
9719-   4C 00 B7    JMP   $B700
971C-   A2 23       LDX   #$23
971E-   A0 00       LDY   #$00
9720-   B9 00 9D    LDA   $9D00,Y
9723-   99 00 2D    STA   $2D00,Y
9726-   C8          INY
9727-   D0 F7       BNE   $9720
9729-   EE 22 97    INC   $9722
972C-   EE 25 97    INC   $9725
972F-   CA          DEX
9730-   D0 EE       BNE   $9720
9732-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE2,A$9600,L$135</code>
      <code>*9600G
...reboots slot 6...
...reboots slot 5...</code>
      <code>]BSAVE BOOT2,A$2D00,L$2300
]CALL -151</code>
      <code>*FE89G FE93G     ; disconnect DOS</code>
      <code>*9D00<2D00.4FFFM ; move DOS into place</code>
      <code>*B400L</code>
    </div>
    <div class="listing">
      <code>B400-   A0 00       LDY   #$00
B402-   B9 12 B4    LDA   $B412,Y
B405-   49 4C       EOR   #$4C
B407-   99 12 B4    STA   $B412,Y
B40A-   C8          INY
B40B-   C0 FF       CPY   #$FF
B40D-   D0 F3       BNE   $B402
B40F-   EA          NOP
B410-   EA          NOP
B411-   EA          NOP
B412-   E5 6F       SBC   $6F
B414-   C1 A0       CMP   ($A0,X)
B416-   FB          ???
B417-   E5 4C       SBC   $4C
B419-   C9 48       CMP   #$48
B41B-   C1 B8       CMP   ($B8,X)
B41D-   FB          ???
B41E-   C1 A7       CMP   ($A7,X)
B420-   FB          ???
B421-   6C A7 F8    JMP   ($F8A7)</code>
    </div>
    <p>Oh goody, self-decrypting code.</p>
    <p>Fortunately, it isn't too difficult to decrypt manually. The decryption loop ends at $B40E but the encrypted code doesn't start until $B412, so there's plenty of room to insert an RTS.</p>
    <div class="line_editing">
      <code>*B40F:60</code>
      <code>*B400G</code>
      <code>*B412L</code>
    </div>
    <div class="listing">
      <p class="comment">track $23</p>
      <code>B412-   A9 23       LDA   #$23
B414-   8D EC B7    STA   $B7EC</code>
      <p class="comment">don't know what this is for yet</p>
      <code>B417-   A9 00       LDA   #$00
B419-   85 04       STA   $04</code>
      <p class="comment">just seek, not read</p>
      <code>B41B-   8D F4 B7    STA   $B7F4
B41E-   8D EB B7    STA   $B7EB</code>
      <p class="comment">get the RWTS parameter table address and call the RWTS to seek</p>
      <code>B421-   20 EB B4    JSR   $B4EB
B424-   20 F2 B4    JSR   $B4F2</code>
      <p class="comment">reset DOS zero page</p>
      <code>B427-   A9 00       LDA   #$00
B429-   85 48       STA   $48</code>
      <p class="comment">turn on drive motor manually (always suspicious)</p>
      <code>B42B-   BD 89 C0    LDA   $C089,X
B42E-   BD 8E C0    LDA   $C08E,X</code>
      <p class="comment">read the next available address field</p>
      <code>B431-   20 44 B9    JSR   $B944</code>
      <p class="comment">compare the sector number we found to the desired sector (apparently 0, initialized at $B419)</p>
      <code>B434-   A5 2D       LDA   $2D
B436-   C5 04       CMP   $04</code>
      <p class="comment">loop until we find it</p>
      <code>B438-   D0 F7       BNE   $B431</code>
    </div>
    <p>That explains why my copy hung forever.</p>
    <div class="listing">
      <p class="comment">look for sync bytes</p>
      <code>B43A-   BD 8E C0    LDA   $C08E,X
B43D-   BD 8C C0    LDA   $C08C,X
B440-   10 FB       BPL   $B43D
B442-   C9 FF       CMP   #$FF
B444-   D0 F4       BNE   $B43A</code>
      <p class="comment">look for $D5 in a specific position</p>
      <code>B446-   A0 00       LDY   #$00
B448-   C8          INY
B449-   BD 8E C0    LDA   $C08E,X
B44C-   BD 8C C0    LDA   $C08C,X
B44F-   10 FB       BPL   $B44C
B451-   C9 D5       CMP   #$D5
B453-   D0 F3       BNE   $B448
B455-   C0 07       CPY   #$07
B457-   D0 B9       BNE   $B412</code>
      <p class="comment">do this for all 16 sectors</p>
      <code>B459-   E6 04       INC   $04
B45B-   A9 10       LDA   #$10
B45D-   C5 04       CMP   $04
B45F-   D0 D0       BNE   $B431</code>
      <p class="comment">back to sector 0 (still on track $23)</p>
      <code>B461-   A9 00       LDA   #$00
B463-   85 04       STA   $04
B465-   20 44 B9    JSR   $B944
B468-   A5 2D       LDA   $2D
B46A-   C5 04       CMP   $04
B46C-   D0 F7       BNE   $B465</code>
      <p class="comment">look for "AA EB"</p>
      <code>B46E-   BD 8C C0    LDA   $C08C,X
B471-   10 FB       BPL   $B46E
B473-   C9 AA       CMP   #$AA
B475-   D0 F7       BNE   $B46E
B477-   BD 8C C0    LDA   $C08C,X
B47A-   10 FB       BPL   $B477
B47C-   C9 EB       CMP   #$EB
B47E-   D0 EE       BNE   $B46E</code>
      <p class="comment">more sync bytes</p>
      <code>B480-   BD 8C C0    LDA   $C08C,X
B483-   10 FB       BPL   $B480
B485-   C9 FF       CMP   #$FF
B487-   D0 F7       BNE   $B480</code>
      <p class="comment">again $D5 in a specific position</p>
      <code>B489-   A0 00       LDY   #$00
B48B-   C8          INY
B48C-   BD 8E C0    LDA   $C08E,X
B48F-   BD 8C C0    LDA   $C08C,X
B492-   10 FB       BPL   $B48F
B494-   C9 D5       CMP   #$D5
B496-   D0 F3       BNE   $B48B
B498-   C0 10       CPY   #$10
B49A-   D0 C5       BNE   $B461</code>
      <p class="comment">do that on all 16 sectors</p>
      <code>B49C-   E6 04       INC   $04
B49E-   A9 0A       LDA   #$0A
B4A0-   20 A8 FC    JSR   $FCA8
B4A3-   A9 04       LDA   #$04
B4A5-   C5 04       CMP   $04
B4A7-   D0 04       BNE   $B4AD
B4A9-   E6 04       INC   $04
B4AB-   D0 B8       BNE   $B465
B4AD-   A9 0F       LDA   #$0F
B4AF-   C5 04       CMP   $04
B4B1-   D0 BB       BNE   $B46E</code>
      <p class="comment">seek to sector $0F (still track $23)</p>
      <code>B4B3-   20 44 B9    JSR   $B944
B4B6-   A5 2D       LDA   $2D
B4B8-   C9 0F       CMP   #$0F
B4BA-   D0 F7       BNE   $B4B3</code>
      <p class="comment">find and denibblize sector data</p>
      <code>B4BC-   20 DC B8    JSR   $B8DC</code>
      <p class="comment">find $A5 byte in a specific position</p>
      <code>B4BF-   A0 00       LDY   #$00
B4C1-   C8          INY
B4C2-   BD 8E C0    LDA   $C08E,X
B4C5-   BD 8C C0    LDA   $C08C,X
B4C8-   10 FB       BPL   $B4C5
B4CA-   C9 A5       CMP   #$A5
B4CC-   D0 F3       BNE   $B4C1
B4CE-   C0 2B       CPY   #$2B
B4D0-   D0 E1       BNE   $B4B3</code>
      <p class="comment">find $D5 byte in specific position</p>
      <code>B4D2-   A0 00       LDY   #$00
B4D4-   C8          INY
B4D5-   BD 8E C0    LDA   $C08E,X
B4D8-   BD 8C C0    LDA   $C08C,X
B4DB-   10 FB       BPL   $B4D8
B4DD-   C9 D5       CMP   #$D5
B4DF-   D0 F3       BNE   $B4D4
B4E1-   C0 5D       CPY   #$5D
B4E3-   D0 CE       BNE   $B4B3</code>
      <p class="comment">turn off drive motor</p>
      <code>B4E5-   BD 88 C0    LDA   $C088,X</code>
      <p class="comment">jump to DOS cold-start as usual</p>
      <code>B4E8-   4C 84 9D    JMP   $9D84</code>
    </div>
    <p class="seen">This nibble check has no side effects.</p>
    <p>If it fails at any point, it just keeps trying until it succeeds. This explains why my copy hung forever. It also means</p>
    <p>I can simply bypass this entire routine and patch boot1 to jump to $9D84.</p>
    <p>A lot of disks need this sort of patching, and I got tired of doing it manually, so I wrote a program to do it for me. It is called Post-Demuffin</p>
    <p>Patcher. (The name comes from the fact that most disks need to be converted to a standard disk format by a tool like</p>
    <p>Super Demuffin or Advanced Demuffin.</p>
    <p>This disk didn't need any conversion, so it's a bit of a misnomer. Life is full of edge cases.)</p>
    <p>PDP prompts you to select a slot and drive, then reads the demuffin'd disk, checks for a modified DOS 3.3-shaped RWTS, and applies the necessary patches so the disk can read itself. It also detects and bypasses some known nibble checks, like the one on this disk.</p>
    <p>I've included a copy of Post-Demuffin</p>
    <p>Patcher on my work disk; the full source code is currently available at <https://archive.org/details/ PostDemuffinPatcher4am>.</p>
    <div class="curses_input">
      <code>[S6,D1=demuffin'd copy]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...</code>
      <code>]BRUN PDP</code>
    </div>
    <p>T00,S01,$48 change 00B4 to 849D</p>
    <p class="seen">(This is the actual output of the program. Post-Demuffin Patcher prints out the changes it is going to make before it writes them to the disk.)</p>
    <p class="seen">I should point out that Post-Demuffin</p>
    <p>Patcher is really quite conservative in making patches. It checks a lot of the surrounding code before deciding to patch a specific location. In the case of bypassing this nibble check, it checks every single byte of code up to and including the JMP $B400, to ensure that the disk is using a standard DOS boot0 and boot1 routine up until that point. And there were dozens of patches that it didn't make to this disk (like RWTS patches), because it decided they weren't needed.</p>
    <div class="line_editing">
      <code>]PR#6</code>
    </div>
    <p class="seen">And it works. The disk boots and runs with no complaint. There doesn't appear to be any further protection. Hooray for automation. (Oh, this is gonna get good.)</p>
    <p class="seen">Quod erat liberandum.</p>
</section>

<p>Next: <a rel="next" href="./152-factoring-algebraic-expressions.html">Factoring Algebraic Expressions</a></p>
</body></html>
