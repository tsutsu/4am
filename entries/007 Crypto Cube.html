<!doctype html>
<html><head>
<title>Crypto Cube</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
</head><body>

<section>

    <p>Crypto Cube is an 1983 educational game distributed by DesignWare, Inc.</p>
    <p>The original disk uses a standard 6-2 nibble encoding and standard address and data prologue and epilogue sequences. COPYA barfs with a disk read error on track $0E. Locksmith 7.0 Fast</p>
    <p>Disk Backup also shows a read error on sector 3 of track $0E but finishes the copy anyway. EDD 4 bit copy shows no errors. In both cases (Locksmith and EDD), the duplicate disk boots, loads several tracks, then fails with an animated graphical message: "ERROR!</p>
    <p>Press RETURN to restart CRYPTO CUBE."</p>
    <p>There is no VTOC on track $11 (so no CATALOG). Listening to the original disk boot, there doesn't seem to be a standard DOS loaded from tracks 2, 1, then 0.</p>
    <p>It's time for boot tracing.</p>
    <div class="curses_input">
      <code>[S6D1=Crypto Cube original disk]
[S5D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
]CALL -151
*9600<C600.C6FFM
*96F8:AD E8 C0 4C 59 FF
*BSAVE TRACE0,A$9600,L$FE
*9600G
...
*2800<800.8FFM
*C500G
...
]BSAVE CRYPTO CUBE T0S0,A$2800,L$100
]BLOAD TRACE0
]CALL -151</code>
    </div>
    <div class="listing">
      <p class="comment">set up a callback to a routine that I control after the disk loads its RWTS into memory</p>
      <code>96F8-   A9 4C       LDA   #$4C
96FA-   8D 4A 08    STA   $084A
96FD-   A9 0A       LDA   #$0A
96FF-   8D 4B 08    STA   $084B
9702-   A9 97       LDA   #$97
9704-   8D 4C 08    STA   $084C
; let it boot
9707-   4C 01 08    JMP   $0801
; callback starts here
; copy RWTS to safe location that won't
; get overwritten when I reboot into my
; work disk
970A-   A2 07       LDX   #$07
970C-   A0 00       LDY   #$00
970E-   B9 00 B7    LDA   $B700,Y
9711-   99 00 27    STA   $2700,Y
9714-   C8          INY
9715-   D0 F7       BNE   $970E
9717-   EE 10 97    INC   $9710
971A-   EE 13 97    INC   $9713
971D-   CA          DEX
971E-   10 EE       BPL   $970E
; reboot into my work disk
9720-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE1,A$9600,L$123
*9600G
...
]BSAVE CRYPTO CUBE B700-BFFF,A$2700,
 L$900</code>
    </div>
    <p>After the RWTS is loaded, the original disk would normally jump to ($08FD), which points to $B700 by that point.</p>
    <div class="listing">
      <p class="comment">normal initialization stuff here, setting up the RWTS parameter table at $B7E8</p>
      <code>B700-   8E E9 B7    STX   $B7E9
B703-   8E F7 B7    STX   $B7F7
B706-   A9 01       LDA   #$01
B708-   8D F8 B7    STA   $B7F8
B70B-   8D EA B7    STA   $B7EA
B70E-   AD E0 B7    LDA   $B7E0
B711-   8D E1 B7    STA   $B7E1
B714-   A9 00       LDA   #$00
B716-   8D EC B7    STA   $B7EC
B719-   AD E2 B7    LDA   $B7E2
B71C-   8D ED B7    STA   $B7ED
B71F-   AD E3 B7    LDA   $B7E3
B722-   8D F1 B7    STA   $B7F1
B725-   A9 01       LDA   #$01
B727-   8D F4 B7    STA   $B7F4
B72A-   8A          TXA
B72B-   4A          LSR
B72C-   4A          LSR
B72D-   4A          LSR
B72E-   4A          LSR
B72E-   4A          LSR
B72F-   AA          TAX
B730-   A9 00       LDA   #$00
B732-   9D F8 04    STA   $04F8,X
B735-   9D 78 04    STA   $0478,X
; hey now, what's all this then?
B738-   20 00 BB    JSR   $BB00
B73B-   A2 FF       LDX   #$FF
B73D-   9A          TXS
B73E-   8E EB B7    STX   $B7EB
; standard patch that tests language
; card and jumps back to $B744
B741-   4C C8 BF    JMP   $BFC8
B744-   20 89 FE    JSR   $FE89
; jump to the start of the game?
B747-   4C 00 08    JMP   $0800</code>
    </div>
    <p>Interesting. It sets up an RWTS parameter table, but instead of calling the usual multi-sector read routine at $B793, it calls $BB00. I know from experience that $BB00 "ought" to be unused. A standard DOS 3.3 RWTS uses it as a temporary buffer for converting nibbles to bytes, which means it the entire range gets overwritten with every disk read. So any code in the $BBxx range is non-standard and therefore suspicious.</p>
    <div class="listing">
      <code>BB00-   86 1B       STX   $1B
; a neat trick to get the high byte of
; the return address from the stack
; without actually pulling it off the
; stack and pushing it back.
BB02-   BA          TSX
BB03-   BD 02 01    LDA   $0102,X
; $B7 --> zero page $03
BB06-   85 03       STA   $03
BB08-   18          CLC
BB09-   69 04       ADC   #$04
; $BB --> zero page $05
BB0B-   85 05       STA   $05
BB0D-   A0 00       LDY   #$00
BB0F-   84 02       STY   $02
BB11-   A0 40       LDY   #$40
BB13-   84 04       STY   $04
BB15-   A0 39       LDY   #$39
BB17-   A9 93       LDA   #$93
; $93 --> $B739
BB19-   91 02       STA   ($02),Y
BB1B-   C8          INY
BB1C-   A5 03       LDA   $03
; $B7 --> $B73A
BB1E-   91 02       STA   ($02),Y
; $BB40..$BBBF --> $0800..$087F
BB20-   A0 7F       LDY   #$7F
BB22-   B1 04       LDA   ($04),Y
BB24-   99 00 08    STA   $0800,Y
BB27-   88          DEY
BB28-   10 F8       BPL   $BB22
; restore X register and jump to
; relocated code
BB2A-   A6 1B       LDX   $1B
BB2C-   4C 00 08    JMP   $0800</code>
    </div>
    <p>Did you catch that? This subroutine erases any evidence that it was ever called, by changing the JSR instruction that called it from JSR $BB00 to JSR $B793. Then it relocates some other code to $0800 and jumps there. This is not innocent code. Innocent code does not cover its tracks to make it seem like it was never called. This code does not have my best interests at heart.</p>
    <p>Let's see what ends up at $0800.</p>
    <div class="line_editing">
      <code>*BB2C:4C 59 FF
*B738G
<beep></code>
    </div>
    <div class="listing">
      <code>0800-   A9 00       LDA   #$00
0802-   A0 EC       LDY   #$EC
; ($02) points to $B700 at this point,
; so this looks like it's setting up
; the RWTS parameter table again.
; $B7EC holds the track number (0).
0804-   91 02       STA   ($02),Y
0806-   C8          INY
; $B7ED holds the sector number (0).
0807-   91 02       STA   ($02),Y
0809-   A9 09       LDA   #$09
080B-   A0 F1       LDY   #$F1
; $B7F1 holds the destination address,
; so it looks like we're going to read
; a bunch of sectors from track 0
; into $0900 and up.
080D-   91 02       STA   ($02),Y
080F-   A9 08       LDA   #$08
0811-   A0 E1       LDY   #$E1
; $B7E1 holds the number of sectors
; to read (8).
0813-   91 02       STA   ($02),Y
; set up a JMP instruction at $01 (WTF)
0815-   A9 4C       LDA   #$4C
0817-   85 01       STA   $01
0819-   A9 93       LDA   #$93
081B-   85 02       STA   $02
; call $B793 to read multiple sectors
081D-   20 01 00    JSR   $0001</code>
    </div>
    <p>Despite the odd indirection, this part isn't suspicious at all. It's using the standard multi-read routine at $B793 to read the rest of track 0 into memory.</p>
    <p>I'm still waiting for the other shoe to drop...</p>
    <div class="listing">
      <code>0820-   A0 00       LDY   #$00
0822-   84 02       STY   $02
0824-   A5 1E       LDA   $1E
0826-   85 08       STA   $08
0828-   0A          ASL
0829-   90 01       BCC   $082C
082B-   C8          INY
082C-   48          PHA
082D-   68          PLA
082E-   D0 F8       BNE   $0828
0830-   C0 02       CPY   #$02
0832-   69 FF       ADC   #$FF
0834-   85 1F       STA   $1F</code>
    </div>
    <p>Wait a minute. Wait wait wait wait wait. The standard DOS 3.3 RWTS doesn't use zero page $1E, so at this point it should be uninitialized and essentially random. But this loop seems to be doing some bit math on it (specifically, counting how many bits are 0 and storing that in the Y register), so either something is setting it or I'm being punked. Am I being punked? That's always a distinct possibility. This code isn't innocent;</p>
    <p>I'm sure of it.</p>
    <p>Rebooting into my trusty Copy ][+ sector editor, I found a few instances of the hex value $1E in track 0. The only one that looked like actual RWTS code was T00,S0F,$3F. After some manual cross-referencing, it appears that this sector is loaded at $B900 by the RWTS loader bootstrap in sector 0. Here is the relevant code:</p>
    <div class="listing">
      <code>B925-   BC 8C C0    LDY   $C08C,X
B928-   10 FB       BPL   $B925
B92A-   D9 00 BA    CMP   $BA00,Y
B92D-   D0 13       BNE   $B942
B92F-   BD 8C C0    LDA   $C08C,X
B932-   10 FB       BPL   $B92F
B934-   B8          CLV
B935-   BD 8C C0    LDA   $C08C,X
B938-   10 FB       BPL   $B935
B93A-   B8          CLV
B93B-   1E 8C C0    ASL   $C08C,X
B93E-   26 1E       ROL   $1E
B940-   50 5C       BVC   $B99E
B942-   38          SEC
B943-   60          RTS</code>
    </div>
    <p>Normally, this code would be checking for the nibble sequence "DE AA", the standard epilogue after the data field. But instead, Crypto Cube accepts any two nibbles, then does some bit math to twiddle zero page $1E based on the timing bit immediately following.</p>
    <p>Since that relocated "definitely not innocent" routine at $0800 reads 8 sectors, zero page $1E gets twiddled 8 times, giving it a fully defined value despite never being initialized.</p>
    <p>Obviously the value in zero page $1E is important. Let's capture it.</p>
    <div class="line_editing">
      <code>]PR#5
]BLOAD TRACE1
]CALL -151</code>
    </div>
    <div class="listing">
      <code>970A-   A9 4C       LDA   #$4C
970C-   8D 60 BB    STA   $BB60
970F-   A9 1C       LDA   #$1C
9711-   8D 61 BB    STA   $BB61
9714-   A9 97       LDA   #$97
9716-   8D 62 BB    STA   $BB62
9719-   6C FD 08    JMP   ($08FD)
971C-   AD E8 C0    LDA   $C0E8
971F-   A5 1E       LDA   $1E
9721-   20 DA FD    JSR   $FDDA
9724-   4C 59 FF    JMP   $FF59</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE2,A$9600,L$127
*9600G
...
F7
<beep>
*</code>
    </div>
    <p>This is repeatable; the original disk always ends up with $F7 in zero page $1E. If my theory is correct (that this is part of the copy protection), then running the same boot trace on my non-working copy should yield a different result.</p>
    <div class="curses_input">
      <code>[S5D1=my work disk]
[S6D1=Crypto Cube non-working copy]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
]BRUN TRACE2
...
00
<beep>
*</code>
    </div>
    <p>This is also repeatable. Copies (even the bit copy I made with EDD 4) end up with $00 in zero page $1E.</p>
    <p>Now I'm wondering if I can simply fake this data by ignoring the value of $1E and hard-coding the expected value. I suppose there's only one way to find out. Using my trusty Copy ][+ sector editor, I found the $BB00 routine in sector 3 of track $00.</p>
    <p>(Why are all these sectors out of order? Sector $0F got loaded into $B900. The answer is interesting. If you look closely at the boot0 routine in sector 0 that loads the RWTS from disk, you'll see that the lookup table that maps logical sectors to physical sectors is in a non-standard order. It didn't affect the boot tracing in any way, because I just let the disk load its own RWTS and interrupt it later.</p>
    <p>But going in with a sector editor, I have to account for the fact that the RWTS is not stored sequentially in logical sectors.</p>
    <p>OK, maybe that wasn't so interesting.</p>
    <p>I don't have an answer to why the original developers did it this way. Just for obfuscation? To load faster? I'm pretty sure it's actually slower. The whole thing is just weird.)</p>
    <p>Continuing with my hunch, I made one small change after the disk reading but before the bit counting. It just ignores the value in zero page $1E and always uses $F7 instead.</p>
    <p>T00,S03,$64 change "A5 1E" to "A9 F7"</p>
    <p>Success! The game boots and no longer displays an error message. The game does more disk reads as you progress, but none of them give me any error message. There doesn't seem to be any further copy protection.</p>
    <p class="seen">Quod erat liberandum.</p>
</section>

</body></html>
