<!doctype html>
<html><head>
<title>Reading and Me</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
<meta name="viewport" content="width=420, initial-scale=1.0, user-scalable=no">
</head><body>

<p>Previous: <a rel="previous" href="./455-math-for-everyday-living.html">Math For Everyday Living</a></p>
<section>

    <p class="seen">Only side A is bootable, so I'll start there.</p>
</section>

<section>
  <h2>Chapter 0</h2>
  <h3>In Which Various Automated Tools Fail In Interesting Ways</h3>

    <p class="seen">COPYA</p>
    <p class="seen">disk read error on last pass</p>
    <p class="seen">Locksmith Fast Disk Backup</p>
    <p class="seen">read error on T22,S00; copy boots ProDOS then quits to program selector</p>
    <p class="seen">EDD 4 bit copy (no sync, no count)</p>
    <p class="seen">works</p>
    <p class="seen">Copy ][+ nibble editor</p>
    <p class="seen">it appears that the mystery sector on track $22 is simply missing (the track is shorter than usual)</p>
    <p class="seen">Disk Fixer</p>
    <p class="seen">no way to read T22,S00</p>
    <p class="seen">Why didn't any of my copies work?</p>
    <p class="seen">Probably a nibble check in the first SYSTEM file that reads the unreadable sector on track $22</p>
</section>

<section>
  <h2>Chapter 1</h2>
  <h3>Success Is Failure, Failure Is Success, Black Is White, Night Is Day, Teaching Is Dead</h3>

    <div class="curses_input">
      <code>[S6,D1=original disk, side A]
[S7,D1=ProDOS hard drive]</code>
    </div>
    <div class="line_editing">
      <code>]PR#7
...
]CAT,S6,D1</code>
      <code>/READ.AND.ME</code>
    </div>
    <div class="terminal_output">
      <code> NAME           TYPE  BLOCKS  MODIFIED</code>
      <code> PRODOS          SYS      32  17-APR-87
 LOADER.SYSTEM   SYS       4   7-APR-88
 AFORTH          BIN      67  19-MAY-88
 TK.ABS          BIN      22  18-JUN-87
 NEW.FONT        BIN       7  23-MAR-88
 DUMP.HIRES.R    BIN       5   7-MAY-87
 PRINTER.DRIVERS TXT      12  18-MAY-87
 INTER.DRIVERS   TXT       5   4-AUG-87
 PRINTER.DATA    TXT       1  31-MAR-88
 TEXT.LIST       TXT       7  19-MAY-88
 LARGE.FONT      TXT      14  23-MAR-88
 CTEXT.LIST      TXT       1  19-MAY-88
 ENTRY.PAK       TXT       7   9-MAR-88
 CERT.PAK        TXT      11   9-MAR-88
 SOUND.ICON      TXT      11  17-AUG-87
 CERTIF.OVL      TXT       7  19-MAY-88</code>
      <code>BLOCKS FREE:   52     BLOCKS USED:  228</code>
    </div>
    <p class="seen">ProDOS always loads the first .SYSTEM file at $2000 and jumps to it.</p>
    <div class="line_editing">
      <code>]PREFIX /READ.AND.ME
]BLOAD LOADER.SYSTEM,A$2000,TSYS
]CALL -151</code>
      <code>*2000L</code>
    </div>
    <div class="listing">
      <code>2000-   20 D2 25    JSR   $25D2
2003-   4C 74 08    JMP   $0874</code>
    </div>
    <p class="seen">There's nothing loaded at $0874 yet, so the subroutine at $25D2 must load something from disk or copy something from memory.</p>
    <div class="line_editing">
      <code>*25D2L</code>
    </div>
    <div class="listing">
      <p class="comment">straightforward memory relocation</p>
      <code>25D2-   A2 00       LDX   #$00
25D4-   BD 03 20    LDA   $2003,X
25D7-   9D 01 08    STA   $0801,X
25DA-   BD 03 21    LDA   $2103,X
25DD-   9D 01 09    STA   $0901,X
25E0-   BD 03 22    LDA   $2203,X
25E3-   9D 01 0A    STA   $0A01,X
25E6-   BD 03 23    LDA   $2303,X
25E9-   9D 01 0B    STA   $0B01,X
25EC-   BD 03 24    LDA   $2403,X
25EF-   9D 01 0C    STA   $0C01,X
25F2-   BD 03 25    LDA   $2503,X
25F5-   E8          INX
25F6-   D0 DC       BNE   $25D4
25F8-   60          RTS</code>
    </div>
    <p class="seen">That's harmless enough. Let's run it.</p>
    <div class="line_editing">
      <code>*25D2G</code>
      <code>*874L</code>
    </div>
    <div class="listing">
      <code>0874-   20 C8 0A    JSR   $0AC8
0877-   F0 D9       BEQ   $0852</code>
    </div>
    <p class="seen">Down the rabbit hole we go.</p>
    <div class="line_editing">
      <code>*AC8L</code>
    </div>
    <div class="listing">
      <p class="comment">$BF30 is the last used slot and drive in "DSSS0000" format ("Beneath Apple ProDOS", p. 8-6)</p>
      <code>0AC8-   AD 30 BF    LDA   $BF30
0ACB-   4A          LSR
0ACC-   4A          LSR
0ACD-   4A          LSR
0ACE-   4A          LSR
0ACF-   29 07       AND   #$07
0AD1-   09 C0       ORA   #$C0</code>
      <p class="comment">slot number munged into the high byte of the drive slot ROM, e.g. $C6</p>
      <code>0AD3-   85 61       STA   $61
0AD5-   A9 00       LDA   #$00
0AD7-   85 60       STA   $60</code>
      <p class="comment">standard fingerprinting to identify the drive ROM</p>
      <code>0AD9-   A0 01       LDY   #$01
0ADB-   B1 60       LDA   ($60),Y
0ADD-   C9 20       CMP   #$20
0ADF-   D0 22       BNE   $0B03
0AE1-   A0 03       LDY   #$03
0AE3-   B1 60       LDA   ($60),Y
0AE5-   D0 1C       BNE   $0B03
0AE7-   A0 05       LDY   #$05
0AE9-   B1 60       LDA   ($60),Y
0AEB-   C9 03       CMP   #$03
0AED-   D0 14       BNE   $0B03
0AEF-   A0 FF       LDY   #$FF
0AF1-   B1 60       LDA   ($60),Y
0AF3-   D0 0E       BNE   $0B03</code>
      <p class="comment">5.25-inch floppy drive, so check block $110 (includes T22,S00)</p>
      <code>0AF5-   A9 10       LDA   #$10
0AF7-   8D CE 0D    STA   $0DCE
0AFA-   A9 01       LDA   #$01
0AFC-   8D CF 0D    STA   $0DCF
0AFF-   D0 0C       BNE   $0B0D
0B01-   F0 0A       BEQ   $0B0D</code>
      <p class="comment">3.5-inch floppy drive, so check block $308</p>
      <code>0B03-   A9 08       LDA   #$08
0B05-   8D CE 0D    STA   $0DCE
0B08-   A9 03       LDA   #$03
0B0A-   8D CF 0D    STA   $0DCF</code>
      <p class="comment">set up other MLI parameters</p>
      <code>0B0D-   AD 30 BF    LDA   $BF30
0B10-   8D CB 0D    STA   $0DCB
0B13-   A9 03       LDA   #$03
0B15-   8D CA 0D    STA   $0DCA
0B18-   A9 CA       LDA   #$CA
0B1A-   8D CC 0D    STA   $0DCC
0B1D-   A9 0B       LDA   #$0B
0B1F-   8D CD 0D    STA   $0DCD</code>
      <p class="comment">call MLI to read the specified block from disk (the $80 byte following the JSR is the READ_BLOCK command, and following that is the address of the MLI parameter table, $0DCA)</p>
      <code>0B22-   20 00 BF    JSR   $BF00
0B25-  [80 CA 0D]</code>
      <p class="comment">if there was NOT a disk read error, then branch</p>
      <code>0B28-   90 09       BCC   $0B33</code>
      <p class="comment">if there was any error other than an</p>
      <p class="comment">I/O error ($27), then branch</p>
      <code>0B2A-   C9 27       CMP   #$27
0B2C-   D0 05       BNE   $0B33</code>
      <p class="comment">success path is here</p>
      <code>0B2E-   A9 FF       LDA   #$FF
0B30-   85 FF       STA   $FF
0B32-   60          RTS</code>
      <p class="comment">failure path is here</p>
      <code>0B33-   A9 00       LDA   #$00
0B35-   85 FF       STA   $FF
0B37-   60          RTS</code>
    </div>
    <p class="seen">This is definitely a form of copy protection. It relies on a specific sector being unreadable, and fails if it's actually readable. Fun fact(*): it is written so it can be re-used for</p>
    <p class="seen">5.25- and 3.5-inch floppies. Since</p>
    <p class="seen">Locksmith FDB just wrote a zeroed (but standard) sector to T22,S00, the MLI read routine unexpectedly succeeds, and the program knows it's been copied.</p>
    <p class="seen">(*) not guaranteed, actual fun may vary</p>
    <p class="seen">Continuing from $0874...</p>
    <div class="listing">
      <code>0877-   F0 D9       BEQ   $0852</code>
    </div>
    <div class="line_editing">
      <code>*852L</code>
    </div>
    <div class="listing">
      <p class="comment">read and write main memory</p>
      <code>0852-   8E 04 C0    STX   $C004
0855-   8E 02 C0    STX   $C002</code>
      <p class="comment">call the quit handler (command $65)</p>
      <code>0858-   20 00 BF    JSR   $BF00
085B-  [65 5E 08]</code>
    </div>
    <p class="seen">...which explains the behavior I saw on my non-working copy.</p>
    <p class="seen">Let's reproduce the success path. I'm not sure if the side effect of setting zero page $FF is relevant, but let's assume it is.</p>
    <div class="curses_input">
      <code>[S6,D1=non-working copy]</code>
    </div>
    <div class="line_editing">
      <code>*FF:FF N 879G</code>
    </div>
    <p class="seen">The program loads without complaint.</p>
    <p class="seen">Out of curiosity, I reproduced this state and tried it with $FF=0.</p>
    <div class="line_editing">
      <code>*FF:00 N 879G</code>
    </div>
    <p class="seen">It still loads without complaint. The only thing that really matters is the value of the accumulator on the way out of the routine.</p>
    <p class="seen">Backtracking all the way back to $2003,</p>
    <p class="seen">I can change "JMP $0874" to "JMP $0879" and bypass the entire thing.</p>
    <p>T04,S0A,$04 change "74" to "79"</p>
    <p class="seen">Side B is unprotected.</p>
    <p class="seen">Quod erat liberandum.</p>
</section>

<p>Next: <a rel="next" href="./457-snoopy-to-the-rescue.html">Snoopy to the Rescue</a></p>
</body></html>
