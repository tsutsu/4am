<!doctype html>
<html><head>
<title>Cotton Tales</title>
<link rel="Stylesheet" type="text/css" href="../site.css">
<meta name="viewport" content="width=420, initial-scale=1.0, user-scalable=no">
</head><body>

<p>Previous: <a rel="previous" href="./264-easy-street.html">Easy Street</a></p>
<section>
  <h2>Chapter 0</h2>
  <h3>In Which Various Automated Tools Fail In Interesting Ways</h3>

    <p class="seen">COPYA</p>
    <p class="seen">reads several passes then fails with a disk read error</p>
    <p class="seen">Locksmith Fast Disk Backup</p>
    <p>reads every track except $14; copy hangs on boot</p>
    <p class="seen">EDD 4 bit copy (no sync, no count)</p>
    <p>no errors, but copy prints an error "THIS IS A COPYRIGHTED DISK. IT IS EITHER PIRATED OR DAMAGED" and hangs</p>
    <p class="seen">Copy ][+ nibble editor</p>
    <p>T14 is mostly self-sync bytes ($FF), no sector structure</p>
    <p class="seen">Disk Fixer</p>
    <p>unable to read track $14 with any combination of parameters</p>
    <p class="seen">Why didn't COPYA work?</p>
    <p>track $14 is intentionally unreadable</p>
    <p class="seen">Why didn't Locksmith FDB work?</p>
    <p>probably stuck in an infinite loop trying to read the unreadable T14</p>
    <p class="seen">Why didn't my EDD copy work?</p>
    <p class="seen">definitely a nibble check during boot</p>
    <p class="seen">Next steps:</p>
    <p class="seen">1. Boot trace</p>
    <p class="seen">2. Find nibble check and disable it</p>
    <p class="seen">3. There is no step 3 (I hope)</p>
</section>

<section>
  <h2>Chapter 1</h2>
  <h3>In Which We Quickly Find Ourselves In Unfamiliar Territory</h3>

    <div class="curses_input">
      <code>[S6,D1=original disk]
[S5,D1=my work disk]</code>
    </div>
    <div class="line_editing">
      <code>]PR#5
...
CAPTURING BOOT0
...reboots slot 6...
...reboots slot 5...
SAVING BOOT0
CAPTURING BOOT1
...reboots slot 6...
...reboots slot 5...
SAVING BOOT1
SAVING RWTS</code>
      <code>]BLOAD BOOT1,A$2600
]CALL -151
*FE89G FE93G     ; disconnect DOS
*B600<2600.2FFFM ; move RWTS into place
*B700L</code>
    </div>
    <div class="listing">
      <p class="comment">right off the bat, I'm lost -- this bootloader is not at all what I was expecting</p>
      <code>B700-   20 06 B7    JSR   $B706
B703-   4C 00 B5    JMP   $B500</code>
    </div>
    <p class="seen">$B500 isn't loaded yet (boot0 loads boot1 into $B600..$BFFF), so it must be the next stage of the boot which is loaded by the subroutine at $B706.</p>
    <div class="listing">
      <code>B706-   AE 27 B7    LDX   $B727
B709-   CA          DEX
B70A-   8A          TXA
B70B-   18          CLC
B70C-   6D 2D B7    ADC   $B72D
B70F-   C9 10       CMP   #$10
B711-   30 08       BMI   $B71B
B713-   EE 2C B7    INC   $B72C
B716-   38          SEC
B717-   E9 10       SBC   #$10
B719-   10 F4       BPL   $B70F
B71B-   8D 2D B7    STA   $B72D
B71E-   AE 27 B7    LDX   $B727
B721-   CA          DEX
B722-   8A          TXA
B723-   18          CLC
B724-   4C 3D B7    JMP   $B73D</code>
    </div>
    <p class="seen">This code didn't make a lot of sense to me until I looked at the bytes starting at $B727:</p>
    <div class="listing">
      <code>B727- 02
B728- 01 60 01 00 01 00 37 B7
B730- 00 B5 00 00 01 00 00 60
B738- 01 00 01 EF D8</code>
    </div>
    <p class="seen">Aha! That's an RWTS parameter table (starting at $B728), and this is all a multi-sector read loop.</p>
    <div class="listing">
      <code>B73D-   6D 31 B7    ADC   $B731
B740-   8D 31 B7    STA   $B731
B743-   78          SEI
B744-   A0 28       LDY   #$28
B746-   A9 B7       LDA   #$B7
B748-   20 00 BD    JSR   $BD00
B74B-   AE 29 B7    LDX   $B729
B74E-   90 63       BCC   $B7B3</code>
      <p class="comment">check RWTS command ($02 = write)</p>
      <code>B750-   AD 34 B7    LDA   $B734
B753-   C9 02       CMP   #$02</code>
      <p class="comment">read ($01) and seek ($00) will branch</p>
      <code>B755-   D0 42       BNE   $B799
...
B799-   58          CLI</code>
      <p class="comment">turn off drive motor (this is usually taken care of within the RWTS, but maybe this RWTS doesn't?)</p>
      <code>B79A-   BD 88 C0    LDA   $C088,X
B79D-   CE 2D B7    DEC   $B72D
B7A0-   10 08       BPL   $B7AA
B7A2-   CE 2C B7    DEC   $B72C
B7A5-   A9 0F       LDA   #$0F
B7A7-   8D 2D B7    STA   $B72D
B7AA-   CE 31 B7    DEC   $B731
B7AD-   CE 27 B7    DEC   $B727
B7B0-   D0 91       BNE   $B743
B7B2-   60          RTS
B7B3-   A9 00       LDA   #$00
B7B5-   8D 35 B7    STA   $B735
B7B8-   4C 99 B7    JMP   $B799</code>
    </div>
    <p class="seen">There doesn't appear to be any funny business. It's a bit convoluted, but it just loads sectors from a given track until $B727 hits 0.</p>
    <p class="seen">Let's see what's at $B500.</p>
    <div class="line_editing">
      <code>*9600<C600.C6FFM</code>
    </div>
    <div class="listing">
      <p class="comment">set up callback #1 after boot0</p>
      <code>96F8-   A9 4C       LDA   #$4C
96FA-   8D 4A 08    STA   $084A
96FD-   A9 0A       LDA   #$0A
96FF-   8D 4B 08    STA   $084B
9702-   A9 97       LDA   #$97
9704-   8D 4C 08    STA   $084C</code>
      <p class="comment">start the boot</p>
      <code>9707-   4C 01 08    JMP   $0801</code>
      <p class="comment">(callback #1) set up callback #2 instead of jumping to $B500</p>
      <code>970A-   A9 4C       LDA   #$4C
970C-   8D 03 B7    STA   $B703
970F-   A9 1C       LDA   #$1C
9711-   8D 04 B7    STA   $B704
9714-   A9 97       LDA   #$97
9716-   8D 05 B7    STA   $B705</code>
      <p class="comment">continue the boot</p>
      <code>9719-   4C 00 B7    JMP   $B700</code>
      <p class="comment">copy the two sectors we read to lower memory so it will survive a reboot</p>
      <code>971C-   A0 00       LDY   #$00
971E-   B9 00 B5    LDA   $B500,Y
9721-   99 00 25    STA   $2500,Y
9724-   B9 00 B6    LDA   $B600,Y
9727-   99 00 26    STA   $2600,Y
972A-   C8          INY
972B-   D0 F1       BNE   $971E</code>
      <p class="comment">reboot to my work disk</p>
      <code>972D-   4C 00 C5    JMP   $C500</code>
    </div>
    <div class="line_editing">
      <code>*BSAVE TRACE,A$9600,L$130
*9600G
...reboots slot 6...
...reboots slot 5...</code>
      <code>]BSAVE BOOT1 B500-B6FF,A$2500,L$200</code>
    </div>
</section>

<section>
  <h2>Chapter 2</h2>
  <h3>In Which We Find Ourselves Behind Enemy Lines</h3>

    <div class="line_editing">
      <code>]CALL -151
*B500<2500.26FFM
*B500L</code>
    </div>
    <div class="listing">
      <code>B500-   20 00 B6    JSR   $B600</code>
    </div>
    <div class="line_editing">
      <code>*B600L</code>
    </div>
    <div class="listing">
      <code>B600-   A2 60       LDX   #$60
B602-   A9 01       LDA   #$01</code>
      <p class="comment">set RWTS parameters (starts at $B728)</p>
      <code>B604-   8D 27 B7    STA   $B727</code>
      <p class="comment">track $14</p>
      <code>B607-   A9 14       LDA   #$14
B609-   8D 2C B7    STA   $B72C</code>
      <p class="comment">sector $00</p>
      <code>B60C-   A9 00       LDA   #$00
B60E-   8D 2D B7    STA   $B72D</code>
      <p class="comment">seek only, no read</p>
      <code>B611-   8D 34 B7    STA   $B734</code>
      <p class="comment">disable last line of RWTS that turns off the drive motor</p>
      <code>B614-   A9 60       LDA   #$60
B616-   8D 4D BE    STA   $BE4D
B619-   58          CLI</code>
      <p class="comment">execute RWTS seek</p>
      <code>B61A-   20 06 B7    JSR   $B706</code>
      <p class="comment">restore RWTS code</p>
      <code>B61D-   A9 BD       LDA   #$BD
B61F-   8D 4D BE    STA   $BE4D
B622-   B0 65       BCS   $B689</code>
      <p class="comment">look for a magic bitstream</p>
      <code>B624-   BD 8C C0    LDA   $C08C,X
B627-   10 FB       BPL   $B624
B629-   48          PHA
B62A-   68          PLA
B62B-   C9 D5       CMP   #$D5
B62D-   D0 F5       BNE   $B624
B62F-   A0 00       LDY   #$00
B631-   8C BA B6    STY   $B6BA
B634-   BD 8C C0    LDA   $C08C,X
B637-   10 FB       BPL   $B634
B639-   C9 D5       CMP   #$D5</code>
      <p class="comment">branch = success</p>
      <code>B63B-   F0 0F       BEQ   $B64C
B63D-   C9 F7       CMP   #$F7
B63F-   D0 01       BNE   $B642
B641-   C8          INY
B642-   18          CLC
B643-   6D BA B6    ADC   $B6BA
B646-   8D BA B6    STA   $B6BA
B649-   4C 34 B6    JMP   $B634</code>
      <p class="comment">success path continues here</p>
      <code>B64C-   98          TYA
B64D-   F0 E0       BEQ   $B62F
B64F-   BD 8C C0    LDA   $C08C,X
B652-   10 FB       BPL   $B64F
B654-   48          PHA
B655-   68          PLA
B656-   C9 FF       CMP   #$FF
B658-   F0 F5       BEQ   $B64F
B65A-   C9 D5       CMP   #$D5</code>
      <p class="comment">branch = failure</p>
      <code>B65C-   F0 2F       BEQ   $B68D
B65E-   A0 05       LDY   #$05
B660-   BD 8C C0    LDA   $C08C,X
B663-   10 FB       BPL   $B660
B665-   48          PHA
B666-   68          PLA
B667-   88          DEY
B668-   D0 F6       BNE   $B660
B66A-   BD 8C C0    LDA   $C08C,X
B66D-   10 FB       BPL   $B66A
B66F-   48          PHA
B670-   68          PLA
B671-   C9 FF       CMP   #$FF
B673-   F0 F5       BEQ   $B66A
B675-   C9 D5       CMP   #$D5</code>
      <p class="comment">branch = failure</p>
      <code>B677-   D0 14       BNE   $B68D
B679-   BD 8C C0    LDA   $C08C,X
B67C-   10 FB       BPL   $B679
B67E-   C9 FF       CMP   #$FF</code>
      <p class="comment">branch = failure</p>
      <code>B680-   D0 0B       BNE   $B68D
B682-   AD BA B6    LDA   $B6BA
B685-   C9 10       CMP   #$10</code>
      <p class="comment">branch = failure</p>
      <code>B687-   D0 04       BNE   $B68D</code>
      <p class="comment">success falls through to here -- turn off drive motor and exit (no flags)</p>
      <code>B689-   BD 88 C0    LDA   $C088,X
B68C-   60          RTS</code>
      <p class="comment">failure path is here</p>
      <code>B68D-   A2 20       LDX   #$20
B68F-   A9 A0       LDA   #$A0
B691-   99 FF 1F    STA   $1FFF,Y
B694-   C8          INY
B695-   D0 FA       BNE   $B691
B697-   EE 93 B6    INC   $B693
B69A-   CA          DEX
B69B-   D0 F4       BNE   $B691</code>
      <p class="comment">display error message (I saw this on my failed EDD bit copy)</p>
      <code>B69D-   A0 19       LDY   #$19
B69F-   B9 BB B6    LDA   $B6BB,Y
B6A2-   49 BB       EOR   #$BB
B6A4-   99 08 07    STA   $0708,Y
B6A7-   88          DEY
B6A8-   10 F5       BPL   $B69F
B6AA-   A0 1E       LDY   #$1E
B6AC-   B9 D5 B6    LDA   $B6D5,Y
B6AF-   49 BB       EOR   #$BB
B6B1-   99 2E 04    STA   $042E,Y
B6B4-   88          DEY
B6B5-   10 F5       BPL   $B6AC</code>
      <p class="comment">loop forever</p>
      <code>B6B7-   78          SEI
B6B8-   30 FE       BMI   $B6B8</code>
    </div>
    <p class="seen">The nibble check at $B600 has no side effects. On success, it exits without setting or clearing any flags; on failure, it never returns. Replacing the first instruction at $B600 with an "RTS" will bypass it.</p>
    <p class="seen">T01,S01,$00 change "A2" to "60"</p>
    <p class="seen">Quod erat liberandum.</p>
</section>

<p>Next: <a rel="next" href="./266-dyno-quest.html">Dyno-Quest</a></p>
</body></html>
